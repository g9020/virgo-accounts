;Routine Save - 04:15 PM  Mar 3, 2016 (DTM-PC/4.3M)
;Saved From USER: source code
AAIN
AAIN	;GRI,,,;01:31 PM  4 Apr 2002;
	;=======================================================
	; Virgo Accounts - Import Time Details
	;                  from Amicus Attorney or AuxTime
	;
	; Copyright Graham R Irwin 1999-2002
	;
	; Screens:       1072
	; Files updated: ^SW ^TE ^WORK($j) ^AB ^AY Time file deleted
	; Subroutines:   X0^XSXS, DMY2H^DATE, ^INPUT, ^OUTPUT,
	;                YX^XSXS, UPAB^XSXS, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	S TT="TS",D5="Time sheet",p2=$c(9),T0=0

	S %S=1072,%J=1 D ^INPUT I X[% Q
	S FILE=$g(^SZ("AAD"))_FN
	zetrap ERR^AAIN
	O 11:(file=FILE:mode="r")

	;Open report
	U 0 D ^OUTPUT Q:%OP[%  S N=99,P=1 D HEAD^XSXS
	W "F/e   Date      Cli/Matter Bill Activity Time",!! S N=3

	;Get next record
U20	U 11 R WK I WK="" G U99
	;fee earner, date, client no, matter no, billable ind., charge gp/activity, time
	S FX=$p(WK,p2,1),A0=$p(WK,p2,2),CX=$p(WK,p2,3),MX=$p(WK,p2,4),BILL=$p(WK,p2,5),A1=$p(WK,p2,7),ACT=$p(WK,p2,6),RX=$p(ACT,"/",1),W3=$p(ACT,"/",2)

	;Ignore if not billable
	I $zconvert(BILL,"U")'="TRUE" S ERX=0 G SKIP

	;Validate
	I CX="" S ERX=1 G SKIP
	S CX=$zconvert(CX,"U")
	I '$d(^SC(CX)) S ERX=1 G SKIP
	I MX="" S ERX=2 G SKIP
	I '$d(^SM(CX,MX)) S ERX=2 G SKIP
	I RX=""!(W3="") S ERX=3 G SKIP
	S RX=$zconvert(RX,"U"),W3=$zconvert(W3,"U")
	I '$d(^SR(RX,W3)) S ERX=3 G SKIP
	I FX="" S ERX=4 G SKIP
	I '$d(^SF(FX)) S ERX=4 G SKIP
	I A0'?6N S ERX=5 G SKIP
	;reformat date
	S Y4=1900+$e(A0,5,6) S:Y4<1970 Y4=Y4+100 S A0=$e(A0,1,2)_"/"_$e(A0,3,4)_"/"_Y4
	;convert time (h.hh) to minutes

	;Update time records
	S SR=^SR(RX,W3),S1=$P(SR,%,1),S2=$P(SR,%,2)
	S W4=A1*60,W5=0 I S2>0 S W5=A1*10,W4=0
	S SW=$G(^SW(CX,MX,FX,W3)),T1=$P(SW,%,1),T2=$P(SW,%,2),T3=$P(SW,%,3)
	S T9=$J(W4*S1/60+(W5*S2),0,2),T1=T1+W4,T2=T2+W5,T3=T3+T9,T0=T0+T9
	S ^SW(CX,MX,FX,W3)=T1_%_T2_%_T3_%
	S SW=$G(^SW(CX,MX)),W1=$P(SW,%,1)+T9,(W2,%DAT)=$P(SW,%,2) D DMY2H^DATE
	S AY=%H,%DAT=A0 D DMY2H^DATE S:%H>AY W2=A0 S ^SW(CX,MX)=W1_%_W2_%
	;detailed time?
	I $g(^SZ("DTE"))="Y" S IX=$g(^TE)+1,^TE=IX,^TE(CX,MX,FX,IX)=A0_%_W3_%_W4_%_W5_%_T9

	;Write to report
	U %OP W ?1,FX,?4,A0,?16,CX,"/",MX,?28,$e(BILL,1),?32,ACT,?41,A1,?46,"- Posted",!! S N=N+2 I N>56 D HEAD^XSXS
	G U20

	;Skip entry
SKIP	U %OP W ?1,FX,?4,A0,?16,CX,"/",MX,?28,$e(BILL,1),?32,ACT,?41,A1
	W ?46,"- Skipped, NOT posted",!?46,"- "
	I ERX W "Invalid ",$p("client:matter:chg-grp/activity:fee earner:date",":",ERX)
	I ERX=0 W "Non-billable"
	W !! S N=N+3 I N>56 D HEAD^XSXS
	G U20

	;Finished...
	;...close and delete file
U99	C 11 D del^%dos(FILE)
	;...close report
	D END^XSXS
	;...update nominal ledger
	D YX^XSXS
	D UPAB^XSXS("N020",T0,DAT,TT,"",D5)
	D UPAB^XSXS("N030",T0,DAT,TT,"",D5)
	Q

	;Special error trapping routine
ERR	i $zerr=219!($zerr=203) u 0 w *7,!,"No such file or file in use - " s x=$$^Cont(0) G AAIN
	zquit

	;Validation - File name
V01	I $d(ERR) Q
	I X'?1.8ANP1"."1.3AN S ERR="Not a valid file name"
	Q

AAOUT
AAOUT	;gri,,,;09:26 AM  4 Apr 2002;
	;=======================================================
	; Virgo Accounts - Export Files and Fee Rates
	;
	; Copyright Graham R Irwin 1999-2002
	;
	; Screens:       none
	; Files updated: DOS files (see documentation)
	; Subroutines:   X0^XSXS
	;
	;=======================================================

	D X0^XSXS
	S p2=$c(9)
	D SM1,SR1
	Q

	;Client and matter details
SM1	U 0 W /c(0,23),@P1,"Exporting client and matter details",/el
	S (CX,MX)="",FN="files.txt" D OPEN
SM2	S CX=$o(^SM(CX)) I CX="" C 10 Q
SM3	S MX=$o(^SM(CX,MX)) I MX="" G SM2
	S SMA=^SM(CX,MX),M1=$g(^SM(CX,MX,"O")),M2=$g(^SM(CX,MX,"C"))
	S SCA=^SC(CX),SCA1=$p(SCA,%,1),SCA2=$p(SCA,%,2),SCA3=$p(SCA,%,3)
	S SCB=$tr(^SC(CX,"A"),%," "),SCC=$tr(^SC(CX,"N"),%," ")
	;client id, matter id, client name, matter desc, open date, close date, client name (again), address, phone, fax, notes
	W CX,p2,MX,p2,SCA1,p2,$p(SMA,%,1),p2,M1,p2,M2,p2,SCA1,p2,SCB,p2,SCA2,p2,SCA3,p2,SCC,!
	G SM3

	;Fee earners
SF1	U 0 W /c(0,23),@P1,"Exporting fee earners",/el
	S FX="",FN="earners.txt" D OPEN
SF2	S FX=$o(^SF(FX)) I FX="" C 10 Q
	W FX,p2,^SF(FX),!
	G SF2

	;Fee rates
SR1	S (RX,X2)="",FN="feerates.txt" D OPEN
SR2	S RX=$o(^SR(RX)) I RX="" C 10 Q
SR3	S X2=$o(^SR(RX,X2)) I X2="" G SR2
	;activity code, activity descr, rate per hour, rate per item
	W RX,"/",X2,p2,RX," ",^SA(X2),p2,$p(^SR(RX,X2),%,1),p2,$p(^SR(RX,X2),%,2),!
	G SR3

OPEN	O 10:(file=FN:mode="w") U 10 Q

AASET
AASET	;GRI,,,;09:26 AM  4 Apr 2002;
	;=======================================================
	; Virgo Accounts - Setup for Time Import
	;
	; Copyright Graham R Irwin 1999-2002
	;
	; Screens:       1074
	; Files updated: ^SZ
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	D X0^XSXS
	S D0=$g(^SZ("AAD"))

	S %S=1074 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 AASET Q

E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE Q:X="Y"  G E02
	I X="U" G U01
	I X="A" G A01
	G E02

	;Update
U01	S ^SZ("AAD")=D0
	Q

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G AASET:%J=1,A01
	G E02

	;Validation - directory name
V01	I $d(ERR) Q
	D CASE
	I X'="A:\",X'?1A1":\"1.16ANP S ERR="Not a valid directory name" Q
	I $e(X,$l(X))'="\" S ERR="Must end in \"
	Q

CASE	S X=$zconvert(X,"U") Q

AATIME
AATIME	;GRI,,,;08:25 AM  17 Apr 2002;
	;=======================================================
	; Virgo Accounts - Input Time Sheet to DOS file
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1022, 1045
	; Files updated: ^WORK($j)
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	D X0^XSXS
	S ONE=1
	S %S=1022 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% Q:%J=1  G AATIME

	W @P1,!?8,"Client Matter Activity",?52,"Units",?60,"Items",!!
	S WS=1 K ^WORK($j)
I01	W /C(0,16),/EF S WX=WS,F1=1,(A4,A5)=""
	S %S=1045 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G I01:%J>1 S WS=WX,A1="",F1=0 G N01
E09	W /C(0,22),@P1,"Update, Amend, Delete or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) S A1="" G N01
	I X="A" G A01
	I X="U" G U01
	I X="D" W @P1,"  Are you sure ? ",@P2 D ^GETX,CASE I X="Y" K ^WORK($j,WX) S A1="" G N01
	G E09

	;Update individual time entry
U01	S ^WORK($j,WX)=CX_%_MX_%_A3_%_A4_%_A5_%_RX,WS=WS+1
	W /C(0,9),/EF,@P1 S A1=WX-6
U03	S A1=$O(^WORK($j,A1)) I A1=""!($Y>20) G I01:F1,L01
U04	S WK=^WORK($j,A1),CX=$P(WK,%,1),MX=$P(WK,%,2),A3=$P(WK,%,3),A4=$P(WK,%,4),A5=$P(WK,%,5),B1=$P($G(^SA(A3)),%,1)
	W @P1,A1,?9,CX,?16,MX,?22,A3,?25,B1,?53,A4,?61,A5,!
	G U03

L01	W /C(0,22),@P1,"Next, Select, Insert, Update or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Forget entire batch ? ",@P2 D ^GETX,CASE G AATIME:X="Y",L01
	I X="N" G N01
	I X="S"!(X="Z") G S01
	I X="I" G I01
	I X="U" G U11
	G L01

	;Update time records
U11	S WX="",p2=$c(9),D0=$g(^SZ("AAD"))
	S FN=D0_"0-"_$e(A0,4,5)_$e(A0,1,2)_$e(A0,9,10)_"."_FX
	O 10:(file=FN:mode="a") U 10
U20	S WX=$O(^WORK($j,WX)) I WX="" G U99
	S WK=^WORK($j,WX),CX=$P(WK,%,1),MX=$P(WK,%,2),W3=$P(WK,%,3),W4=$P(WK,%,4),W5=$P(WK,%,5),RX=$p(WK,%,6)
	S T0=W4/10 I W5>0 S T0=W5/10
	W FX,p2,$e(A0,1,2),$e(A0,4,5),$e(A0,9,10),p2,CX,p2,MX,p2,"True",p2,RX,"/",W3,p2,T0,!
	G U20

U99	C 10
	G AATIME

	;Next
N01	W /C(0,9),/EF,@P1 G U04:A1'="",U03

	;Amend
A01	W /C(0,16),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G N01:%J=1,A01
	G E09

	;Select
S01	W @P1,"  Select item ",@P2 D ^GETX S WX=+X I $G(^WORK($j,WX))="" G L01
	S WK=^WORK($j,WX),CX=$P(WK,%,1),MX=$P(WK,%,2),A3=$P(WK,%,3),A4=$P(WK,%,4),A5=$P(WK,%,5),RX=$p(WK,%,6)
	W /C(0,16),/EF F %J=1:1 D D^INPUT Q:X="END"!(X[%)
	G E09

	;Validation ----------------------------------------
	;Matter code
V01	Q:$D(ERR)  S SM=^SM(CX,X),M7=$P(SM,%,7),M7A=$p(M7,"/",1),M7B=$p(M7,"/",2)
	Q
	;Activity
V02	Q:$D(ERR)  I '$D(^SR(RX,X)) S ERR="Not defined for this charge group" Q
	S SR=^SR(RX,X),S1=$P(SR,%,1),S2=$P(SR,%,2),SA=^SA(X)
	Q
	;No of units
V03	S A5=""
	Q
	;No of items
V04	S A4=""
	Q

CASE	S X=$zconvert(X,"U") Q

BioRhythm
BioRhythm	;GRI;12:44 PM  30 Jan 2000;
	w #,@P1 f j=1:1 d write q:u="END"
	w !?8,"Please enter your date of birth",!
	s D1="" i $d(^BioR) s D1=^BioR
	s %S="g1",%J=1 d ^INPUT i X[% q
	s B1=+$H,%DAT=C1 d DMY2H^DATE s B2=%H
	s B0=B1-B2,PH=B0#23,EM=B0#28,IN=B0#33
	w !?8,@P1,"It is ",B0," days since your birth (sorry!). You are on :"
	w !?8,"day ",PH," of your physical cycle, which is a "
	s x="HIGH" s:(PH=6!(PH=17)) x="CRITICAL" s:(PH<6!(PH>17)) x="LOW" d x
	w !?8,"day ",EM," of your emotional cycle, which is a "
	s x="HIGH" s:(EM=7!(EM=21)) x="CRITICAL" s:(EM<7!(EM>21)) x="LOW" d x
	w !?8,"and day ",IN," of your intellectual cycle, which is a "
	s x="HIGH" s:(IN=8!(IN=25)) x="CRITICAL" s:(IN<8!(IN>25)) x="LOW" d x
	i C1'=D1 s ^BioR=C1
	w !!,@P1,"Press a key to continue " r *X r:'X *X q

write	s x=$t(tab+j),u=$p(x,";",3) q:u="END"  w ?80-$l(u)\2,u,! q

x	w @P3,x,@P1 q

tab
	;;BioRhythms
	;;==========
	;;
	;;According to ancient Japanese theory our lives run in continuous cycles
	;;starting from our date of birth. There is a physical cycle of 23 days,
	;;an emotional cycle of 28 days and an intellectual cycle of 33 days.
	;;Each cycle is divided into two phases, an active (high) phase
	;;and a passive (low) phase. The changeover days are regarded
	;;as critical as the body is in a state of transition.
	;;END

CHEQUE
CHEQUE(DX,W4,W5,W13,W14,W16,O5,TT)	;GRI,,,;06:51 PM  5 Mar 2003;
	;=======================================================
	; Virgo Accounts - Write Cheque Merge File subroutine
	;
	; Copyright Graham R Irwin 2003
	;
	; Screens:       none
	; Files updated: DOS file CHEQUE.SPS
	; Subroutines:   NUM2WD
	;
	;=======================================================

	; DX - cashbook number
	; W4 - client code/null
	; W5 - matter code/nominal account
	; W13 - date
	; W14 - amount
	; W16 - cheque number
	; O5 - narrative
	; TT - transaction type

	S D1=$p(^CB(DX),%,1)
	S D0="Office" S:DX>99 D0="Client" S D0=D0_" Cheque"
	S (W6,W7,W8,W9,W10,W11,W12)=""
	I W4="" S W4="nominal"
	S W17=$p(O5," - ",1),W18=$p(O5," - ",2)
	I TT'="OFP" S W7=$p($g(^SM(W4,W5)),%,1)
	I TT="CLP" S SCA=^SC(W4,"A"),W8=$p(SCA,%,1),W9=$p(SCA,%,2),W10=$p(SCA,%,3),W11=$p(SCA,%,4),W12=$p(SCA,%,5),W6=$p($g(^SC(W4)),%,1)
	E  S W6=W18
	S X=W14\1 D ^NUM2WD S W15=Y_"Pounds "
	I W14 S W15=W15_(W14#1*100)_"p"
	E  S W15=W15_"Only"
	S W14=$fn(W14,",",2)

	;Write merge file
	;zetrap ERR^CHEQUE
	o 10:(file="cheque.sps":mode="w") u 10
	w D0,%,DX,%,D1,%,W4,%,W5,%,W6,%,W7,%,W8,%,W9,%,W10,%,W11,%,W12,%,W13,%,W14,%,W15,%,W16,%,W17,%,W18,%,TT,!
	c 10 u 0
	Q

	;Error trap if unable to write to CHEQUE.SPS
ERR	i $zerr=219 u 0 w *7,"  Warning - File in use" H 2 G CHEQUE
	zquit

Cont
Cont(z)	;gri;01:25 PM  7 Jul 1999

	; Routine to wait for a key press
	; has parameters 0 or 1

	w "Press a key to Continue " w:z "or <Esc> to Cancel " r *x r:'x *y
	q x

DATE
DATE	;GRI,,,;12:27 PM  19 Jan 1999
	;=======================================================
	; Date and Time Routines
	;
	; Copyright Graham R Irwin 1993-9
	;
	;=======================================================

	; 21549 = 31/12/1899
	; 21608 = 28/02/1900
	; 21914 = 31/12/1900

	;Usual entry point - Get today's date and time
	S %Y=$H-21549\365.25,%D=$H-21914-(%Y-1*365.25\1),%M=1
	I $H<21550 S %D=%D+365,%Y=%Y-1
	F %F=31,%Y#4=0&%Y+28,31,30,31,30,31,31,30,31,30 Q:%D'>%F  S %D=%D-%F,%M=%M+1
	S:$L(%D)=1 %D=0_%D S:$L(%M)=1 %M=0_%M S %Y=1900+%Y S DAT=%D_"/"_%M_"/"_%Y K %M,%D,%Y,%F
	I $H["," S %MI=$P($H,",",2)\60,TIM=$e(%MI\60+100,2,3)_":"_$e(%MI#60+100,2,3) K %MI
	Q

	;Convert $H format to DD/MM/YYYY
H2DMY	S:'$D(%H) %H=$H
	S %Y=%H-21549\365.25,%D=%H-21914-(%Y-1*365.25\1),%M=1
	I %H<21550 S %D=%D+365,%Y=%Y-1
	F %F=31,%Y#4=0&%Y+28,31,30,31,30,31,31,30,31,30 Q:%D'>%F  S %D=%D-%F,%M=%M+1
	S:$L(%D)=1 %D=0_%D S:$L(%M)=1 %M=0_%M S %Y=1900+%Y S %DAT=%D_"/"_%M_"/"_%Y K %M,%D,%Y,%F
	I %H["," S %MI=$P(%H,",",2)\60,TIM=$e(%MI\60+100,2,3)_":"_$e(%MI#60+100,2,3) K %MI
	K %H Q

	;Convert DD/MM/YY to $H format
DMY2H	S %D=+%DAT,%M=$P(%DAT,"/",2),%Y=$P(%DAT,"/",3)-1900,%H=365.25*$S(%M>2:%Y+60,1:%Y+59)\1+(30.6*$S(%M>2:%M+1,1:%M+13)\1)+%D-428 S:%H>21608 %H=%H-1 K %M,%D,%Y Q

DCHECK
DCHECK	;GRI,,,;08:46 PM  16 Feb 1999;
	;=======================================================
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:       1018
	;
	;=======================================================

	D X0^XSXS
	S %S=1018 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 DCHECK Q

	W !,@P1,?8,"Difference",@P2,?25,D2-D1,!!
	s x=$$^Cont(1)
	Q

	;Validation - From date
V01	Q:$d(ERR)  S %DAT=X D DMY2H^DATE S D1=%H
	I %H>+$H S ERR="May not be in the future"
	Q
	;To date
V02	Q:$d(ERR)  S %DAT=X D DMY2H^DATE S D2=%H
	I D1>%H S ERR="May not be earlier than From date"
	Q

DESPOOL
DESPOOL	;gri,gjl:A:B-UTILITY;04:52 PM  27 Oct 2000;
	;;Copyright (C) 1991 DataTree, Inc.  All rights reserved.
	; amended by Graham Irwin 1994, 2000

	; Simple manual de-spooler
	; ------------------------

	w /aa
	i $o(^%SPOOL(""))'="" g init
none	w *7,"  Nothing to de-spool  ",! h 3
	q
init
	w # n
	s sn="",sn=$o(^%SPOOL(sn))

begin
listsn
getbin
	k blist
	s bin="",n=0 f  s bin=$o(^%SPOOL(sn,bin)) q:bin=""  d
	. i '$d(^%SPOOL(sn,bin,0)) k ^%SPOOL(sn,bin) q  ;buggered header
	. s x=^%SPOOL(sn,bin,0) i $p(x,"|",2)="" q  ;still being spooled
	. s n=n+1,blist(n)=bin_"|"_$p(x,"|",1)_"|"_$zp(^(99999999999999))
	i 'n w !,"nothing in ",sn," to de-spool" q
listbin
	w !!,"The following jobs are currently filed with Spooler ",sn,":"
	w !,"      -No.-         -time spooled-              -pages-"
	f i=1:1:n s x=blist(i) d
	. w !,?7,i,?21,$$^%now($p(x,"|",2)),?50,$p(x,"|",3)

	w !!,"Enter the No. of the job you want to de-spool: "
	r m,! i m="" q
	s m=$tr(m,$c(0,9,32),"") i m'?1n.n g listbin
	i '$d(blist(m)) g listbin
	s bin=$p(blist(m),"|",1),h=$p(blist(m),"|",2)

	w !,"Enter the Page No. from which you want to start: "
	r sp,! i +sp=0 s sp=1
	i '$d(^%SPOOL(sn,bin,sp)) g listbin

	w !,"Print to " s dev=$$^%mdevice w ! i dev=-1 g listbin
	s nl=$p(dev,$c(22),2),mc=$p(dev,$c(22),3),dev=$p(dev,$c(22),1)

	u 0 w #,"...printing ",sn,"(",bin,")  spooled:",$$^%now(h)
	u dev f p=sp:1 q:'$d(^%SPOOL(sn,bin,p))  w:dev'=1&(p>1) # d
	. f l=1:1 q:'$d(^%SPOOL(sn,bin,p,l))  w ^(l),! d testend q:x=27
	i +dev'=1 w # c dev
	u 0 w !
	i $$^%myesno("Delete spool file","yes")="Y" d
	. LOCK +^%SPOOL(sn)
	. k ^%SPOOL(sn,bin)
	. LOCK -^%SPOOL(sn)
	g getbin

testend	q:+dev'=1
	i $y>22 s x=$$^Cont(1) w #
	q

DOSDEV
DOSDEV	;GRI;09:03 AM  18 Jan 2002
	;=======================================================
	; Routine to get DOS file name
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       9
	; Files updated: none
	; Subroutines:   ^INPUT
	;
	;=======================================================

	S %S=9 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 DOSDEV Q
	O 10:(file=FN:mode="w") U 10

	;Validation - File name
V01	I X'?1.8an S ERR="Illegal file name" Q
	S X=X_".txt" Q

EXPORT
EXPORT	;gri,,,;01:48 PM  16 Jan 2002
	;=======================================================
	; Virgo Accounts - Export Data
	;
	; Copyright Graham R Irwin 1997-2002
	;
	; Screens:       1066
	; Files updated: DOS files (see documentation)
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, H2DMY^DATE
	;
	;=======================================================

	; N.B. Day-end must be run first as Daybook file is not exported

	D X0^XSXS S YES="Y",NO="N"
	S (SC,SM,SB,AB,ML,CL,SW,TE,AY,CB,SF,SA,SD,SV,SZ)=YES

E01	S %S=1066 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 EXPORT Q
E02	W /C(0,22),@P1,"Update or Quit ? ",/EL,@P2 D ^GETX S X=$zconvert(X,"U")
	I X="Q"!(X=%) G EXPORT
	I X="U" G U01
	G E02

	;Update
U01	I SC="Y" D SC1
	I SM="Y" D SM1
	I SB="Y" D SB1
	I AB="Y" D AB1
	I ML="Y" D ML1
	I CL="Y" D CL1,CI1
	I SW="Y" D SW1
	I TE="Y" D TE1
	I AY="Y" D AY1
	I CB="Y" D CB1
	I SF="Y" D SF1
	I SA="Y" D SA1,SR1
	I SD="Y" D SD1
	I SV="Y" D SV1
	I SZ="Y" D SZ1
	U 0
	Q

	;Client details
SC1	U 0 W /c(0,23),@P1,"Exporting client details",/el
	S CX="",FN="Clients.txt" D OPEN
SC2	S CX=$o(^SC(CX)) I CX="" C 10 Q
	W CX,%,^SC(CX),!
	W ^SC(CX,"A"),!
	W ^SC(CX,"N"),!!
	G SC2

	;Matter details
SM1	U 0 W /c(0,23),@P1,"Exporting matter details",/el
	S (CX,MX)="",FN="Matters.txt" D OPEN
SM2	S CX=$o(^SM(CX)) I CX="" C 10 Q
SM3	S MX=$o(^SM(CX,MX)) I MX="" G SM2
	W CX,%,MX,%,^SM(CX,MX),!
	W ^SM(CX,MX,"N"),!
	W $g(^SM(CX,MX,"O")),%,$g(^SM(CX,MX,"C")),!!
	G SM3

	;Bills
SB1	U 0 W /c(0,23),@P1,"Exporting bill details",/el
	S BX="",FN="BillReg.txt" D OPEN
SB2	S BX=$o(^SB(BX)) I BX="" C 10 Q
	W BX,%,^SB(BX),!
	G SB2

	;Matter ledger
ML1	U 0 W /c(0,23),@P1,"Exporting matter ledger",/el
	S (CX,MX,X3)="",FN="MLedger.txt" D OPEN
ML2	S CX=$o(^ML(CX)) I CX="" C 10 Q
ML3	S MX=$o(^ML(CX,MX)) I MX="" G ML2
	W CX,%,MX,%,^ML(CX,MX),!
ML4	S X3=$o(^ML(CX,MX,X3)) I X3="" W ! G ML3
	W X3,%,^ML(CX,MX,X3),!
	G ML4

	;Client ledger
CL1	U 0 W /c(0,23),@P1,"Exporting client ledger",/el
	S (CX,MX,X3)="",FN="CLedger.txt" D OPEN
CL2	S CX=$o(^CL(CX)) I CX="" C 10 Q
CL3	S MX=$o(^CL(CX,MX)) I MX="" G CL2
	W CX,%,MX,%,^CL(CX,MX),!
CL4	S X3=$o(^CL(CX,MX,X3)) I X3="" W ! G CL3
	W X3,%,^CL(CX,MX,X3),!
	G CL4

	;Client interest
CI1	U 0 W /c(0,23),@P1,"Exporting interest rates",/el
	S IX="",FN="IntRates.txt" D OPEN
CI2	S IX=$o(^CI(IX)) I IX="" C 10 Q
	W IX,%,^CI(IX),!
	G CI2

	;WIP
SW1	U 0 W /c(0,23),@P1,"Exporting Work-in-progress",/el
	S (CX,MX,X3,X4)="",FN="WorkInP.txt" D OPEN
SW2	S CX=$o(^SW(CX)) I CX="" C 10 Q
SW3	S MX=$o(^SW(CX,MX)) I MX="" G SW2
	W CX,%,MX,%,^SW(CX,MX),!
SW4	S X3=$o(^SW(CX,MX,X3)) I X3="" W ! G SW3
SW5	S X4=$o(^SW(CX,MX,X3,X4)) I X4="" G SW4
	W X3,%,X4,%,^SW(CX,MX,X3,X4),!
	G SW5

	;Detailed time entries
TE1	U 0 W /c(0,23),@P1,"Exporting detailed time",/el
	S (CX,MX,X3,X4)="",FN="TimeEnts.txt" D OPEN
TE2	S CX=$o(^TE(CX)) I CX="" C 10 Q
TE3	S MX=$o(^TE(CX,MX)) I MX="" G TE2
TE4	S X3=$o(^TE(CX,MX,X3)) I X3="" G TE3
TE5	S X4=$o(^TE(CX,MX,X3,X4)) I X4="" G TE4
	W CX,%,MX,%,X3,%,X4,%,^TE(CX,MX,X3,X4),!
	G TE5

	;Cashbooks
CB1	U 0 W /c(0,23),@P1,"Exporting cashbook details",/el
	S (KX,X2)="",FN="CashBoox.txt" D OPEN
CB2	S KX=$o(^CB(KX)) I KX="" C 10 Q
	W KX,%,^CB(KX),!
CB3	S X2=$o(^CB(KX,X2)) I X2="" W ! G CB2
	W X2,%,^CB(KX,X2),!
	G CB3

	;Account balances and budgets
AB1	U 0 W /c(0,23),@P1,"Exporting account balances",/el
	S AX="",FN="AccBals.txt" D OPEN
AB2	S AX=$o(^AB(AX)) I AX="" C 10 Q
	W AX,%,^AB(AX),$g(^NB(AX)),!
	G AB2

	;Nominal ledger (transactions)
AY1	U 0 W /c(0,23),@P1,"Exporting nominal ledger",/el
	S (AX,X2)="",FN="NLedger.txt" D OPEN
AY2	S AX=$o(^AY(AX)) I AX="" C 10 Q
	W AX,!
AY3	S X2=$o(^AY(AX,X2)) I X2="" W ! G AY2
	W X2,%,^AY(AX,X2),!
	G AY3

	;Historic entries
SD1	U 0 W /c(0,23),@P1,"Exporting historic entries",/el
	S (CX,MX,X3,X4)="",FN="HistDisb.txt" D OPEN
SD2	S CX=$o(^SD(CX)) I CX="" C 10 Q
SD3	S MX=$o(^SD(CX,MX)) I MX="" G SD2
SD4	S X3=$o(^SD(CX,MX,X3)) I X3="" G SD3
SD5	S X4=$o(^SD(CX,MX,X3,X4)) I X4="" G SD4
	W CX,%,MX,%,X3,%,X4,%,^SD(CX,MX,X3,X4),!
	G SD5

	;Fee earners
SF1	U 0 W /c(0,23),@P1,"Exporting fee earners",/el
	S FX="",FN="FeeErnrs.txt" D OPEN
SF2	S FX=$o(^SF(FX)) I FX="" C 10 Q
	W FX,%,^SF(FX),!
	G SF2

	;Fee rates & activities
SA1	U 0 W /c(0,23),@P1,"Exporting fee rates",/el
	S AX="",FN="Activs.txt" D OPEN
SA2	S AX=$o(^SA(AX)) I AX="" C 10 Q
	W AX,%,^SA(AX),!
	G SA2

SR1	S (RX,X2)="",FN="FeeRates.txt" D OPEN
SR2	S RX=$o(^SR(RX)) I RX="" C 10 Q
	W RX,%,^SR(RX),!
SR3	S X2=$o(^SR(RX,X2)) I X2="" W ! G SR2
	W X2,%,^SR(RX,X2),!
	G SR3

	;VAT file
SV1	U 0 W /c(0,23),@P1,"Exporting VAT file",/el
	S FN="VATFile.txt" D OPEN
	W $d(^SV),!
	C 10 Q

	;System parameters
SZ1	U 0 W /c(0,23),@P1,"Exporting system parameters",/el
	S FN="System.txt" D OPEN
	W "Practice name = ",^SZ("PN"),!
	W "Licence no = ",^SZ("SW"),!
	W "Time units = ",^SZ("TU"),!
	W "VAT rate = ",^SZ("VAT"),!
	W "VAT cash basis = ",^SZ("VCB"),!
	W "Matter limits report = ",$g(^SZ("MLR"),"N"),!
	W "Default office cashbook = ",$g(^SZ("DOC"),1),!
	W "Auto day-end = ",^SZ("ADE"),!
	W "30 days date check = ",$g(^SZ("D30"),"Y"),!
	W "Detailed time = ",$g(^SZ("DTE"),"N"),!
	W "Write-off disbs = ",$g(^SZ("WOD"),"Y"),!
	W "Overdraw client = ",$g(^SZ("ODC"),"N"),!
	W "Last day-end = " S %H=^SZ("LDE") D H2DMY^DATE W %DAT,!
	W "Last period-end = " S %H=^SZ("LPE") D H2DMY^DATE W %DAT,!
	W "Next year-end = " S %H=^SZ("NYE") D H2DMY^DATE W %DAT,!
	W "Next item number = ",^SY,!
	W "Next bill number = ",^SB,!
	C 10 Q

OPEN	O 10:(file=FN:mode="w") U 10 Q

EnCode
EnCode	;gri;02:51 PM  16 Dec 1994

	s n=53,l="3,5,7,11,13,17,19,23,29,31"
	f i=1:1:$l(x) s a=$a($e(x,i))-48 s:a>9 a=a-7 d n
	s n=n#36,n=n+48 s:n>57 n=n+7 s n=$c(n)
	q

n	s n=n+a
	q

GETPGM
GETPGM(M,N)	;gri;01:41 PM  28 Apr 1997
	;=======================================================
	; Routine to get program name
	;
	; Copyright Graham R Irwin 1994
	;
	; Screens:	
	; Files updated:
	; Subroutines:
	;
	;=======================================================

	Q:M=""  Q:N=""
	Q:'$d(^MENU(M,N))
	S X=^MENU(M,N),H2=$p(X,%,1),PGM=$p(X,%,2)
	Q

GETX
GETX	;GRI,,,;01:46 PM  13 Sep 2002;
	;=======================================================
	; Input Routine for Option prompt &c
	;
	; Copyright Graham R Irwin 1994-2002
	;
	;=======================================================

	s j=1,X="",x1=$x,y1=$y
read	r *a s c=$c(a)
	i a=13 s j=$l(X)+1 d move w /ef q  ;return
	i c=% s X=% r *a q
	i c?1anp d ins g read
	;i a=156 d ins g read ;pound sign
	i a=14 i j>1 s j=j-1 d move g read ;cursor left
	i a=18 i j'>$l(X) s j=j+1 d move g read ;cursor right
	i a=127 s X=$e(X,1,j-1)_$e(X,j+1,999) d show g read ;delete
	i a=8 s X=$e(X,1,j-2)_$e(X,j,999) s:j>1 j=j-1 d show g read ;backspace
	i a=4 s j=$l(X)+1 d move g read ;end
	i a=21 s X="?" q  ;F1
	i a=23 s X="I" q  ;F3
	i a=24 s X="A" q  ;F4
	i a=25 s X="U" q  ;F5
	i a=26 s X="D" q  ;F6
	i a=27 s X="^" q  ;Esc
	i a=28 s X="^" q  ;F7
	i a=29 s X="P" q  ;F8
	i a=30 s X="N" q  ;F9
	i a=31 s X="Z" q  ;F10
	i a=12 s X="N" q  ;PgDn
	i a=5 s X="V" q  ;PgUp
	i a=130 s X="****" q  ;Alt+F4
	i a=0 d next i end q  ;extended char
	g read

ins	i $l(X)'<55 w *7 q
	s X=$e(X,1,j-1)_c_$e(X,j,999) d show s j=j+1 w /c($x+1,$y)
	q

move	w /c(x1+j-1,y1) q

show	w /c(x1+j-1,$y),/el,$e(X,j,999),/c(x1+j-1,$y) q

	;extended character
next	r *a s end=0
	i a=71 s X="\\",end=1 q  ;home
	i a=147 s X="",j=1 d show q  ;ctrl+delete
	;i a=133 s X="" ;F11
	i a=134 s X="Y" ;F12
	s end=1
	q

HELP
HELP	;gri;10:03 AM  1 Dec 2000

	; Run Consistency

	D V^XSINIT
	S PGM="XSHB",H2="Consistency"
	D ^XSHB
	H

HELPLIST
HELPLIST	;GRI,,,;01:51 PM  7 Dec 1995;
	;=======================================================
	; List Help Text
	;
	; Copyright Graham R Irwin 1993-94
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	S H2="Help Text Listing"
	D X0^XSXS
	D ^OUTPUT Q:%OP[%  S N=99,P=1
	D HEAD^XSXS

	S (S1,S2)=""
H01	S S1=$o(^HELP(S1)) I S1="" G END
H02	S S2=$o(^HELP(S1,S2)) I S2="" G H01
	W !,"--- Help text for: ",S1,"/",S2," ---------------------------------------",!! S N=N+3,H3=""
H03	S H3=$o(^HELP(S1,S2,H3)) I H3="" G H02
	W ^HELP(S1,S2,H3),! S N=N+1 I N>58 D HEAD^XSXS
	G H03

END
	D END^XSXS Q

ICAA
ICAA	;GRI,,,;08:44 PM  27 May 2005;
	;=======================================================
	; Virgo Accounts - Maintain Interest Rate Tables
	;
	; Copyright Graham R Irwin 1993-2005
	;
	; Screens:       1035, 1100
	; Files updated: 
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	D X0^XSXS
	S %S=1100 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% Q:%J=1  G ICAA

	W @P1,!?8,"Min Amount   Int Rate",!!
E00	S X3="",%S=1035
E03	S X3=$o(^IN(RX,D0,X3)) I X3=""!($Y>20) G L01
	S R1=^IN(RX,D0,X3)
	W @P1,?8,$j(X3,8),?21,$j(R1,7,3),! G E03

I01	W /c(0,19),/ef
	F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G I01:%J>1 S F1=0 G N01
E09	W /C(0,22),@P1,"Update, Amend, Delete or Quit ? ",/el,@P2 D ^GETX,CASE
	I X="Q"!(X=%) G ICAA
	I X="A" G A01
	I X="U" G U01
	I X="D" W @P1,"  Are you sure ? ",@P2 D ^GETX,CASE I X="Y" S A1="" G D01
	G E09

	;Update
U01	S ^IN(RX,D0,IX)=A1
	W /c(0,9),/ef,@P1 G E00

L01	W /C(0,22),@P1,"Next, Select, Insert or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) G ICAA
	I X="N" G N01
	I X="S"!(X="Z") G S01
	I X="I" G I01
	G L01

	;Delete
D01	K ^IN(RX,D0,IX)
	W /c(0,9),/ef,@P1 G E00

	;Next
N01	W /C(0,9),/EF,@P1 G E03:A1'="",E00

	;Amend
A01	W /c(0,19),/ef F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G N01:%J=1,A01
	G E09

	;Select
S01	W @P1,"  Select item ",@P2 D ^GETX S IX=+X I $g(^IN(RX,D0,IX))="" G L01
	S A1=^INT(RX,D1,IX)
	W /c(0,19),/ef F %J=1:1 D D^INPUT Q:X="END"!(X[%)
	G E09

	;Validation ----------------------------------------
	;Date
V01	I $D(ERR) Q
	S %DAT=X D DMY2H^DATE S D0=%H
	Q

CASE	S X=$zconvert(X,"U") Q

ICAB
ICAB	;GRI,,,;03:31 PM  26 May 2005;
	;=======================================================
	; Virgo Accounts - Print Interest Rate Tables
	;
	; Copyright Graham R Irwin 1999-2002
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, H2DMY^DATE, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	D ^OUTPUT Q:%OP[%  S N=99,P=1
	D HEAD^XSXS
	W ?2,"Table ID   Eff Date   Min Amount    Rate",! S N=N+1

	S (X1,X2,X3)=""
E01	S X1=$o(^IN(X1)) I X1="" G END
	W !?5,X1
E02	S X2=$o(^IN(X1,X2)) I X2="" G E01
	S %H=X2 D H2DMY^DATE W ?12,%DAT
E03	S X3=$o(^IN(X1,X2,X3)) I X3="" W ! S N=N=1 G E02
	S A1=^IN(X1,X2,X3)
	W ?24,$j(X3,8),?35,$j(A1,7,2),! S N=N+1 I N>60 D HEAD^XSXS
	G E03

END	D END^XSXS
	Q

ICAC
ICAC	;GRI,,,;01:01 PM  28 May 2005;
	;=======================================================
	; Virgo Accounts - Calculate Client Interest
	;
	; Copyright Graham R Irwin 1998-2001
	;
	; Screens:       1101
	; Files updated: ^WORK($j)
	; Subroutines:   X0^XSXS, ^INPUT, ^OUTPUT, HEAD^XSXS,
	;                END^XSXS, DMY2H^DATE, H2DMY^DATE
	;
	;=======================================================

	; DP - start date 	DPH - same %H format
	; DQ - end date  	DQH - same %H format
	; D1 - current working date

	D X0^XSXS K ^WORK($j)
	S %S=1101 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 ICAC Q

	;Get ^CL transactions and sort by date
	S IX=""
S01	S IX=$o(^CL(CX,MX,IX)) I IX="" G T01
	S CL=^CL(CX,MX,IX),D1=$p(CL,%,1),D2=$p(CL,%,2),D3=$p(CL,%,3)
	S %DAT=D1 D DMY2H^DATE S D1H=%H,(T2,T3)=0
	I $d(^WORK($j,D1H)) S WA=^WORK($j,D1H),T2=$p(WA,%,2),T3=$p(WA,%,3)
	S ^WORK($j,D1H)=D1_%_(D2+T2)_%_(D3+T3)
	G S01

	;Get interest rates
T01	S JX=""
T02	S JX=$o(^IN(RX,JX)) I JX="" G P01
	I '$d(^WORK($j,JX)),JX'=DPH,JX'=DQH S ^WORK($j,JX)=""
	G T02

	;Print headings
P01	D ^OUTPUT I %OP[% G ICAC
	S N=99,P=1,IX="" D HEAD^XSXS
	W "Client ",CX,?12,$p(^SC(CX),%,1),!
	W "Matter ",MX,?12,$p(^SM(CX,MX),%,1),!
	W "Rate Table  ",RX,!,"Start Date  ",DP,!,"End Date    ",DQ,!!
	W "-- Date --",?19,"Balance   Int Rate    # days",?53,"Interest",?72,"Total",!!

	;First entry
	S IX=$o(^WORK($j,"")) I IX="" G END
	I DPH<IX W DP,?16,"No transactions on or before ",DP,! G END

	S (T1,SUM,DYS)=0
L01	S WA=^WORK($j,IX)
	I IX>DPH W DP,?16,$j(T1,10,2),! S D0H=DPH,T0=T1,T2=0,N=9,IX=IX-1 G M01
	S IX=$o(^WORK($j,IX)) I IX="" G END
	S T1=T1+$p(WA,%,3)-$p(WA,%,2)
	G L01

	;Now for the meat of it
M01	S IX=$o(^WORK($j,IX)) I IX="" G N01
	I IX'<DQH G N01
	S WA=^WORK($j,IX)
	S D1=$p(WA,%,1),D2=$p(WA,%,2),D3=$p(WA,%,3)
	I WA="" S %H=IX D H2DMY^DATE S D1=%DAT,(D2,D3)=0
	S T1=T1-D2+D3
	D LINE S N=N+1 I N>56 D HEAD^XSXS
	G M01

	;Last entry
N01	S D1=DQ,IX=DQH D LINE
	W !,"Total Interest for the Period: ",$j(T2,10,2)
	W !,"Average Daily Balance: ",?31,$j((SUM/DYS),10,2)

END	D END^XSXS
	G ICAC

LINE	D GETIR
	S DY=IX-D0H,DYS=DYS+DY,SUM=SUM+(T0*DY),IN=T0*DY*IR/36500,T2=T2+IN
	W D1,?16,$j(T1,10,2),?27,$J(IR,9,2),?41,$j(DY,5),?49,$J(IN,12,4),?65,$j(T2,12,4),!
	S D0H=IX,T0=T1
	Q

GETIR	S JX=$zp(^IN(RX,D0H+1))
	I JX="" S JX=$o(^IN(RX,""))
	S X3=$zp(^IN(RX,JX,T0+1))
	I X3="" S IR=0 Q
	S IR=^IN(RX,JX,X3)
	Q

CASE	S X=$zconvert(X,"U") Q

	;Validation
	;rate table
V01	I $d(ERR) Q
	I '$d(^IN(X)) S ERR="Table must exist"
	Q
	;start date
V02	I $d(ERR) Q
	S %DAT=X D DMY2H^DATE S DPH=%H
	I DPH<$o(^IN(RX,"")) S ERR="No interest rates at this date" Q
	Q
	;end date
V03	I $d(ERR) Q
	S %DAT=X D DMY2H^DATE S DQH=%H
	I DQH'>DPH S ERR="Must be later than start date"
	Q

INPASSW
INPASSW	;GRI,,,;12:49 PM  17 Aug 1994
	;=======================================================
	; Phoenix Accounts - Input Password
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   xencrypt
	;
	;=======================================================

	s echoa=$zdevechoa u 0:echoa=0 ; save echo status - turn off
	s x="",%S2=15
	W ?8,"Password",?25 d read
	s x=$zconvert(x,"U"),X=$$crypt(x)
	u 0:echoa=echoa
	w ! q

read	r *a s c=$c(a)
	i a=13 q
	i c?1anp d ins g read
	i a=8 d back g read ;backspace
	g read

ins	i $l(x)'<%S2 w *7 q
	s x=x_c w "#"
	q

back	i x="" q
	s x=$e(x,1,$l(x)-1) w $c(8,32,8)
	q

crypt<x>
	s x=$j(x,8)
	zc xencrypt(x,"login")
	s y="" f i=1:1:$l(x) s y=y_$c($a(x,i)\16+64,$a(x,i)#16+64)
	q y

INPUT
INPUT	;GRI;02:43 PM  10 Jun 2003
	;=======================================================
	; Simple Screen Input Routine
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Files updated: none
	; Subroutines:   ^READX, ^GETX, ^NEWNAME, ^H2DMY^DATE,
	;                ^Cont, ^%video
	;
	;=======================================================

	;-------------------------------------------------------------------
	; Parameters:-
	;
	; %S1 - Prompt text	%S2 - Max length	%S3 - Min length
	; %S4 - Variable name	%S5 - Search file	%S6 - Default
	; %S7 - Validation      %S8 - Condition (optional)
	;-------------------------------------------------------------------

	S %I=0
IN1	S X=^SCREEN(%S,%J),%S1=$P(X,%,1),%S2=$P(X,%,2),%S3=$P(X,%,3),%S4=$P(X,%,4),%S5=$P(X,%,5),%S6=$P(X,%,6),%S7=$P(X,%,7,99) Q:X="END"
	S %S8=$g(^SCREEN(%S,%J,"IF")) I %S8'="",@%S8 S X="" Q
	I $e(%S1,1)="@" S %S1=$$^NEWNAME(%S1) ;check pseudonyms
	W @P1,?8,%S1,@P2,?25 S X1=$X,Y1=$Y ;display prompt
	;if it's not a display field show underscores ------
IN2	I %S2 W /C(X1,Y1),@P2,$E(PU,1,%S2)
	;if not amend mode show default --------------------
	I '%I,%S6'="" W /C(X1,Y1),@%S6
	;if it's a display field show the value ------------
	I '%S2,%S4'="" W /C(X1,Y1),@%S4 S X=@%S4 G IN7
	;if we're in amend mode show original value --------
	I %I W /C(X1,Y1),@%S4
	W /c(0,22),/el,@P1,"Esc - Quit; F1 - Help" W:%S5'="" "; F2 - Search" W:%I ?72,"[amend]"
	W /c(X1,Y1),@P2 K ERR D ^READX Q:X[%
	I X="?" D HELP G IN2
	I X="??" D SEARCH G IN2:X=""!(X[%) S @%S4=X W /C(X1,Y1),@P2,X,/ef G IN7
	I X="",%S6'="" S X=@%S6
	I $L(X)<%S3 S ERR="Min length is "_%S3 G ERR
IN7	I %S7'="" X %S7
	S X2=$y,Y2=$y W /C(X1,Y1),@P2,X I X2*80+Y2>($x*80+$y) W /C(X2,Y2)
	I $g(ERR)="" S @%S4=X W ! Q
ERR	W /C(0,23),*7,@P3,"Invalid input - ",ERR,/EF W @P2 G IN2

	;Amend mode
A	S %I=1 G IN1

	;Display mode
D	S X=^SCREEN(%S,%J) Q:X="END"  S %S1=$P(X,%,1),%S4=$P(X,%,4),%S5=$P(X,%,5),%S7=$P(X,%,7,99),X=@%S4
	S %S8=$g(^SCREEN(%S,%J,"IF")) I %S8'="",@%S8 S X="" Q
	I $e(%S1,1)="@" S %S1=$$^NEWNAME(%S1)
	W @P1,?8,%S1,@P2,?25,@%S4
	I %S5'="",%S7'="" X %S7
	W ! Q

	;Help routine
HELP	W @P5,/C(69,23),PGM,"/",%S4
	d uwo^%video(8,16)
	S %K="",%H1=PGM,%H2=%S4
	I '$d(^HELP(%H1,%H2)) S %H1=$e(PGM,1,2)
	I '$d(^HELP(%H1,%H2)) S %H2=0
H01	W #
H02	S %K=$o(^HELP(%H1,%H2,%K)) I %K="" w /c(0,13) s %X=$$^Cont(0) G QHLP
	W ^(%K),! I $y<13 G H02
	w /c(0,13) s %X=$$^Cont(1) w @P5 i %X=27 g QHLP
	G H01
QHLP	d uwc^%video()
	q

	;Search routine
SEARCH	S X="" I %S5="" Q
	I '$d(^SEARCH(%S5)) Q
	S %K=$g(^SEARCH(%S5)) I %K'="" X %K Q
	d uwo^%video(6,18)
	W #,@P1,/EF,"Search for " R %X,!! S %X=$zconvert(%X,"L"),%K=1,S3="",S2=%X
S01	S S2=$o(^SEARCH(%S5,S2)) I S2=""!(%X'=$e(S2,1,$l(%X))) G S03
S02	S S3=$o(^SEARCH(%S5,S2,S3)) I S3="" G S01
	W %K,?10,S2,?45,S3,! S SS(%K)=S3,%K=%K+1 I $Y>13 G S03
	G S02

S03	i '$d(SS) g QSCH
	W /c(0,15),"Select an entry " D ^GETX I X[% G QSCH
	I X,$g(SS(X))="" G S03
	I 'X G QSCH:S2=""!(%X'=$e(S2,1,$l(%X))) W # K SS G S02
	S X=SS(X)
QSCH	K S2,S3,%X,SS
	d uwc^%video()
	Q

	;Validation Routines --------------------------------

DATE	I X?1"-".N!(X?1"+".N) S %H=$H+X D H2DMY^DATE S X=%DAT
	I X?6N S X=$e(X,1,2)_"/"_$e(X,3,4)_"/"_$e(X,5,6)
	S X=$tr(X,".-","//")
	S ERR="Invalid date" I X'?1.2N1"/"1.2N1"/"1.4N,X'?1.2N1"/"1.2N Q
	S %D=+X,%M=$P(X,"/",2),%Y=$P(X,"/",3) I '%M!(%M>12)!'%D Q
	I %Y="" S %Y=$P(DAT,"/",3)
	S %P=31_(%Y#4=0+28)_"31303130313130313031" I %D>$E(%P,%M+%M-1,%M+%M) Q
	S:$L(%D)=1 %D=0_%D S:$L(%M)=1 %M=0_%M S:%Y<80 %Y=2000+%Y S:%Y<100 %Y=1900+%Y S X=%D_"/"_%M_"/"_%Y K ERR,%D,%M,%Y,%P Q

TIME	S ERR="Invalid time",%H=+X S:X[":" %M=$P(X,":",2) I X>24!(%M>59) K %M,%H Q
	K ERR,%M,%H Q

EXIST	I '$D(@("^"_%S5_"(X)")) S ERR="Does not exist"
	Q

EXDISP	I '$D(@("^"_%S5_"(X)")) S ERR="Does not exist" Q
	W ?48,$p(^(X),%,1),/ef Q

NOTEX	I $D(@("^"_%S5_"(X)")) S ERR="Already exists"
	Q

YESNO	D CASE I "YN"'[X S ERR="Must be 'Y' or 'N'"
	Q

AMOUNT	I $e(X,1)="-" S ERR="May not be negative" Q
	I X'?1.7N,X'?.7N1"."1.2N S ERR="Amount required" Q
	I X'="*" S X=$J(X,0,2)
	Q

AMOUNTN	I X'?1.N,X'?1"-"1.N,X'?.7N1"."1.2N,X'?1"-".7N1"."1.2N S ERR="Amount required" Q
	I X'="*" S X=$J(X,0,2)
	Q

NUMBER	I X'?.N S ERR="Must be a number"
	Q

ALPHNUM	I X'?.AN S ERR="Must be alphanumeric"
	Q

CASE	S X=$zconvert(X,"U") Q

ISBN
ISBN	;gri;01:16 PM  5 Jan 2006;

	S A="10,9,8,7,6,5,4,3,2"
	;S Y="095333310",X=Y
	S Y="095420470",X=Y
	F K=1:1:10 D
	.S J=0
	.F I=1:1:9 S J=J+($e(X,I)*$p(A,",",I))
	.S Z=11-(J#11) S:Z=10 Z="X" S:Z=11 Z=0
	.W $e(X,1),"-",$e(X,2,8),"-",$e(X,9),"-",Z,!
	.S X=X+1,X=$e("00000000",1,9-$l(X))_X

	W !
	S A="1,3,1,3,1,3,1,3,1,3,1,3"
	S X="978"_Y
	F K=1:1:10 D
	.S J=0
	.F I=1:1:12 S J=J+($e(X,I)*$p(A,",",I))
	.S Z=10-(J#10) S:Z=10 Z=0
	.W $e(X,1,4),"-",$e(X,5,11),"-",$e(X,12),"-",Z,!
	.S X=X+1
	Q

KILLPW
KILLPW	;GRI;12:55 PM  21 Sep 1994;
	;=======================================================
	; Phoenix Accounts - Delete Password
	;
	; Copyright Graham R Irwin 1993-94
	;
	; Screens:       none
	; Files updated: ^Access
	; Subroutines:
	;
	;=======================================================

	w #
del	w !?8,"Delete password? " r X d CASE i X="Y" k ^Access w !!?8,"Password Deleted" q
	i X'="N" g del

CASE	S X=$zconvert(X,"U") q

LEGUP
LEGUP ;GRI,,,;03:10 PM  27 Mar 1995;
	;=======================================================
	; Virgo Accounts - Update Legal Aid fee rates
	;
	; Copyright Graham R Irwin 1993-1995
	;
	; Screens:       1098
	; Files updated: ^SA ^SR
	; Subroutines:   X0^XSXS, XSLEGAID, RECR^XSHB
	;
	;=======================================================

	S H2="Update Legal Aid Rates" D X0^XSXS
	W @P1,?8,"This routine will update your fee rate tables. Only run it"
	W !?8,"if you are quite sure you wish to change your fee rates."
	W !!?8,"Which Legal Aid rates do you wish to use?"
	W !!?8,"1 - London rates"
	W !?8,"2 - Provincial rates",!!
	S %S=1098 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 LEGUP G QUIT
	I A2="N" G QUIT

	S A6="N" S:A1=1 A6="Y"
	D ^XSLEGAID
	D RECR^XSHB

QUIT	Q

	;Validation
V01	I "12"'[X S ERR="Must be '1' or '2'"
	Q

NAMES
NAMES	;GRI,,,;01:09 PM  3 Nov 1994;
	;=======================================================
	; Maintain Pseudonyms
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:       2
	; Files updated: ^NAMES
	; Subroutines:   ^INPUT, ^GETX
	;
	;=======================================================

	S H2="Maintain Pseudonyms"
	D X0^XSXS
	S %S=2
	W @P1,!?18,"Identity",?36,"Pseudonym",!! S AX=""
E03	S AX=$O(^NAMES(AX)) I AX=""!($Y>20) G L01
E04	S A1=^NAMES(AX) W @P1,?20,AX,?36,A1,! G E03
L01	W /C(0,22),@P1,"Next, Select, Insert or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) Q
	I X="N" G N01
	I X="S"!(X="Z") G S01
	I X="I" G I01
	G L01

	;Insert
I01	W /C(0,18),/EF F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G NAMES

E09	W /C(0,22),@P1,"Update, Amend, Delete or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G NAMES:X="Y",E09
	I X="A" G A01
	I X="U" G U01
	I X="D" W @P1,"  Are you sure ? ",@P2 D ^GETX D CASE I X="Y" K ^NAMES(AX) S AX="" G N01
	G E09

	;Update
U01	S ^NAMES(AX)=A1 W /C(0,7),/EF,@P1 S AX="" G E03

	;Next
N01	W /C(0,7),/EF,@P1 G E04:AX'="",E03

	;Amend
A01	W /C(0,18),/EF S %J=1 D D^INPUT
	F %J=2:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G NAMES:%J=2,A01
	G E09

	;Select
S01	W @P1,"  Identity ",@P2 D ^GETX,CASE S AX=X I $G(^NAMES(AX))="" G L01
	S A1=^NAMES(AX)
	W /C(0,18),/EF F %J=1:1 D D^INPUT Q:X="END"!(X[%)
	G E09

CASE	S X=$zconvert(X,"U") Q

NEWNAME
NEWNAME(XX)	;GRI;12:54 PM  27 Oct 1994
	;=======================================================
	; Pseudonym replacement
	;
	; Copyright Graham R Irwin 1993-94
	;
	;=======================================================

	S XX2=$p(XX,"@",2),XX3=$p(XX,"@",3,99),XX=$g(^NAMES(XX2))_XX3
	Q XX

NUM2WD
NUM2WD	;gri;09:33 AM  2 Feb 2003;
	;=======================================================
	; Convert number to words
	;
	; Copyright Graham R Irwin 2002
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   none
	;
	;=======================================================

	; Number in X ( < 1,000,000 )	Result in Y
	; I = index from left		J = index from right

	S Y="",W1="One ;Two ;Three ;Four ;Five ;Six ;Seven ;Eight ;Nine ",W2="Ten ;Twenty ;Thirty ;Forty ;Fifty ;Sixty ;Seventy ;Eighty ;Ninety ",W3="Eleven ;Twelve ;Thirteen ;Fourteen ;Fifteen ;Sixteen ;Seventeen ;Eighteen ;Nineteen "

	F I=1:1:$l(X) D IT
	Q

IT	I X>999999 S Y="******" Q
	S C1=$e(X,I),J=$l(X)-I+1,Y3=$p(W1,";",C1)

	; 10s and 10,000s in pairs
	I J=2!(J=5) D
	.S C2=$e(X,I+1),Y3=$p(W2,";",C1)_$p(W1,";",C2),I=I+1,J=J-1
	.I C1=1,C2'=0 S Y3=$p(W3,";",C2)

	; 100s
	I J=3 D
	.I C1 S Y3=Y3_"Hundred "
	.I $e(X,I+1,I+2)'="00" S Y3=Y3_"and "

	; 1,000s
	I J=4,$e(X,I-2,I)'="000" S Y3=Y3_"Thousand "

	; 100,000s
	I J=6 D
	.I C1 S Y3=Y3_"Hundred "
	.I $e(X,I+1,I+2)'="00" S Y3=Y3_"and "

	; 1,000,000s
	I J=7 S Y3=Y3_"Million "

	S Y=Y_Y3
	Q

OUTPUT
OUTPUT ;GRI,,,;12:52 PM  5 Jun 2007
	;=======================================================
	; Routine to select print device
	;
	; Copyright Graham R Irwin 1993-2003
	;
	; Screens:       none
	; Files updated: ^WRITE
	; Subroutines:   ^printstat, ^GETX, ^Cont
	;
	;=======================================================

E01	S X=^SYS("PTR"),FN=$p(X,%,1)
	K ^WRITE($j)
	W /C(0,22),@P1,"Ready to print to ",FN," ? (Press F1 for Help) ",/EF,@P2
	D ^GETX,CASE
	I X="?" D HELP G E01
	I X="N"!(X[%) S %OP=% Q
	I $e(X,1,2)="F:" G F01
	I X="SPOOL"!(X="SP") G S01
	I X="WB"!(X="Y"&(FN="WB")) G W01
	I X?3A1N,"LPT1;LPT2;LPT3;LPT4"[X S FN=X G E09
	I X'="Y" G E01

	;Check parallel port
E09	S X=$$^printstat(FN) I X["Printer-Selected",X["Ready" G F02
	U 0 W @P1,/c(0,23),*7,"Printer Not Ready "
	;s x=$$^Cont(1) w @P1 i x=27 S %OP=% Q
	W "- [R]etry, [I]gnore or [A]bort? " D ^GETX,CASE I X="A"!(X[%) S %OP=% Q
	I X="I" G F02
	G E09

	;Open spooler
S01	S %OP=20 O %OP U %OP
	Q

	;Write to web browser
W01	S FN=$g(^SYS("WBD"),"c:\docume~1\alluse~1\desktop\")_"virgo000.htm"
	S ^WRITE($j)="WB"
	G F02

	;Write to file
F01	S FN=$e(X,3,99) I FN'?1.8an G E01
	S FN=$g(^SYS("WTF"))_FN_".txt"

	;Open device
F02	S %OP=10 O %OP:(file=FN:mode="w") U %OP
	Q

	;Help routine
HELP	W @P5
	d uwo^%video(8,16)
	S %K="",%H1="R2PRINT",%H2=1
	I '$d(^HELP(%H1,%H2)) S %H1=$e(PGM,1,2)
	I '$d(^HELP(%H1,%H2)) S %H2=0
H01	W #
H02	S %K=$o(^HELP(%H1,%H2,%K)) I %K="" w /c(0,13) s %X=$$^Cont(0) G QHLP
	W ^(%K),! I $y<13 G H02
	w /c(0,13) s %X=$$^Cont(1) w @P5 i %X=27 g QHLP
	G H01
QHLP	d uwc^%video()
	q

CASE	S X=$zconvert(X,"U") Q

PATCH
PATCH	;GRI;10:42 AM  18 Jan 1999;
	;=======================================================
	; Virgo Accounts - Install Patch
	;
	; Copyright Graham R Irwin 1993-9
	;
	; Screens:       none
	; Files updated: Routines overwritten
	; Subroutines:   auto^%rload
	;
	;=======================================================

	s f="patch"
	o 10:(file=f:mode="r")
	d auto^%rload(10,1,0,"O","A","F",0,"",0,0)
	q

READX
READX	;GRI,,,;09:07 AM  27 Aug 1999
	;=======================================================
	; Input subroutine
	; read a value into variable X until <return> or <tab>
	;
	; Copyright Graham R Irwin 1994-99
	;
	; X - input variable
	; j - character position
	;
	; key values:-
	;  4 = end
	;  8 = backspace
	;  9 = return
	; 13 = tab
	; 14 = cursor left
	; 18 = cursor right
	; 21 = F1
	; 22 = F2
	; 27 = Esc
	; 28 = F7
	; 127 = delete
	; 142 = ctrl+left arrow
	; 146 = ctrl+right arrow
	; 156 = pound
	; 0,71 = Home
	; 0,147 = ctrl+delete
	;
	;=======================================================

	s j=1,X=""
	;if amend mode get original value
	i $d(%I),%I s X=@%S4

read	r *a s c=$c(a)
	i a=13!(a=9) s j=$l(X)+1 d move w /ef q
	i c=% s X=% r *a q
	i c?1anp d ins g read
	i a=156 d ins g read
	i a=14 i j>1 s j=j-1 d move g read
	i a=18 i j'>$l(X) s j=j+1 d move g read
	i a=127 s X=$e(X,1,j-1)_$e(X,j+1,999) d show g read
	i a=8 s X=$e(X,1,j-2)_$e(X,j,999) s:j>1 j=j-1 d show g read
	i a=4 s j=$l(X)+1 d move g read
	i a=142 d ctll,move g read
	i a=146 d ctlr,move g read
	i a=21 s X="?" q
	i a=22 s X="??" q
	i a=27 s X="^" q
	i a=28 s X="^" q
	i a=0 d next i end q
	g read

ins	i $l(X)'<%S2 w *7 q
	s X=$e(X,1,j-1)_c_$e(X,j,999) d show s j=j+1 w /c($x+1,$y)
	q

move	w /c(X1+j-1,Y1) q

show	w /c(X1+j-1,$y),$e(X,j,999),$e(PU,1,%S2-$l(X)),/c(X1+j-1,$y) q

	;Control + left arrow
ctll	i j=1 q
	f j=j-1:-1:1 q:$e(X,j)'=" "
	i j=1 q
	f j=j-1:-1:1 q:$e(X,j)=" "
	i j>1 s j=j+1
	q

	;Control + right arrow
ctlr	s j=$f(X," ",j)
	i j=0 s j=$l(X)+1 q
	f  q:$e(X,j)'=" "  s j=j+1
	q

	;Extended character
next	r *a s end=0
	i a=71 s j=1 d move q
	i a=147 s X=$e(X,1,j-1) s:%I @%S4=X d show q
	;i a=134 d ^XSY ;F12
	s end=1
	q

SCRDEF
SCRDEF	;GRI,,,;01:37 PM  3 Nov 1994
	;=======================================================
	; Screen Definition Routine
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:       0
	; Files updated: ^SCREEN, ^WORK($j)
	; Subroutines:   X0^XSXS, has its own INPUT routine, ^GETX
	;
	;=======================================================

	D X0^XSXS
	S %J=1,%S=0 D INPUT Q:X=%
	I $d(^SCREEN(X)) G D01
	S S2=1 W /C(44,4),"* NEW RECORD *",!
	;Input
E01	W /C(0,5),/EF F %J=2:1 D INPUT Q:X="END"!(X=%)
	I X=% G SCRDEF
E02	D S01 I X="N" G SCRDEF
	S ^SCREEN(S1,S2)="END" G E01

	;Display
D01	S S1=X,S2=1
D02	S SCA=^SCREEN(S1,S2) I SCA="END" G SCRDEF
	S SCA1=$P(SCA,%,1),SCA2=$P(SCA,%,2),SCA3=$P(SCA,%,3),SCA4=$P(SCA,%,4),SCA5=$P(SCA,%,5),SCA6=$P(SCA,%,6),SCA7=$P(SCA,%,7,99),SCB=$g(^SCREEN(S1,S2,"IF"))
	W /C(0,5),/EF F %J=2:1 D D Q:X="END"

D03	W /C(0,22),@P1,"Amend, Delete, Insert, Next field, Kill record, Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) G SCRDEF
	I X="A" G A01
	I X="I" G I01
	I X="D" G D10
	I X="N" S S2=S2+1 G D02
	I X="K" W @P1,"  Are you sure ? ",@P2 D ^GETX D CASE I X="Y" K ^SCREEN(S1) G SCRDEF
	G D03

	;Delete field
D10	S SA=S2
D11	S ^SCREEN(S1,SA)=^SCREEN(S1,SA+1),SA=SA+1
	I ^SCREEN(S1,SA)="END" K ^SCREEN(S1,SA) G D02
	G D11

	;Amend
A01	W /C(44,4),!,/EF F %J=2:1 D A Q:X="END"!(X=%)
	I X=% G SCRDEF
	D S01 I X="N" G SCRDEF
	G D02

	;Insert
I01	S SA=S2 K ^WORK($j)
I02	S ^WORK($j,SA)=^SCREEN(S1,SA),SA=$O(^SCREEN(S1,SA)) I SA'="" G I02
	S SA=S2
I03	S ^SCREEN(S1,SA+1)=^WORK($j,SA),SA=$O(^WORK($j,SA)) I SA'="" G I03
	K ^WORK($j) G D02

	;Continue
S01	W /C(0,22),@P1,"Continue ? (Y/N) ",/EL,@P2 D ^GETX D CASE Q:X="N"  G S01:X'="Y"
	S ^SCREEN(S1,S2)=SCA1_%_SCA2_%_SCA3_%_SCA4_%_SCA5_%_SCA6_%_SCA7
	I SCB'="" S ^SCREEN(S1,S2,"IF")=SCB
	S S2=S2+1 Q

CASE	S X=$TR(X,"abcdefghijklmnopqrstuvwxyz","ABCDEFGHIJKLMNOPQRSTUVWXYZ") Q

	;AMENDED INPUT ROUTINE TO PERMIT ^ -------------------------------
INPUT	S %I=0
IN1	S X=^SCREEN(%S,%J),%S1=$P(X,%,1),%S2=$P(X,%,2),%S3=$P(X,%,3),%S4=$P(X,%,4),%S5=$P(X,%,5),%S6=$P(X,%,6),%S7=$P(X,%,7,99) Q:X="END"
	W @P1,?8,%S1,@P2,?25 S X1=$X,Y1=$Y I %S2 W $E(PU,1,%S2)
	I %S6'="" D IN9 W @%S6
	I '%S2,%S4'="" D IN9 W @%S4,! Q
	I %I D IN9 W @%S4
IN2	D IN9 W @P2 K ERR R X Q:X=%
	I X="",%I W @%S4 S X=@%S4 G IN7
	I X=" ",%I S @%S4="" W /EF,! Q
	I X="",%S6'="" S X=@%S6 W X
	I $L(X)>%S2 S ERR="MAX LENGTH IS "_%S2 G ERR
	I $L(X)<%S3 S ERR="MIN LENGTH IS "_%S3 G ERR
IN7	X:%S7'="" %S7 I $G(ERR)="" S @%S4=X W /EF,! Q
ERR	W /C(0,23),*7,@P3,"INVALID INPUT - ",ERR,/EF W @P2 G IN2

IN9	S $X=X1,$Y=Y1 W /C($x,$Y) Q

A	S %I=1 G IN1

D	S X=^SCREEN(%S,%J) Q:X="END"  S %S1=$P(X,%,1),%S4=$P(X,%,4),%S5=$P(X,%,5),%S7=$P(X,%,7),X=@%S4
	W @P1,?8,%S1,@P2,?25,@%S4 I %S5'="",%S7'="" X %S7
	W ! Q

	;Validation Routines
EXIST	I '$D(@("^"_%S5_"(X)")) S ERR="DOES NOT EXIST"
	Q
EXDISP	I '$D(@("^"_%S5_"(X)")) S ERR="DOES NOT EXIST" Q
	W ?48,$p(^(X),%,1) Q

SCRDFP
SCRDFP	;GRI,,,;01:38 PM  3 Nov 1994
	;=======================================================
	; Screen Definition Print Routine
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:       0, 1
	; Files updated: none
	; Subroutines:   X0^XSXS, ^INPUT, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	S H2="Screen Definition Print"
	D X0^XSXS
	S N=99,P=1,%S=1 F %J=1:1 D ^INPUT Q:X="END"!(X=%)
	W @P1 D ^OUTPUT I %OP=% G SCRDFP
	F I=3:1 S X=^SCREEN(0,I),T1(I)=$P(X,%,1) I X="END" Q
	u 0 w /c(19,22),@P1," Printing screen " u %OP
A01	D SCREEN I S1=S2 G END
	S S1=$O(^SCREEN(S1)) I S1="" G END
	G A01

END	D END^XSXS
	Q

	;Print a screen
SCREEN	u 0 w /c(36,22),S1,/el u %OP D HEAD^XSXS W "Screen No ",S1
	F I=1:1 S X=^SCREEN(S1,I) Q:X="END"  D FIELD W ! S N=N+1 I N>58 D HEAD^XSXS
	Q

	;Print one field
FIELD	W ?17,"Field No ",I
	F J=3:1:9 S U=$P(X,%,J-2) S:J=9 U=$P(X,%,7,99) W ?31,T1(J),?44,U,! S N=N+1
	W ?31,"Omit if...",?44,$g(^SCREEN(S1,I,"IF")),! S N=N+1
	Q
	;Validation
V1	I '$D(^SCREEN(X)) S ERR="DOES NOT EXIST"
	Q

SCRPRINT
SCRPRINT	;GRI,,,;02:15 PM  29 Mar 1999;
	;=======================================================
	; Screen Print Routine
	;
	; Copyright Graham R Irwin 1998
	;
	; Screens:       0, 1
	; Files updated: none
	; Subroutines:   X0^XSXS, ^INPUT, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	S H2="Screen Layout Print"
	D X0^XSXS
	S N=99,P=1,%S=1 F %J=1:1 D ^INPUT Q:X="END"!(X=%)
	W @P1 D ^OUTPUT,HEAD^XSXS I %OP=% G SCRPRINT
	F I=3:1 S X=^SCREEN(0,I),T1(I)=$P(X,%,1) I X="END" Q
	u 0 w /c(19,22),@P1," Printing screen " u %OP
A01	D SCREEN I S1=S2 G END
	S S1=$O(^SCREEN(S1)) I S1="" G END
	G A01

END	D END^XSXS
	Q

	;Print a screen
SCREEN	u 0 w /c(36,22),S1,/el u %OP W "Screen No ",S1,!! S N=N+2
	F I=1:1 S X=^SCREEN(S1,I) Q:X="END"  D FIELD
	W !! S N=N+2 I N>58 D HEAD^XSXS
	Q

	;Print one field
FIELD	S X1=$p(X,%,1) I $e(X1,1)="@" S X1=$$^NEWNAME(X1)
	W ?8,X1,?25 F J=1:1:$p(X,%,2) W "_"
	W ! S N=N+1 I N>58 D HEAD^XSXS
	Q

	;Validation
V1	I '$D(^SCREEN(X)) S ERR="DOES NOT EXIST"
	Q

SETCOL
SETCOL	;GRI,,,;01:53 PM  7 Dec 1995
	;=======================================================
	; Change Screen Colours
	;
	; Copyright Graham R Irwin 1993-95
	;
	; Screens:       4
	; Files updated: ^SYS
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	D X0^XSXS
	S X=^SYS("COL"),A1=$p(X,%,1),A2=$p(X,%,2),A3=$p(X,%,3),A4=$p(X,%,4),A5=$p(X,%,5),A6=+$p(X,%,6),ABC="Felicitas Est Parvus Canis Calidus  "
	F I=1:1:8 W /color(I,A6),ABC,I,?40,/color(I+8,A6),ABC,I+8,!

	W ! S %S=4 F %J=1:1 D D^INPUT Q:X="END"!(X[%)
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) Q
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S ^SYS("COL")=A1_%_A2_%_A3_%_A4_%_A5_%_A6,j="/color(",k=","_A6_")"
	S P1=j_A1_k,P2=j_A2_k,P3=j_A3_k,P4=j_A4_k,P5=j_A5_k
	Q

	;Amend
A01	W /C(0,13),/EF F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	G SETCOL:X[%,E02

CASE	S X=$zconvert(X,"U") Q

	;Validation - foreground colours
V01	I X'?1.2N!(X>15)!(X<1) S ERR="Must be a number from 1 to 15" Q
	I X=A6 S ERR="Must not be the same as background" Q
	w ?40,/color(X,A6),ABC
	Q
	;Background colour
V02	I X'?1N!(X>7) S ERR="Must be a number from 0 to 7" Q
	w ?40,/color(X,X),ABC
	Q

SETPTR
SETPTR	;GRI,,,;02:34 PM  1 Jul 2003
	;=======================================================
	; Change Printer
	;
	; Copyright Graham R Irwin 1994-2003
	;
	; Screens:       5
	; Files updated: ^SYS
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	S H2="Change Printer" D X0^XSXS
	S X=^SYS("PTR"),A1=$p(X,%,1),A2=$p(X,%,2),AMD=0
	S %S=5 F %J=1:1 D D^INPUT Q:X="END"!(X[%)
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) Q:'AMD  W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE Q:X="Y"  G E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S ^SYS("PTR")=A1_%_A2
	Q

	;Amend
A01	S AMD=1 W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G SETPTR:%J=1,A01
	G E02

	;Validation - Printer id
V01	Q:$D(ERR)  S ERR="Invalid ID"
	D CASE I X?1"LPT"1N,"1234"[$e(X,4) K ERR
	I X="WB" K ERR
	Q

CASE	S X=$zconvert(X,"U") Q

SETPW
SETPW	;GRI;11:44 AM  17 Aug 1994
	;=======================================================
	; Phoenix Accounts - Change Password
	;
	; Copyright Graham R Irwin 1993-94
	;
	; Screens:       none
	; Files updated: ^Access
	; Subroutines:   ^INPASSW
	;
	;=======================================================

	w #!! s a1=$g(^Access)
	i a1'="" W ?8,"Please enter the current password",! d ^INPASSW i X'=a1 g quit
q1	W !?8,"Enter the new password",!
	d ^INPASSW i x=""!(X=a1) g quit
	i x="*" g del
	i $l(x)<5 w *7,?25,"must be at least 5 characters" g q1
	s a2=X
q2	W !?8,"Enter the new password again for confirmation",!
	d ^INPASSW i x="" g quit
	i X'=a2 w *7 g q2

q3	w !?8,"OK to change? " r X d CASE i X="Y" s ^Access=a2 w !!?8,"Password Changed" q
	i X'="N" g q3
	g quit

del	w !?8,"Delete password? " r X d CASE i X="Y" k ^Access w !!?8,"Password Deleted" q
	i X'="N" g del

quit	w !!,*7,?8,"Password *NOT* Changed" h 2 q

CASE	S X=$zconvert(X,"U") q

SETWBD
SETWBD	;GRI,,,;07:54 AM  17 Oct 2007
	;=======================================================
	; Set Directories
	;
	; Copyright Graham R Irwin 1994-2003
	;
	; Screens:       6
	; Files updated: ^SYS
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	S H2="Set Directories" D X0^XSXS
	S A1=$g(^SYS("WBD"),"c:\windows\desktop\")
	S A2=$g(^SYS("WTF")),AMD=0
	S %S=6 F %J=1:1 D D^INPUT Q:X="END"!(X[%)
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) Q:'AMD  W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE Q:X="Y"  G E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S ^SYS("WBD")=A1,^SYS("WTF")=A2 I A2="" K ^SYS("WTF")
	Q

	;Amend
A01	S AMD=1 W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G SETWBD:%J=1,A01
	G E02

	;Validation - Desktop
V01	I $D(ERR) Q
	S ERR="Invalid directory"
	I X="*VI" S X="c:\users\public\desktop\"
	I X="*XP" S X="c:\docume~1\alluse~1\desktop\"
	I X="*98" S X="c:\windows\desktop\"
	I $e(X,$l(X))'="\" S X=X_"\"
	I X?1A1":\".E K ERR
	Q
	; - Write to File
V02	I X="" Q
	I $e(X,$l(X))'="\" S X=X_"\"
	Q

CASE	S X=$zconvert(X,"U") Q

UPDATE
UPDATE	;GRI;04:36 PM  10 Aug 2001;
	;=======================================================
	; Virgo Accounts - Release Update Routine (New)
	;
	; Copyright Graham R Irwin 1993-2001
	;
	; Screens:       none
	; Files updated: ^HELP, ^MENU, ^SCREEN, ^MENU1
	;                and any others within file gsave
	; Subroutines:   auto^%gload
	;
	;=======================================================

	k ^HELP,^MENU,^SCREEN,^MENU1
	s f="update.gsa"
	o 10:(file=f:mode="r")
	d auto^%gload(10,0,1,"A",0,0)
	u 0 w !!,"Update complete",*7 h 1
	q

XSAA
XSAA	;GRI,,,;09:41 AM  15 Apr 2002
	;=======================================================
	; Virgo Accounts - Maintain Client Details
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1000
	; Files updated: ^SC ^SEARCH
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	D X0^XSXS
	L ^SC
	S %S=1000,%J=1 D ^INPUT Q:X[%
	I $D(^SC(X)) G D01
	W /C(48,4),"* NEW RECORD *",!
	F %J=2:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSAA
E02	W /C(0,22),@P1,"Update, Matter, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSAA:X="Y",E02
	I X="A" G A01
	I X="U" D U01 G XSAA
	I X="M"!(X="Z") S PGM="XSAB",H2="Maintain Matter Details" D U01 G ^XSAB
	G E02

	;Update
U01	S ^SC(CX)=C1_%_C7_%_C8_%
	S ^SC(CX,"A")=C2_%_C3_%_C4_%_C5_%_C6_%
	S ^SC(CX,"N")=C9_%_C10_%_C11_%
	S C1L=$zconvert(C1,"L"),^SEARCH("SC",C1L,CX)=""
	I $d(C1X) S C1XL=$zconvert(C1X,"L") I C1L'=C1XL K ^SEARCH("SC",C1XL,CX)
	Q

	;Display
D01	S SC=^SC(X),C1=$P(SC,%,1),C7=$P(SC,%,2),C8=$P(SC,%,3)
	S SCA=^SC(X,"A"),C2=$P(SCA,%,1),C3=$P(SCA,%,2),C4=$P(SCA,%,3),C5=$P(SCA,%,4),C6=$P(SCA,%,5)
	S SCN=^SC(X,"N"),C9=$P(SCN,%,1),C10=$P(SCN,%,2),C11=$P(SCN,%,3)
	S C1X=C1,DEL='$D(^SM(CX)),AMD=0
D02	F %J=2:1 D D^INPUT Q:X="END"
D03	W /C(0,22),@P1,"Amend" W:DEL ", Delete" W " or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) G XSAA:'AMD W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSAA:X="Y",D03
	I X="A" G A01
	I X="D",DEL W @P1,"  Are you sure ? ",@P2 D ^GETX D CASE I X="Y" K ^SC(CX),^SEARCH("SC",$zconvert(C1,"L"),CX) G XSAA
	G D03

	;Amend
A01	S AMD=1 W /C(0,5),/EF F %J=2:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSAA:%J=2,A01
	G E02

QUIT	Q

	;Validation -----------------------------------
	;Client code
V01	I X["*" D AUTONUM
	I X'?.UN S ERR="Must be alpha-numeric"
	Q

AUTONUM	S (G0,G1)=$p(X,"*",1),G2=0
ANEXT	S G1=$o(^SC(G1)) I G1="" G AEND
	S G3=$p(G1,G0,2,9) S:G0="" G3=G1 I G3>G2 S G2=G3
	G ANEXT
AEND	S X=G0_(G2+1)
	Q

CASE	S X=$zconvert(X,"U") Q

XSAB
XSAB	;GRI,,,;07:10 PM  12 Apr 2002
	;=======================================================
	; Virgo Accounts - Matter Maintenance
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:	 1001 1021
	; Files updated: ^SM FILES.TXT
	;     (^SM ^SD ^ML ^CL ^SW ^TE killed if matter deleted)
	; Subroutines:	 X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================
 
	D X0^XSXS
	L ^SM
	S YES="Y",NO="N",M10="",NEW=0,DCG=$g(^SZ("DCG"))
 
	;Get client and matters codes
E01	S %S=1021 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSAB Q
	S %S=1001
	I $D(^SM(CX,MX)) G D01
 
	;Input
	W /C(48,5),"* NEW RECORD *",! S NEW=1
	F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSAB
 
	;Option prompt
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSAB:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02
 
	;Update
U01	S ^SM(CX,MX)=M1_%_M2_%_M3_%_M4_%_M5_%_M6_%_RX_"/"_M7B_%_M8_%_M9_%_M10_%_M14
	S ^SM(CX,MX,"N")=M11_%_M12_%_M13_%
	I SMO1'="" S ^SM(CX,MX,"O")=SMO1
	I NEW,$e(H0,5)="4" D AAOUT
	G XSAB
 
	;Display
D01	S SM=^SM(CX,MX),M1=$P(SM,%,1),M2=$P(SM,%,2),M3=$P(SM,%,3),M4=$P(SM,%,4),M5=$P(SM,%,5),M6=$P(SM,%,6),M7=$P(SM,%,7),M8=$P(SM,%,8),M9=$P(SM,%,9),M10=$P(SM,%,10),M14=$P(SM,%,11),RX=$p(M7,"/",1),M7B=$p(M7,"/",2)
	S SMN=^SM(CX,MX,"N"),M11=$P(SMN,%,1),M12=$P(SMN,%,2),M13=$P(SMN,%,3)
	S SMO1=$g(^SM(CX,MX,"O"))
	S DEL=1,AMD=0
	S A1=$g(^ML(CX,MX)),A2=$g(^CL(CX,MX)),A3=$g(^SW(CX,MX))
	I $p(A1,%,1)!$p(A1,%,2)!$p(A2,%,1)!$p(A2,%,2)!A3 S DEL=0
D02	F %J=1:1 D D^INPUT Q:X="END"
 
	;Option prompt
D03	W /C(0,22),@P1,"Amend" W:DEL ", Delete, Remove" W " or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) G XSAB:'AMD W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSAB:X="Y",D03
	I X="A" G A01
	I X="D",DEL W @P1,"  Are you sure ? ",@P2 D ^GETX,CASE I X="Y" K ^SM(CX,MX) D REM G XSAB
	I X="R"!(X="Z"),DEL W @P1,"  Remove transactions ? ",@P2 D ^GETX,CASE I X="Y" D REM G XSAB
	G D03
 
	;Amend
A01	S AMD=1 W /C(0,6),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSAB:%J=1,A01
	G E02

	;Remove all transactions
REM	K ^ML(CX,MX),^CL(CX,MX),^SW(CX,MX),^TE(CX,MX),^SD(CX,MX)
	Q
 
	;Validation ------------------------------------------
	;Client ledger cashbook
V01	I $D(ERR) Q
	I X<100 S ERR="Must be a client cashbook" Q
	S CB=^CB(X),CBA4=$p(CB,%,4),CLB1=+$g(^CL(CX,MX))
	I CBA4'=M5 S ERR="Client cashbook must be same status as matter" Q
	I %I,X'=M10,CLB1'=0 S ERR="C/L balance is not zero" W /C(0,22),@P1,"C/L balance is not zero - Are you sure ? ",/EL,@P2 R Y S Y=$zconvert(Y,"U") Q:Y'="YESSIR"  K ERR
	Q
	;Trust matter
V02	I %I,M5'=X,$D(^CL(CX,MX)) S ERR="Client account exists, cannot amend" Q
	S Q10=100 S:X="Y" Q10=101
	Q
	;Matter code
V03	I X="*" K ERR D AUTONUM Q
	I X=0 S ERR="Cannot be zero" Q
	Q

	;Auto numbering
AUTONUM	S G1=0,G2=""
ANEXT	S G2=$o(^SM(CX,G2)) I G2="" G AEND
	I G2>G1 S G1=+G2
	G ANEXT
AEND	S X=G1+1
	Q

CASE	S X=$zconvert(X,"U") Q

	;Create record for Amicus
AAOUT	O 10:(file="files.txt":mode="a") u 10
	S p2=$c(9)
	S SCA=^SC(CX),SCA1=$p(SCA,%,1),SCA2=$p(SCA,%,2),SCA3=$p(SCA,%,3)
	S SCB=$tr(^SC(CX,"A"),%," "),SCC=$tr(^SC(CX,"N"),%," ")
	W CX,p2,MX,p2,SCA1,p2,M1,p2,SMO1,p2,p2,SCA1,p2,SCB,p2,SCA2,p2,SCA3,p2,SCC,!
	c 10 q

XSAD
XSAD	;GRI,,,;12:49 PM  10 Jun 2002
	;=======================================================
	; Virgo Accounts - Client/Matter Enquiry
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:	 1000, 1001, 1003, 1031
	; Files updated: none
	; Subroutines:	 X0^XSXS, ^INPUT, ^GETX, ^OUTPUT, 
	;                HEAD^XSXS, END^XSXS, ^NEWNAME
	;
	;=======================================================

	D X0^XSXS S A1=""
	S %S=1003 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSAD Q

	I CX="*" G P01

	D GETC
	;Display client details
	W /C(0,4) S %S=1000 F %J=1:1 D D^INPUT Q:X="END"
E01	W /C(0,22),@P1,"Matters, Print or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="M"!(X="Z") G M01
	I X="P" G P40
	I X="Q"!(X=%) G XSAD
	G E01

	;Display matters
M01 	D S^XSXS G XSAD:X[%,N02:'X S MX=X
M02	D GETM W @P1,/C(0,6),/EF S %S=1001 F %J=1:1 D D^INPUT Q:X="END"
M03	W /C(0,22),@P1,"Next, Print or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="N" G N01
	I X="P" G P50
	I X="Q"!(X=%) G XSAD
	G M03

	;Next
N01	S MX=$O(^SM(CX,MX)) I MX'="" G M02
N02	W /C(0,22),@P1,"No More Matters. Press any key to continue ",/EL R *X R:'X *X G XSAD

	;Print all (ask detail/summary)
P01	S %S=1031 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSAD
	I A2="Y" W !?8,"WARNING: This could be a long report. You are advised to print it",!?8,"to the Spooler or to a File - if unsure please refer to the manual",!?8,"for details.",!
	D ^OUTPUT G:%OP[% XSAD S N=99,P=1,(CX,MX)=""
	I A2="N" G P10
	;print all in detail
P07	S CX=$O(^SC(CX)) I CX="" G END
	D CLINE S MX="" I N>55 D HEAD^XSXS
	I '$d(^SM(CX)) W "No Matters for this Client",!! S N=N+2 G P07
	W "List of Matters for this Client:",!! S N=N+2
P08	S MX=$O(^SM(CX,MX)) I MX="" G P07
	D MLINE
	G P08

	;Print all in summary
P10	S CX=$o(^SC(CX)) I CX="" G END
	D GETC
	I N>58 D HEAD^XSXS
	W !,"Client ",CX,?12,C1,?50,"----------",!! S N=N+3
	I '$d(^SM(CX)) W " No matters for this client",!! S N=N+2 I N>58 D HEAD^XSXS
P11	S MX=$o(^SM(CX,MX)) I MX="" G P10
	D GETM
	I A1="OPEN",$d(^SM(CX,MX,"C")) G P11
	W " Matter ",MX,?13,M1,!! S N=N+2 I N>58 D HEAD^XSXS
	G P11

	;Print a single client and its matters
P40	D ^OUTPUT G:%OP[% XSAD S N=99,P=1
	D CLINE S MX="" I N>55 D HEAD^XSXS
	I '$d(^SM(CX)) W "No Matters for this Client",!! S N=N+2 G END
	W "List of Matters for this Client:",!! S N=N+2
P41	S MX=$O(^SM(CX,MX)) I MX="" G END
	D MLINE
	G P41

	;Print a single matter
P50	D ^OUTPUT G:%OP[% XSAD S N=99,P=1
	D CLINE,MLINE

END	D END^XSXS
	G XSAD

	;Subroutines

CLINE	I N>55 D HEAD^XSXS
	S J=1 D GETC,WRC
	Q

MLINE	I N>55 D HEAD^XSXS
	I A1="OPEN",$d(^SM(CX,MX,"C")) Q
	S J=1 D GETM S S1=$g(^SR(RX)),S2="" I M7B'="" S S2=$g(^SA(M7B))
	D WRM
	Q

	;Get client details
GETC	S SC=^SC(CX),C1=$P(SC,%,1),C7=$P(SC,%,2),C8=$P(SC,%,3)
	S SCA=^SC(CX,"A"),C2=$P(SCA,%,1),C3=$P(SCA,%,2),C4=$P(SCA,%,3),C5=$P(SCA,%,4),C6=$P(SCA,%,5)
	S SCN=^SC(CX,"N"),C9=$P(SCN,%,1),C10=$P(SCN,%,2),C11=$P(SCN,%,3)
	Q

	;Get matter details
GETM	S SM=^SM(CX,MX),M1=$P(SM,%,1),M2=$P(SM,%,2),M3=$P(SM,%,3),M4=$P(SM,%,4),M5=$P(SM,%,5),M6=$P(SM,%,6),M7=$P(SM,%,7),M8=$P(SM,%,8),M9=$P(SM,%,9),M10=$P(SM,%,10),M14=$P(SM,%,11),RX=$p(M7,"/",1),M7B=$p(M7,"/",2)
	S SMN=^SM(CX,MX,"N"),M11=$P(SMN,%,1),M12=$P(SMN,%,2),M13=$P(SMN,%,3)
	S SMO1=$g(^SM(CX,MX,"O"))
	Q

	;Write client details
WRC	W "Client ",CX,?13,C1,?44,"-------------------------",!!
	W ?4,C2,!?4,C3,!?4,C4,!
	W ?4,C5,?44,"Telephone ",C7,!
	W ?4,C6,?44,"Facsimile ",C8,!!
	W ?4,C9,!?4,C10,!?4,C11,!! S N=N+12
	Q

	;Write matter details
WRM	W ?1,MX,?6,M1,?44,$$^NEWNAME("@F1@")," ",M3,?60,$$^NEWNAME("@F2@")," ",M4,!
	W ?44,$$^NEWNAME("@F3@")," ",M5,?60,"VATable ",M6,!
	W ?1,"Charge group ",RX,?17,S1,?44,$$^NEWNAME("@F7@")," ",M8,?59," ",$$^NEWNAME("@F8@")," ",M9,!
	W ?1,"Activity ",M7B,?17,S2,?44,"Action date ",M2,!
	W ?44,"Budget ",M14,!
	W ?1,M11,!
	W ?1,M12,!
	W ?1,M13,! S N=N+7
	Q

	;Validation - Client number
V01	Q:X="*"  D CASE I '$D(^SC(X)) S ERR="Does not exist"
	Q
	;Confirm
V02	I X'="ALL",X'="OPEN" S ERR="Wrong confirmation"
	Q

CASE	S X=$zconvert(X,"U") Q

XSAE
XSAE	;GRI,,,;08:28 AM  14 Jul 2002
	;=======================================================
	; Virgo Accounts - Matter Search
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1002, 1076
	; Files updated: ^WORK($j)
	; Subroutines:	 X0^XSXS, ^INPUT, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS S YES="Y"
	S %S=1002 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSAE Q
	S %S=1076 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSAE Q

	W /C(0,13),@P1
	K ^WORK($j) S (CX,MX)="",NC=0
G01	S CX=$o(^SM(CX)) I CX="" G H01
G02	S MX=$o(^SM(CX,MX)) I MX="" G G01
	S SM=^SM(CX,MX),M1=$p(SM,%,1),M3=$P(SM,%,3),M4=$P(SM,%,4),M5=$P(SM,%,5),M6=$P(SM,%,6),M7A=$p($P(SM,%,7),"/",1),M8=$P(SM,%,8),M9=$P(SM,%,9)
	I A9="Y",$d(^SM(CX,MX,"C")) G G02
	I A1'="",A1'=M4 G G02
	I A2'="",A2'=M3 G G02
	I A3'="",A3'=M5 G G02
	I A4'="",A4'=M8 G G02
	I A5'="",A5'=M9 G G02
	I A6'="",A6'=M7A G G02
	I A7'="",M1'[A7,^SM(CX,MX,"N")'[A7 G G02
	S NC=NC+1,SCA1=$p(^SC(CX),%,1),SMA1=$p(^SM(CX,MX),%,1)
	S ^WORK($j,NC)=CX_%_MX_%_SCA1_%_SMA1
	I NC<10 W NC,?5,CX,?10,MX,?16,SMA1,?48,SCA1,!
	I NC>9 S WX=NC-9 D NXTSCR
	G G02

	;Done searching
H01	I '$d(^WORK($j)) W /C(0,22),@P1,"No matters found. Press any key to continue " R *X R:'X *X G XSAE

	;Print
P01	D ^OUTPUT I %OP[% G XSAE
	S N=99,P=1 D HEAD^XSXS
	W "Matters with:"
	I A2'="" W ?15,$$^NEWNAME("@F1@")," = ",A2,! S N=N+1
	I A1'="" W ?15,$$^NEWNAME("@F2@")," = ",A1,! S N=N+1
	I A3'="" W ?15,$$^NEWNAME("@F3@")," = ",A3,! S N=N+1
	I A6'="" W ?15,"Charge group = ",A6,! S N=N+1
	I A4'="" W ?15,$$^NEWNAME("@F7@")," = ",A4,! S N=N+1
	I A5'="" W ?15,$$^NEWNAME("@F8@")," = ",A5,! S N=N+1
	W ! S N=N+1,WX=""
H02	S WX=$o(^WORK($j,WX)) I WX="" G J01
	S WK=^WORK($j,WX),CX=$p(WK,%,1),MX=$p(WK,%,2),SCA1=$p(WK,%,3),SMA1=$P(^SM(CX,MX),%,1)
	W "Client ",CX,?12,SCA1,!,"Matter ",MX,?12,SMA1
	I $d(^SM(CX,MX,"C")) W ?55,"Closed ",^SM(CX,MX,"C")
	W !! S N=N+3 I N>58 D HEAD^XSXS
	G H02

J01	D END^XSXS
	G XSAE

	;Move all up one line and redisplay
NXTSCR	W /C(0,13),/EF
NS1	S WX=$o(^WORK($j,WX)) I WX="" Q
	S WK=^WORK($j,WX),CX=$p(WK,%,1),MX=$p(WK,%,2),SCA1=$p(WK,%,3),SMA1=$P(WK,%,4)
	W WX,?5,CX,?10,MX,?16,SMA1,?48,SCA1,!
	G NS1

	;Validation - Descr/narr
V05	I A1="",A2="",A3="",A6="",A4="",A5="",X="" S ERR="Select one or more of the above options"
	Q

CASE	S X=$zconvert(X,"U") Q

XSAF
XSAF	;GRI,,,;11:23 AM  18 Mar 2003
	;=======================================================
	; Virgo Accounts - Matter Limits Report
	;
	; Copyright Graham R Irwin 1993-2003
	;
	; Screens:       1020
	; Files updated: ^SZ
	; Subroutines:   X0^XSXS, ^INPUT, H2DMY^DATE, DMY2H^DATE,
	;                ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	S AUTO=0

A00	D X0^XSXS
	S X=$g(^SZ("MLR")),A1=$p(X,%,1),A2=$p(X,%,2),A3=$p(X,%,3)

	I AUTO G A01
	S %S=1020 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSAF Q

A01	S (B2,%H)=$H+A2 D H2DMY^DATE
	D ^OUTPUT G:%OP[% XSAF S N=99,P=1 D HEAD^XSXS
	W "Matters with Action date < ",%DAT," and/or Disbursements+WIP > ",A3,"%",!! S N=N+2

	S (CX,MX)=""
G01	S CX=$o(^SM(CX)) I CX="" G J01
G02	S MX=$o(^SM(CX,MX)) I MX="" G G01
	S SM=^SM(CX,MX),ML=$g(^ML(CX,MX)),D1=$p(ML,%,1),W1=+$g(^SW(CX,MX)),L1=D1+W1,M1=$p(SM,%,1),(M2,%DAT)=$p(SM,%,2),M11=+$p(SM,%,11) D DMY2H^DATE S B1=%H,L2=M11*(100-A3/100)
	;print matter if Budget is not zero and disb+time > A3% of Budget
	I M11'=0,L1>L2 D WRITE G G02
	;print matter if Action date is not null and today+A2 >= Action date
	I M2'="",B1<B2 D WRITE G G02
	G G02

J01	D END^XSXS
	S ^SZ("MLR")=A1_%_A2_%_A3_%_+$H
	Q

WRITE	W "Client ",CX,?12,$p(^SC(CX),%,1),?50,"Disbursements",$j(D1,12,2),!
	W "Matter ",MX,?12,M1,?50,"Work-in-Prog.",$j(W1,12,2),!
	W ?50,"Budget value",$j(M11,13,2),!
	W ?50,"Remaining   ",$j(M11-D1-W1,13,2),!
	W ?50,"Action date    ",M2,!! S N=N+5 I N>56 D HEAD^XSXS
	Q

AUTO	S PGM="XSAF",H2="Matter Limits Report",AUTO=1 G A00

CASE	S X=$zconvert(X,"U") Q

XSAG
XSAG	;gri;09:29 AM  22 Feb 1999
	;=======================================================
	; Virgo Accounts - List Action Dates
	;
	; Copyright Graham R Irwin 1998
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, ^OUTPUT, HEAD^XSXS, END^XSXS,
	;                DMY2H^DATE
	;
	;=======================================================

	D X0^XSXS
	K ^WORK($j)
	D ^OUTPUT Q:%OP[%  S N=99,P=1 D HEAD^XSXS
	W "Action Date  Client  Matter",!! S N=N+2

	;Find matters with an Action Date
	S (CX,MX)=""
G02	S CX=$o(^SM(CX)) I CX="" G H01
G03	S MX=$o(^SM(CX,MX)) I MX="" G G02
	S SM=$g(^SM(CX,MX)),%DAT=$p(SM,%,2)
	I %DAT'="" D DMY2H^DATE S ^WORK($j,%H,CX,MX)=%DAT
	G G03

	;Now print them
H01	S (X1,CX,MX)=""
H02	S X1=$o(^WORK($j,X1)) I X1="" G J01
H03	S CX=$o(^WORK($j,X1,CX)) I CX="" G H02
H04	S MX=$o(^WORK($j,X1,CX,MX)) I MX="" G H03
	S WK=^WORK($j,X1,CX,MX)
	W WK,?14,CX,?22,MX
	I X1<$H W ?32,"** overdue **"
	W ! S N=N+1 I N>56 D HEAD^XSXS
	G H04

J01
	D END^XSXS
	Q

XSAH
XSAH	;GRI,,,;01:56 PM  24 Jul 2002;
	;=======================================================
	; Virgo Accounts - Detailed Matter Listing
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1002, 1076, 1081
	; Files updated: ^WORK($j)
	; Subroutines:	 X0^XSXS, ^INPUT, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS S YES="Y",NO="N"
	S %S=1002 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSAH Q
	S %S=1076 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSAH Q
	S %S=1081 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSAH Q

	D ^OUTPUT I %OP[% G XSAH
	S N=99,P=1 D HEAD^XSXS
	W "Matters with:"
	I A2'="" W ?15,$$^NEWNAME("@F1@")," = ",A2,! S N=N+1
	I A1'="" W ?15,$$^NEWNAME("@F2@")," = ",A1,! S N=N+1
	I A3'="" W ?15,$$^NEWNAME("@F3@")," = ",A3,! S N=N+1
	I A6'="" W ?15,"Charge Group = ",A6,! S N=N+1
	I A4'="" W ?15,$$^NEWNAME("@F7@")," = ",A4,! S N=N+1
	I A5'="" W ?15,$$^NEWNAME("@F8@")," = ",A5,! S N=N+1

	S (CX,MX)=""
G01	S CX=$o(^SM(CX)) I CX="" G J01
G02	S MX=$o(^SM(CX,MX)) I MX="" G G01
	S SCA=^SC(CX),SCA1=$p(SCA,%,1),SCA2=$p(SCA,%,2),SCA3=$p(SCA,%,3)
	S SCB=^SC(CX,"A"),SCB1=$p(SCB,%,1),SCB2=$p(SCB,%,2),SCB3=$p(SCB,%,3),SCB4=$p(SCB,%,4),SCB5=$p(SCB,%,5)
	S SCN=^SC(CX,"N"),SCN1=$p(SCN,%,1),SCN2=$p(SCN,%,2),SCN3=$p(SCN,%,3)
	S SMA=^SM(CX,MX),SMA1=$p(SMA,%,1),SMA2=$p(SMA,%,2),SMA3=$p(SMA,%,3),SMA4=$p(SMA,%,4),SMA5=$p(SMA,%,5),SMA6=$p(SMA,%,6),SMA7A=$p($p(SMA,%,7),"/",1),SMA8=$p(SMA,%,8),SMA9=$p(SMA,%,9)
	S SMN=^SM(CX,MX,"N"),SMN1=$p(SMN,%,1),SMN2=$p(SMN,%,2),SMN3=$p(SMN,%,3)
	I A9="Y",$d(^SM(CX,MX,"C")) G G02
	I A1'="",A1'=SMA4 G G02
	I A2'="",A2'=SMA3 G G02
	I A3'="",A3'=SMA5 G G02
	I A4'="",A4'=SMA8 G G02
	I A5'="",A5'=SMA9 G G02
	I A6'="",A6'=SMA7A G G02

	I A10="Y" G G10
	W !,"Client ",CX,?12,SCA1,?67,"=========="
	W !,"Address",?12,SCB1,! S N=N+3
	I SCB2'="" W ?12,SCB2,! S N=N+1
	I SCB3'="" W ?12,SCB3,! S N=N+1
	I SCB4'="" W ?12,SCB4,! S N=N+1
	I SCB5'="" W ?12,SCB5,! S N=N+1
	W "Phone",?12,SCA2,! S N=N+1
	W "Fax",?12,SCA3,! S N=N+1 I N>55 D HEAD^XSXS
	W "Comments",?12,SCN1,! S N=N+1
	I SCN2'="" W ?12,SCN2,! S N=N+1
	I SCN3'="" W ?12,SCN3,! S N=N+1
	I N>55 D HEAD^XSXS
	W !,"........",?12,"Matter ",MX,?24,SMA1,!
	W ?12,"Action Date",?24,SMA2,!
	W ?12,"Comments",?24,SMN1,! S N=N+4
	I SMN2'="" W ?24,SMN2,! S N=N+1
	I SMN3'="" W ?24,SMN3,! S N=N+1
	I N>55 D HEAD^XSXS
	G G02

	;Client only
G10	W !,SCA1
	W !,SCB1,! S N=N+3
	I SCB2'="" W SCB2,! S N=N+1
	I SCB3'="" W SCB3,! S N=N+1
	I SCB4'="" W SCB4,! S N=N+1
	I SCB5'="" W SCB5,! S N=N+1
	W SCN1,! S N=N+1
	I SCN2'="" W SCN2,! S N=N+1
	I SCN3'="" W SCN3,! S N=N+1
	I N>55 D HEAD^XSXS
	G G02

J01	D END^XSXS
	Q

	;Validation
V05	I A1="",A2="",A3="",A6="",A4="",X="" S ERR="Select one or more of the above options"
	Q

XSAJ
XSAJ	;GRI,,,;04:53 PM  4 Jun 2003;
	;=======================================================
	; Virgo Accounts - Close Matter
	;
	; Copyright Graham R Irwin 1999-2003
	;
	; Screens:	 1071
	; Files updated: ^SM
	; Subroutines:	 X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================
 
	D X0^XSXS
 
E01	S %S=1071 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSAJ Q
 
	;Option prompt
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSAJ:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02
 
	;Update
U01	S SMA=^SM(CX,MX),SMA1=$p(SMA,%,1),SMA299=$p(SMA,%,2,99)
	I D1="*" K ^SM(CX,MX,"C") S L=$l(SMA1) I $e(SMA1,L)="$" S SMA1=$e(SMA1,1,L-2)
	I D1'="*" S ^SM(CX,MX,"C")=D1,SMA1=$e(SMA1,1,38)_" $"
	S ^SM(CX,MX)=SMA1_%_SMA299
	G XSAJ

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSAJ:%J=1,A01
	G E02
 
	;Validation ------------------------------------------
	;Matter code
V02	S E1=$g(^SM(CX,X,"C"),"*")
	Q
	;Date
V03	I X="*" K ERR
	Q

CASE	S X=$zconvert(X,"U") Q

XSAK
XSAK	;GRI,,,;03:38 PM  12 Jan 2001;
	;=======================================================
	; Virgo Accounts - Opened/Closed Matters List
	;
	; Copyright Graham R Irwin 1993-2001
	;
	; Screens:       1018, 1073, 1041, 1070
	; Files updated: ^WORK($j)
	; Subroutines:	 X0^XSXS, ^INPUT, DMY2H^DATE, ^OUTPUT,
	;                HEAD^XSXS, END^XSXS, ^NEWNAME
	;
	;=======================================================

	D X0^XSXS
	;from date & to date
	S %S=1018 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSAK Q
	;opened/closed
	S %S=1073 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSAK
	;option
	W !?8,@P1,"Do you wish to list matters by:-"
	W !!?8,"A - ",$$^NEWNAME("@F7@")
	W !?8,"B - ",$$^NEWNAME("@F8@")
	W !?8,"C - Client",!!
	S %S=1041 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSAK
	;legal aid
	S %S=1070 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSAK
	I A4="" S A4="YN"

	W /C(0,22),@P1,"Sorting, please wait ..."
	;walk matter file and create work file
	K ^WORK($j) S (CX,MX)=""
M01	S CX=$o(^SM(CX)) I CX="" G P00
M02	S MX=$o(^SM(CX,MX)) I MX="" G M01
	I '$d(^SM(CX,MX,X3)) G M02
	S SMA=^SM(CX,MX),SMA8=$p(SMA,%,8),SMA9=$p(SMA,%,9),SMA3=$p(SMA,%,3)
	I A4'[SMA3 G M02
	S:A3="A" X1=SMA8 S:A3="B" X1=SMA9 S:A3="C" X1=CX I X1="" S X1="null"
	S %DAT=^SM(CX,MX,X3) D DMY2H^DATE S D0=%H
	I D1'>D0,D0'>D2 S ^WORK($j,X1,CX,MX)=""
	G M02

	;Print
P00	D ^OUTPUT G:%OP[% XSAK
	S N=99,P=1 D HEAD^XSXS
	W "Matters " W:X3="O" "open" W:X3="C" "clos" W "ed between ",A1," and ",A2," - ",$$^NEWNAME("@F1@")," = " W:$l(A4)=1 A4 W:$l(A4)=2 "Y or N" W !! S N=N+2
	S (X1,CX,MX)=""
P01	S X1=$o(^WORK($j,X1)) I X1="" G P99
	I A3="A" W $$^NEWNAME("@F7@")," = ",X1
	I A3="B" W $$^NEWNAME("@F8@")," = ",X1
	I A3="C" W "Client ",X1
	W !! S N=N+2
P02	S CX=$o(^WORK($j,X1,CX)) I CX="" G P01
P03	S MX=$o(^WORK($j,X1,CX,MX)) I MX="" G P02
	S C1=$p(^SC(CX),%,1),M1=$p(^SM(CX,MX),%,1)
	W CX,?6,C1,!,MX,?6,M1,?55 W:X3="O" "Open" W:X3="C" "Clos" W "ed ",^SM(CX,MX,X3),!!
	S N=N+3 I N>56 D HEAD^XSXS
	G P03

P99	D END^XSXS
	Q

	;Validation - Opened/closed
V01	I $d(ERR) Q
	D CASE I "OC"'[X S ERR="Must be 'O' or 'C'"
	Q

CASE	S X=$zconvert(X,"U") Q

XSAL
XSAL	;GRI,,,;09:23 AM  26 Jun 2000;
	;=======================================================
	; Virgo Accounts - Matters With No Transactions
	;
	; Copyright Graham R Irwin 2000
	;
	; Screens:       none
	; Files updated: 
	; Subroutines:	 X0^XSXS, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	D ^OUTPUT I %OP[% Q
	S N=99,P=1 D HEAD^XSXS

	S (CX,MX)=""
M01	S CX=$o(^SM(CX)) I CX="" G P99
M02	S MX=$o(^SM(CX,MX)) I MX="" G M01

	I $d(^ML(CX,MX))>9 G M02
	I $d(^CL(CX,MX))>9 G M02
	I $d(^SW(CX,MX))>9 G M02
	W CX,"/",MX,?10,$e($p(^SC(CX),%,1),1,32)," - ",$e($p(^SM(CX,MX),%,1),1,32),!! S N=N+1 I N>60 D HEAD^XSXS
	G M02

P99	D END^XSXS
	Q

XSAM
XSAM	;GRI,,,;02:08 PM  6 Mar 2001;
	;=======================================================
	; Virgo Accounts - Matter Progress Report
	;
	; Copyright Graham R Irwin 1993-2001
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, ^INPUT, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS

A01	D ^OUTPUT G:%OP[% XSAM S N=99,P=1 D HEAD^XSXS
	W "Client/Matter    Budget        W-I-P        Disbs    Remaining",!! S N=N+2

	S (CX,MX)=""
G01	S CX=$o(^SM(CX)) I CX="" G J01
G02	S MX=$o(^SM(CX,MX)) I MX="" G G01
	S SM=^SM(CX,MX),M1=$p(SM,%,1),M11=+$p(SM,%,11)
	S ML=$g(^ML(CX,MX)),D1=$p(ML,%,1),W1=+$g(^SW(CX,MX)),L1=D1+W1
	I M11=0 G G02
	W CX,"/",MX,?10,$j(M11,13,2),$j(D1,13,2),$j(W1,13,2),$j(M11-L1,13,2),!! S N=N+2 I N>58 D HEAD^XSXS
	G G02

J01	D END^XSXS
	Q

XSBA
XSBA	;GRI,,,;09:52 AM  16 Feb 2003
	;=======================================================
	; Virgo Accounts - Post Disbursement Item
	;
	; Copyright Graham R Irwin 1993-2003
	;
	; Screens:       1004
	; Files updated: ^ML ^CB ^SY ^SV ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS
	;                UPAB^XSXS, CHEQUE
	;
	;=======================================================

	D X0^XSXS
	S E5="Disbursement",TT="DIS",VAT=^SZ("VAT")/100,ONE=$g(^SZ("DOC"),1)
	S %S=1004 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBA G QUIT
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSBA:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S E5=D5_" - "_$p(^SC(CX),%,1),E5=$e(E5,1,70)
	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_CX_%_MX_%_DX_%_D2_%_D3_%_%_%_%_D4_%_E5
	S ^ML(CX,MX,YX)=D1_%_%_D2_%_TT_%_D4_%_%_D5_%
	S ML=$G(^ML(CX,MX)),B1=$P(ML,%,1)+D2,B2=$P(ML,%,2,9),^ML(CX,MX)=B1_%_B2

	S ^CB(DX,YX)=D1_%_E2_%_%_TT_%_D4_%_E5_%_"N"_%
	S CB=^CB(DX),B1=$P(CB,%,1,5),B6=$P(CB,%,6)-E2,^CB(DX)=B1_%_B6_%

	;Update VAT records
	I D3="*"!(^SZ("VCB")="X") G NV1
	S SV=$G(^SV),V1=$P(SV,%,1)+D2,V2=$P(SV,%,2)+D3,V3=$P(SV,%,3,99),^SV=V1_%_V2_%_V3,^SV("P"_YX)="Disbm't"_%_D1_%_D2_%_D3
	D UPAB^XSXS("L040",-D3,D1,TT,D4,E5)

NV1	D UPAB^XSXS("A120",D2,D1,TT,D4,E5)
	D UPAB^XSXS("N060",D2,D1,TT,D4,E5)
	D UPAB^XSXS("A130",-E2,D1,TT,D4,E5)
	;Write cheque
	D ^CHEQUE(DX,CX,MX,D1,E2,D4,E5,TT)
	G XSBA

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBA:%J=1,A01
	G E02

QUIT	
	Q

	;Validation - Net amount
V01	S M6=$P(^SM(CX,MX),%,6),E1=$J(X*VAT*(M6="Y"),0,2)
	Q
	;VAT amount
V02	I X="*" K ERR
	I X<0,D2>0 S ERR="Must be a positive amount" Q
	I X>0,D2<0 s ERR="Must be a negative amount" Q
	S E2=$J(D2+X,0,2)
	Q
	;Cashbook
V03	Q:$D(ERR)  I X>99 S ERR="Must be an office cashbook" Q
	S B6=$J($P(^CB(X),%,6),0,2)
	Q

CASE	S X=$zconvert(X,"U") Q

XSBB
XSBB	;GRI,,,;03:22 AM  22 May 1998
	;=======================================================
	; Virgo Accounts - Write Off Disbursement
	;
	; Copyright Graham R Irwin 1993-98
	;
	; Screens:       1005
	; Files updated: ^ML ^SY ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	D X0^XSXS
	S E5="Disbursement Write-off",TT="DSW",VAT=^SZ("VAT")/100
	S %S=1005 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBB Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSBB:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S E5=D5_" - "_$p(^SC(CX),%,1),E5=$e(E5,1,70)
	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_CX_%_MX_%_%_D2_%_%_%_%_%_D4_%_E5
	S ^ML(CX,MX,YX)=D1_%_D2_%_%_TT_%_D4_%_%_D5_%
	S ML=$G(^ML(CX,MX)),B1=$P(ML,%,1)-D2,B2=$P(ML,%,2,9),^ML(CX,MX)=B1_%_B2
	D UPAB^XSXS("A120",-D2,D1,TT,D4,E5)
	D UPAB^XSXS("N080",D2,D1,TT,D4,E5)
	D UPAB^XSXS("E280",D2,D1,TT,D4,E5)
	G XSBB

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBB:%J=1,A01
	G E02

	;Validation - Amount
V01	S ML=$g(^ML(CX,MX)),B1=$p(ML,%,1)
	I X>B1 W @P3,?48,"Disbursement Balance in Credit",*7
	Q

CASE	S X=$zconvert(X,"U") Q

XSBC
XSBC	;GRI,,,;04:54 PM  28 Jun 2013
	;=======================================================
	; Virgo Accounts - Post Bill
	;
	; Copyright Graham R Irwin 1998-2003
	;
	; Screens:       1006
	; Files updated: ^ML ^SB ^SD ^SU ^SV ^SW ^SY ^AB ^AY
	;                ^WORK($j) and DOS file XSBILL.SPS
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	; B0 - Default bill no		B1 - Bill date
	; B2 - Time billed		B3 - Time removed
	; B4 - Disbursements billed	B5 - VAT amount
	; E2 - Gross amount		G2 - Net billed (=B2+B4)
	; L0 - Default disbs billed	L1 - Disbursements written off

	D X0^XSXS
	S E5="Bill",E8="N",TT="BIL",VAT=^SZ("VAT")/100,B0=$G(^SB)+1
	S %S=1006 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBC G QUIT

	;Get all disbursements
	S X3="",WX=1 K ^WORK($j)
	S (L1,WK7)=0 I $g(^SZ("WOD"),"Y")="Y" S L1=+$g(^ML(CX,MX)),WK7=1
G01	S X3=$O(^ML(CX,MX,X3)) I X3="" G E02
	S ML=^ML(CX,MX,X3),M4=$P(ML,%,4)
	I "DIS,DSW,DSP,DSA,TOC,COD,OMT,N2D"[M4 S ^WORK($j,WX)=X3_%_$P(ML,%,1,5)_%_WK7_%_$P(ML,%,7,99),WX=WX+1
	G G01

E02	W /C(0,22),@P1,"Disbursements, Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSBC:X="Y",E02
	I X="A" G A01
	I X="D"!(X="Z") G M01
	I X="U" G U01
	G E02

	;Update...
U01	S F5=C5_" - "_$p(^SC(CX),%,1),F5=$e(F5,1,70)
	;... Daybook
	D YX^XSXS S ^SY(DAT,TT,YX)=B1_%_CX_%_MX_%_%_G2_%_B5_%_%_%_%_BX_%_F5
	;... Matter Ledger (bills)
	S ^ML(CX,MX,YX)=B1_%_%_E2_%_TT_%_BX_%_%_C5_%
	S ML=$G(^ML(CX,MX)),M1=$P(ML,%,1),M2=$P(ML,%,2)+E2,^ML(CX,MX)=M1_%_M2
	;... Bill Register
	I BX'=0 S ^SB(BX)=B1_%_CX_%_MX_%_B2_%_B4_%_B5_%_B3_%_E2_%_"U"_%_E8_%
	I BX'<B0 S ^SB=BX
	I BX'=0 S ^SU(BX)=""
	;... VAT records if NOT cash basis
	I ^SZ("VCB")="N" S SV=$G(^SV),V1=$P(SV,%,1),V2=$p(SV,%,2),V3=$P(SV,%,3)+G2,V4=$P(SV,%,4)+B5,^SV=V1_%_V2_%_V3_%_V4,^SV(YX)=BX_%_B1_%_G2_%_B5
	;... Nominal
	S AX="L040" S:^SZ("VCB")="Y" AX="L140" D UPAB^XSXS(AX,B5,B1,TT,BX,F5)
	D UPAB^XSXS("A110",E2,B1,TT,BX,F5)
	S T2=-L1 S:L1<B4 T2=-B4 D UPAB^XSXS("A120",T2,B1,TT,BX,F5)
	D UPAB^XSXS("I020",B2,B1,TT,BX,F5)
	S T1=L1-B4 S:L1<B4 T1=0 D UPAB^XSXS("E280",T1,B1,TT,BX,F5)
	D UPAB^XSXS("N090",E2,B1,TT,BX,F5)
	D UPAB^XSXS("N020",-B3,B1,TT,BX,F5)
	D UPAB^XSXS("N070",B4,B1,TT,BX,F5)
	D UPAB^XSXS("N040",B2,B1,TT,BX,F5)
	;... Billed Disbs and Matter Ledger (disbs)
	S WX=""
P01	S WX=$O(^WORK($j,WX)) I WX="" G P04
	S ML=^WORK($j,WX),X3=$P(ML,%,1),M6=$P(ML,%,7)
	I M6=1 S ^SD(CX,MX,BX,X3)=$P(ML,%,2,99) K ^ML(CX,MX,X3)
	G P01
P04	S ML=^ML(CX,MX),M1=$P(ML,%,1)+T2,M2=$P(ML,%,2),^ML(CX,MX)=M1_%_M2_%
	I L1<B4 S ^ML(CX,MX,YX+.1)=B1_%_(B4-L1)_%_%_"DSA"_%_BX_%_%_"Anticipated disb"_%
	;... Time Records
	S SW=$g(^SW(CX,MX)),W1=$p(SW,%,1)-B3,W2=$p(SW,%,2,99) K ^SW(CX,MX)
	I W1 S ^SW(CX,MX,1,"!C")=%_%_W1,^SW(CX,MX)=W1_%_W2

	;Write merge file
	zetrap ERR^XSBC
	o 10:(file="xsbill.sps":mode="a") u 10
	s SCA=^SC(CX,"A") w $p(^SC(CX),%,1),%,$p(SCA,%,1),%,$p(SCA,%,2),%,$p(SCA,%,3),%,$p(SCA,%,4),%,$p(SCA,%,5),%,B1,%,BX,%,B2,%,B4,%,VAT*100,%,B5,%,E2,!
	c 10 u 0
	G XSBC

	;Error trap if unable to write to XSBILL.SPS
ERR	i $zerr=219!($zerr=302) u 0 w *7,!,"Warning: Merge File Not Created - " S x=$$^Cont(0) G XSBC
	zquit

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBC:%J=1,A01
	G E02

	;Disbursements (second screen) -----------------------------
	;Display disbursements
M01	S WX="" W /C(0,4),/EF,@P1,"Item Date",?16,"Description",?63,"Amount",?72,"W/O?",!!
M02	S WX=$O(^WORK($j,WX)) I WX=""!($Y>19) G M09
M03	S ML=^WORK($j,WX),D1=$P(ML,%,2),D2=$P(ML,%,3),D3=$P(ML,%,4),D4=$P(ML,%,5),D5=$P(ML,%,6),D6=$P(ML,%,7),D7=$P(ML,%,8),F2=D3-D2
	W:D6 @P5 W WX,?4,D1,?16,$e(D7,1,40),?60,$J(F2,10,2),?73,$S(D6:"Yes",'D6:"No"),@P1,!
	G M02
M09	W /C(0,22),@P1,"Mark/unmark, Next, Update or Back ? ",/EL,@P2 D ^GETX,CASE
	I X="U" G U01
	I X="N" G N01
	I X="B" G B01
	I X="M"!(X="Z") G S01
	G M09

	;Back
B01	W /C(0,4),/EF F %J=1:1 D D^INPUT Q:X="END"
	G E02

	;Select
S01	W @P1,"  Mark/unmark item ",@P2 D ^GETX I $G(^WORK($j,+X))="" G M09
	S WK=^WORK($j,X),W1=$P(WK,%,1,2),W3=$P(WK,%,3),W4=$P(WK,%,4),W5=$P(WK,%,5,6),W7=$P(WK,%,7),W8=$P(WK,%,8,99)
	S W7=1-W7,F2=W3-W4 S:W7 L1=L1-F2 S:'W7 L1=L1+F2
	S ^WORK($j,X)=W1_%_W3_%_W4_%_W5_%_W7_%_W8_%
	G M01

	;Next
N01	W /C(0,6),/EF,@P1 G M03:WX'="",M02

QUIT	Q

	;Validation routines ---------------------------------------
	;Matter code
V01	Q:$D(ERR)  S SM=^SM(CX,X),M6=$P(SM,%,6),SW=$J($G(^SW(CX,X)),0,2)
	S ML=$P($G(^ML(CX,X)),%,1),L0=$J($P(ML,%,1),0,2)
	Q
	;VAT amount
V02	I BX=0,X>0 S ERR="Must be zero" Q
	S E2=$J(G2+X,0,2)
	Q
	;Disbursements
V03	Q:$D(ERR)  I BX=0,X>0 S ERR="Must be zero" Q
	S G2=B2+X,E3=$J(G2*VAT*(M6="Y"),0,2)
	Q
	;Fees billed
V04	I BX=0,X>0 S ERR="Must be zero" Q
	Q
	;Time removed
V05	I $D(ERR) Q
	I X>SW W @P3,?48,"Exceeds WIP booked"
	Q

CASE	S X=$zconvert(X,"U") Q

XSBD
XSBD	;GRI,,,;09:16 AM  5 Mar 2003
	;=======================================================
	; Virgo Accounts - Post Bill Receipt
	;
	; Copyright Graham R Irwin 1993-94
	;
	; Screens:       1007, 1046, 1047
	; Files updated: ^ML ^CB ^CL ^SB ^SU ^SY ^SV ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	; R2 - Net amount
	; R3 - VAT amount
	; E3 - Gross amount

	D X0^XSXS
	S E5="Bill Receipt",TT="BPY",ONE=$g(^SZ("DOC"),1)
	S %S=1046,%J=1 D ^INPUT Q:X="END"!(X[%)
	I X[% G QUIT
	S %S=1047 F %J=1:1 D D^INPUT Q:X="END"
	S %S=1007 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSBD
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSBD:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update...
U01	S E5=C5_" - "_$p(^SC(CX),%,1),E5=$e(E5,1,70)
	;...daybook
	D YX^XSXS S ^SY(DAT,TT,YX)=R1_%_CX_%_MX_%_DX_%_R2_%_R3_%_%_%_%_BX_%_E5

	;...client ledger if appropriate
	I DX>99 S ^CL(CX,MX,YX)=R1_%_%_E3_%_TT_%_BX_%_C5_%,CL=$G(^CL(CX,MX)),C1=$P(CL,%,1)+E3,C2=$P(CL,%,2),^CL(CX,MX)=C1_%_C2_% G NV1

	;...or matter ledger & bill register
	S ^ML(CX,MX,YX)=R1_%_E3_%_%_TT_%_BX_%_%_C5_%
	S ML=$G(^ML(CX,MX)),M1=$P(ML,%,1),M2=$P(ML,%,2)-E3,^ML(CX,MX)=M1_%_M2
	S B8=B8-E3 S:B8<0 B8=0 S B9="P" S:(B4+B5+B6)=B8 B9="U" S:'B8 B9="F"
	S ^SB(BX)=B1_%_CX_%_MX_%_B4_%_B5_%_B6_%_B7_%_B8_%_B9_%_B10_%
	I B9="F" K ^SU(BX)

	;...VAT records if cash basis
	I ^SZ("VCB")'="Y" G NV1
	S SV=$G(^SV),V1=$P(SV,%,1),V2=$p(SV,%,2),V3=$P(SV,%,3)+R2,V4=$P(SV,%,4)+R3,^SV=V1_%_V2_%_V3_%_V4,^SV(YX)=BX_%_R1_%_R2_%_R3
	D UPAB^XSXS("L040",R3,R1,TT,BX,E5)
	D UPAB^XSXS("L140",-R3,R1,TT,BX,E5)

	;...cashbook
NV1	S ^CB(DX,YX)=R1_%_%_E3_%_TT_%_BX_%_E5_%_%
	S CB=^CB(DX),C1=$P(CB,%,1,5),C6=$P(CB,%,6)+E3,^CB(DX)=C1_%_C6_%
	;...cashbook control account
	S AX="A130" I DX>99 S AX="L010" S:$P(^SM(CX,MX),%,5)="Y" AX="L020"
	D UPAB^XSXS(AX,E3,R1,TT,BX,E5)
	I DX<100 S AX="A110" D UPAB^XSXS(AX,-E3,R1,TT,BX,E5)

	G XSBD

	;Amend
A01	W /C(0,4),/EF S %S=1046,%J=1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBD
	S %S=1047 F %J=1:1 D D^INPUT Q:X="END"
	S %S=1007 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	G A01:X[%,E02

QUIT	
	Q

	;Validation - Bill number
V01	Q:$D(ERR)  S SB=^SB(X),B1=$P(SB,%,1),CX=$P(SB,%,2),MX=$P(SB,%,3),B4=$P(SB,%,4),B5=$P(SB,%,5),B6=$J($P(SB,%,6),0,2),B7=$P(SB,%,7),B8=$J($P(SB,%,8),0,2),B9=$P(SB,%,9),B10=$P(SB,%,10),E2=$J(B4+B5,0,2),BV=B8-B4-B5-B6
	;I B9="F"!'B8 S ERR="Bill has been paid" Q
	I B10="Y" W @P3,?48,"Includes Unpaid Prof Disb"
	Q
	;Net amount
V02	I X>0,B9="F"!'B8 S ERR="Must be a negative amount" Q
	I X<0,X<BV S ERR="Cannot exceed amount paid" Q
	Q
	;VAT amount
V03	Q:$D(ERR)  I X>B6 S ERR="Cannot exceed original VAT amount" Q
	I X>0,R2<0 S ERR="Must be a negative amount" Q
	S E3=$J(R2+X,0,2)
	I X<0,E3<BV S ERR="Gross amount cannot exceed amount paid" Q
	Q
	;Gross amount
V09	I E3>B8 W @P3,?48,"Exceeds outstanding amount"
	Q
	;Cashbook
V04	Q:$D(ERR)  I E3'>B8 Q
	I X<100 S ERR="Overpayment must be to a client cashbook" Q
	S K=$P(^SM(CX,MX),%,10),ERR="No client cashbook defined for this matter" Q:K=""  Q:'$D(^CB(K))  K ERR
	I K'=X S ERR="Must be same as client cashbook"
	Q

CASE	S X=$zconvert(X,"U") Q

XSBE
XSBE	;GRI,,,;02:07 PM  18 Jun 2004
	;=======================================================
	; Virgo Accounts - Post Bill Credit
	;
	; Copyright Graham R Irwin 1993-2004
	;
	; Screens:       1046, 1047, 1008
	; Files updated: ^ML ^SB ^SD ^SU ^SV ^SW ^SY ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	; R2 - Net amount		R3 - VAT amount
	; E3 - Gross amount
	; T0 - Disbs written off	B5 - Disbursements billed
	; B4 - Time billed		B7 - Time removed
	; B8 - O/s amount		B9 - Bill status

	S E5="Bill Credit",TT="BCR",NO="N"
	D X0^XSXS
	S %S=1046,%J=1 D ^INPUT Q:X="END"!(X[%)
	I X[% Q
	S %S=1047 F %J=1:1 D D^INPUT Q:X="END"
	S %S=1008 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSBE
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSBE:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update...
U01	S E5=C5_" - "_$p(^SC(CX),%,1),E5=$e(E5,1,70)
	;...daybook
	D YX^XSXS S ^SY(DAT,TT,YX)=R1_%_CX_%_MX_%_%_R2_%_R3_%_%_%_%_BX_%_E5
	;...matter ledger
	S ^ML(CX,MX,YX)=R1_%_E3_%_%_TT_%_BX_%_%_C5_%
	S ML=$G(^ML(CX,MX)),M1=$P(ML,%,1),M2=$P(ML,%,2)-E3,M3=$P(ML,%,3,9),^ML(CX,MX)=M1_%_M2_%_M3
	;...bill register
	S B8=B8-E3 S:B8<0 B8=0
	S B9="P" I B8=0 S B9="F" I R6="Y" S B9="C" ;order is important!
	S ^SB(BX)=B1_%_CX_%_MX_%_B4_%_B5_%_B6_%_B7_%_B8_%_B9_%_B10_%
	I "FC"[B9 K ^SU(BX)
	;...VAT records if NOT cash basis
	I ^SZ("VCB")="N" S SV=$G(^SV),V1=$P(SV,%,1),V2=$p(SV,%,2),V3=$P(SV,%,3)-R2,V4=$P(SV,%,4)-R3,^SV=V1_%_V2_%_V3_%_V4,^SV(YX)=BX_%_R1_%_-R2_%_-R3
	;...nominal ledger
	S AX="L040" S:^SZ("VCB")="Y" AX="L140" D UPAB^XSXS(AX,-R3,R1,TT,BX,E5)
	D UPAB^XSXS("A110",-E3,R1,TT,BX,E5)

	;If not reinstating balances
	I $g(R6)="Y" G P00
	D UPAB^XSXS("E330",R2,R1,TT,BX,E5)
	D UPAB^XSXS("N110",E3,R1,TT,BX,E5)
	G E99

	;Reinstate billed disbursements
P00	S X3="",T0=0
P01	S X3=$O(^SD(CX,MX,BX,X3)) I X3="" G P03
	S ML=^SD(CX,MX,BX,X3),T0=T0+$P(ML,%,3)-$P(ML,%,2),^ML(CX,MX,X3)=ML G P01
P03	S ML=$G(^ML(CX,MX)),M1=$P(ML,%,1)+T0,M2=$P(ML,%,2),^(MX)=M1_%_M2
	K ^SD(CX,MX,BX)
	D UPAB^XSXS("A120",T0,R1,TT,BX,E5)
	S T1=B5-T0 D UPAB^XSXS("E280",T1,R1,TT,BX,E5)
	D UPAB^XSXS("N070",-B5,R1,TT,BX,E5)

	;Reinstate removed time
	S SW=$G(^SW(CX,MX,1,"G")),W1=$P(SW,%,1),W2=$P(SW,%,2),W3=$P(SW,%,3),W3=W3+B7,^("G")=W1_%_W2_%_W3
	S SW=$G(^SW(CX,MX)),W1=$P(SW,%,1)+B7,W2=$P(SW,%,2),^SW(CX,MX)=W1_%_W2
	D UPAB^XSXS("N020",B7,R1,TT,BX,E5)
	D UPAB^XSXS("I020",-B4,R1,TT,BX,E5)
	D UPAB^XSXS("N040",-B4,R1,TT,BX,E5)

E99	D UPAB^XSXS("N090",-E3,R1,TT,BX,E5)
	G XSBE

	;Amend
A01	W /C(0,4),/EF S %S=1046 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBE
	S %S=1047 F %J=1:1 D D^INPUT Q:X="END"
	S %S=1008 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	G A01:X[%,E02

	;Validation - Bill number
V01	Q:$D(ERR)  S SB=^SB(X),B1=$P(SB,%,1),CX=$P(SB,%,2),MX=$P(SB,%,3),B4=$P(SB,%,4),B5=$P(SB,%,5),B6=$J($P(SB,%,6),0,2),B7=$P(SB,%,7),B8=$J($P(SB,%,8),0,2),B9=$P(SB,%,9),B10=$P(SB,%,10),E2=$J(B4+B5,0,2),BV=B8-B4-B5-B6
	I B9="F"!'B8 S ERR="Bill has been paid" Q
	I B10="Y" W @P3,?48,"Includes Unpaid Prof Disb"
	Q
	;Net amount
V02	Q:$D(ERR)
	I X>E2!(X<-E2) S ERR="Cannot exceed original net amount"
	Q
	;VAT amount
V03	Q:$D(ERR)
	I X>0,R2<0 S ERR="Must be a negative amount" Q
	I X<0,R2>0 S ERR="Must be a positive amount" Q
	S E3=$J(R2+X,0,2)
	I E3>B8 S ERR="Credit amount cannot exceed O/S amount" Q
	I X>B6 S ERR="Cannot exceed original VAT amount" Q
	I X<0,E3<BV S ERR="Gross amount cannot exceed amount credited"
	Q
	;Reinstate?
V04	Q:$D(ERR)  Q:X="N"  I B9'="U" S ERR="Bill is part paid" Q
	I +R2'=+E2!(+R3'=+B6) S ERR="Must be for the full value of the bill"
	Q

CASE	S X=$zconvert(X,"U") Q

XSBF
XSBF	;GRI,,,;05:17 PM  11 Dec 2002
	;=======================================================
	; Virgo Accounts - Post Office Receipt
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:       1009
	; Files updated: ^CB ^SV ^SY ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	S TT="OFR",VAT=^SZ("VAT")/100,ONE=$g(^SZ("DOC"),1)
	D X0^XSXS
	S %S=1009 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBF Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSBF:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	D YX^XSXS S ^SY(DAT,TT,YX)=O1_%_%_%_DX_%_O2_%_O3_%_"Acct:"_%_AX_%_%_O4_%_O5
	S ^CB(DX,YX)=O1_%_%_E2_%_TT_%_O4_%_O5_%_"N"_%
	S CB=^CB(DX),B1=$P(CB,%,1,5),B6=$P(CB,%,6)+E2,^CB(DX)=B1_%_B6_%
	S T1=O2 S:"AE"[$e(AX,1) T1=-O2 D UPAB^XSXS(AX,T1,O1,TT,O4,O5)

	;Update VAT records
	I O3="*"!(^SZ("VCB")="X") G NV1
	S SV=$g(^SV),V1=$p(SV,%,1),V2=$p(SV,%,2),V3=$p(SV,%,3)+O2,V4=$p(SV,%,4)+O3,^SV=V1_%_V2_%_V3_%_V4,^SV(YX)="Receipt"_%_O1_%_O2_%_O3
	D UPAB^XSXS("L040",O3,O1,TT,O4,O5)

NV1	D UPAB^XSXS("A130",E2,O1,TT,O4,O5)
	G XSBF

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBF:%J=1,A01
	G E02

	;Validation (also used by XSBG)
	;Net amount
V01	S E3=$J(X*VAT,0,2) Q
	;VAT amount
V02	I X="*" K ERR
	I X<0,O2>0 S ERR="Must be a postive amount" Q
	I X>0,O2<0 S ERR="Must be a negative amount" Q
	S E2=$J(O2+X,0,2) Q
	;Cashbook number
V03	Q:$D(ERR)  I X>99 S ERR="Must be an office cashbook" Q
	S B6=$j($p(^CB(X),%,6),0,2)
	Q
	;Account code (also used by XSBJ, XSGF)
V04	Q:$D(ERR)  I "A110,A120,A130,L010,L020"[X S ERR="Cannot post to this account" Q
	I $E(X,1)="N" S ERR="Cannot be a non-financial account" Q
	I $P(^AB(X),%,5)="Y" S ERR="Cannot be a system account" W /C(0,22),@P1,"This is a system account - Are you sure ? ",/EL,@P2 S Z=X D ^GETX,CASE S Y=X,X=Z Q:Y'="Y"  K ERR
	S E5=$p(^AB(X),%,1)
	Q

CASE	S X=$zconvert(X,"U") Q

XSBG
XSBG	;GRI,,,;09:04 AM  3 Feb 2003
	;=======================================================
	; Virgo Accounts - Post Office Payment
	;
	; Copyright Graham R Irwin 1993-2003
	;
	; Screens:       1010
	; Files updated: ^CB ^SV ^SY ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS, 
	;                UPAB^XSXS, ^CHEQUE, ^XSBF validation routines
	;
	;=======================================================

	S TT="OFP",VAT=^SZ("VAT")/100,ONE=$g(^SZ("DOC"),1)
	D X0^XSXS
	S %S=1010 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBG Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSBG:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	D YX^XSXS S ^SY(DAT,TT,YX)=O1_%_%_%_DX_%_O2_%_O3_%_"Acct:"_%_AX_%_%_O4_%_O5
	S ^CB(DX,YX)=O1_%_E2_%_%_TT_%_O4_%_O5_%_"N"_%
	S CB=^CB(DX),B1=$P(CB,%,1,5),B6=$P(CB,%,6)-E2,^CB(DX)=B1_%_B6_%
	S T1=O2 S:"LI"[$e(AX,1) T1=-O2 D UPAB^XSXS(AX,T1,O1,TT,O4,O5)

	;Update VAT records
	I O3="*"!(^SZ("VCB")="X") G NV1
	S SV=$G(^SV),V1=$P(SV,%,1)+O2,V2=$P(SV,%,2)+O3,V3=$P(SV,%,3,99),^SV=V1_%_V2_%_V3,^SV("P"_YX)="Payment"_%_O1_%_O2_%_O3
	D UPAB^XSXS("L040",-O3,O1,TT,O4,O5)

NV1	D UPAB^XSXS("A130",-E2,O1,TT,O4,O5)
	;Write check
	D ^CHEQUE(DX,"",AX,O1,E2,O4,O5,TT)
	G XSBG

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBG:%J=1,A01
	G E02

CASE	S X=$zconvert(X,"U") Q

XSBH
XSBH	;GRI,,,;11:48 AM  6 Jun 2002
	;=======================================================
	; Virgo Accounts - Cashbook Transfer
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1011, 1049
	; Files updated: ^CB ^SY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS
	;
	;=======================================================

	D X0^XSXS
	S E1=1,E5="Cashbook Transfer",TT="TOO",ONE=$g(^SZ("DOC"),1)
	W @P1,?8,"TRANSFER FROM ----------------------",!
	S %S=1011 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBH Q
	W @P1,!?8,"TRANSFER TO ------------------------",!
	S %S=1049 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSBH

E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSBH:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	D YX^XSXS S ^SY(DAT,TT,YX)=O1_%_%_%_DX_%_O2_%_%_%_%_DX2_%_O4_%_O5
	S ^CB(DX,YX)=O1_%_O2_%_%_TT_%_O4_%_O5_%_"N"_%
	S CB=^CB(DX),B1=$P(CB,%,1,5),B6=$P(CB,%,6)-O2,^CB(DX)=B1_%_B6_%
	S ^CB(DX2,YX)=O1_%_%_O2_%_TT_%_O4_%_O5_%_"N"_%
	S CB=^CB(DX2),B1=$P(CB,%,1,5),B6=$P(CB,%,6)+O2,^CB(DX2)=B1_%_B6_%
	G XSBH

	;Amend
A01	W /C(0,5),/EF S %S=1011 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBH:%J=1,A01
	W @P1,!?8,"TRANSFER TO ------------------------",!
	S %S=1049 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G A01
	G E02

	;Validation - From cashbook
V03	Q:$D(ERR)  I X>99 S ERR="Must be an office cashbook" Q
	S B6=$j($p(^CB(X),%,6),0,2)
	Q
	;To cashbook
V06	Q:$D(ERR)  I X>99 S ERR="Must be an office cashbook" Q
	I X=DX S ERR="Cannot be the same account" Q
	S B6=$j($p(^CB(X),%,6),0,2)
	Q

CASE	S X=$zconvert(X,"U") Q

XSBI
XSBI	;GRI,,,;11:51 AM  6 Jun 2002
	;=======================================================
	; Virgo Accounts - Bank Reconciliation
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1012
	; Files updated: ^CB ^WORK($j) ^WORK2($j), ^WORK3($j)
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, ^OUTPUT, 
	;                HEAD^XSXS, END^XSXS
	;
	;=======================================================

	; ^WORK($j,i=1:1) - all transactions sorted by date
	; ^WORK2($j,0=unmatched/1=matched,item no) - all transactions
	;            split into matched and unmatched
	; ^WORK3($j,date,i=1:1) - initial file for sorting by date

	S MFL=0 ;set Mark mode flag off
	D X0^XSXS
	S ONE=$g(^SZ("DOC"),1)
	S %S=1012 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBI Q

	; Get all the transactions ---------
	S X3="",WX=1,DT=0 K ^WORK3($j)
G01	S X3=$O(^CB(DX,X3)) I X3="" G G02
	S CB=^CB(DX,X3),%DAT=$p(CB,%,1),D2=$p(CB,%,2),D3=$p(CB,%,3),D7=+$p(CB,%,7),DT=D3-D2*D7+DT D DMY2H^DATE
	I X3=0 S %H=0 ;if there's an o/bal keep it first
	S ^WORK3($j,%H,WX)=X3_%_CB,WX=WX+1
	G G01

	; Now sort them --------------------
G02	S (X2,X3)="",WX=1 K ^WORK($j)
G03	S X2=$o(^WORK3($j,X2)) I X2="" G M01
G04	S X3=$o(^WORK3($j,X2,X3)) I X3="" G G03
	S ^WORK($j,WX)=^WORK3($j,X2,X3),WX=WX+1
	G G04

	;Display items
M01	S WX="" D WRDT W /C(0,7),/EF,@P1,"Item Date",?20,"Amount  Ref     Narrative",?74,"Match?",!!
M02	S WX=$O(^WORK($j,WX)) I WX=""!($Y>20) G M09
M03	S CB=^WORK($j,WX),D1=$P(CB,%,2),D2=$P(CB,%,3),D3=$P(CB,%,4),D4=$P(CB,%,5),D5=$P(CB,%,6),D6=$P(CB,%,7),D7=+$P(CB,%,8),F2=D3-D2
	W:D7 @P5 W WX,?4,D1,?15,$j(F2,11,2),?28,D5,?36,$e(D6,1,38),?76,$s(D7:"Yes",'D7:"No"),@P1,!
	G M02

M09	I MFL G S01 ;if Mark mode go to Select prompt
	W /C(0,22),@P1,"Mark/unmark, Next, Update or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSBI:X="Y",M09
	I X="U" G U01
	I X="N" G N01
	I X="\\" G H01
	I X="M"!(X="Z") S MFL=1 G S01
	G M09

	;Select
S01	W @P1,/c(39,22),"Mark/unmark item ",@P2 D ^GETX
	I X="*" D S10 G M01
	I X="N" G N01
	I X="\\" G H01
	S X=+X I $g(^WORK($j,X))="" S MFL=0 G M09
	S WK=^WORK($j,X),W1=$p(WK,%,1,2),W3=$p(WK,%,3),W4=$p(WK,%,4),W5=$p(WK,%,5,7),W8=$p(WK,%,8),W8=1-W8,^WORK($j,X)=W1_%_W3_%_W4_%_W5_%_W8,T1=W4-W3
	S:W8 DT=DT+T1 S:'W8 DT=DT-T1
	D WRDT S WX=X-6 W /C(0,9),/EF,@P1
	G M02

	;Mark all
S10	S WX=""
S11	S WX=$o(^WORK($j,WX)) I WX="" Q
	S WK=^WORK($j,WX),^WORK($j,WX)=$p(WK,%,1,7)_%_1,DT=+B6
	G S11

	;Next
N01	W /C(0,9),/EF,@P1 G M03:WX'="",M02

	;Home
H01	W /C(0,9),/EF,@P1 S WX="" G M02

	;Update
U01	S WX="" K ^WORK2($j)
U02	S WX=$O(^WORK($j,WX)) I WX="" G U04
	S WK=^WORK($j,WX),X3=$P(WK,%,1),CB=$P(WK,%,2,99),D7=+$P(WK,%,8)
	S ^CB(DX,X3)=CB,^WORK2($j,D7,WX)=CB
	G U02

	;Print report
U04	D ^OUTPUT G:%OP[% XSBI S N=99,P=1 D HEAD^XSXS
	W "Cashbook ",DX,?14,B1,?50,"Balance ",B6,!!
	W "Matched (Presented) Items:-",!!
	W "Date",?15,"Amount",?23,"Type  Ref",?37,"Narrative",!!
	S N=N+6,T1=0,WX=""
U05	S WX=$O(^WORK2($j,1,WX)) I WX="" G U06
	S WK=^WORK2($j,1,WX) D WRITE S T1=T1+F2
	G U05
U06	W !,"Total",?12,$j(T1,10,2),!!
	W "Unmatched (Unpresented) Items:-",!!
	W "Date",?15,"Amount",?23,"Type  Ref",?37,"Narrative",!!
	S N=N+7,T1=0
U07	S WX=$O(^WORK2($j,0,WX)) I WX="" G END
	S WK=^WORK2($j,0,WX) D WRITE S T1=T1+F2
	G U07

END	W !,"Total",?12,$j(T1,10,2),!
	D END^XSXS

	;Remove/archive transactions
W00	W /C(0,22),@P1,"Remove or Archive matched transactions (R/A/N) ? ",/EF,@P2 D ^GETX,CASE
	I X="N" Q
	I X'="R",X'="A" G W00

	S X2=0,T1=0
W01	S X2=$o(^CB(DX,X2)) I X2="" G W09
	S CB=^CB(DX,X2),C7=$p(CB,%,7)
	I C7 S C2=$p(CB,%,2),C3=$p(CB,%,3),T1=T1+C3-C2 K ^CB(DX,X2) I X="A" S ^CA(DX,X2)=CB
	G W01
W09	S CB=$g(^CB(DX,0)),C2=$p(CB,%,2),C3=$p(CB,%,3)
	S T1=T1+C3-C2,(C2,C3)=0 S:T1>0 C3=T1 S:T1<0 C2=-T1
	S ^CB(DX,0)=DAT_%_C2_%_C3_%_""_%_%_"Balance forward"_%_0
	Q

	;Validation - Cashbook
V01	Q:$D(ERR)  S B1=$P(^CB(X),%,1),B6=$J($P(^(X),%,6),0,2)
	Q

	;Print line item
WRITE	S D1=$P(WK,%,1),D2=$P(WK,%,2),D3=$P(WK,%,3),D4=$P(WK,%,4),D5=$P(WK,%,5),D6=$P(WK,%,6),D7=+$P(WK,%,7),F2=D3-D2
	W D1,?12,$J(F2,10,2),?24,D4,?29,D5,?37,$e(D6,1,40),!
	S N=N+1 I N>56 D HEAD^XSXS
	Q

	;display total matched amount
WRDT	W @P5,/C(48,5),$j(DT,0,2),@P1,/EL Q

CASE	S X=$zconvert(X,"U") Q

XSBJ
XSBJ	;GRI,,,;07:52 PM  18 Aug 1998
	;=======================================================
	; Virgo Accounts - Journal Transfer
	;
	; Copyright Graham R Irwin 1998
	;
	; Screens:       1017, 1052, 1065
	; Files updated: ^SY ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS,
	;                UPAB^XSXS, ^XSBF validation routines
	;
	;=======================================================

	S TT="JRN",F5="Journal Transfer"
	D X0^XSXS
	W @P1,?8,"TRANSFER FROM (credit) -------------",!
	S %S=1065 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBJ Q
	S %S=1017 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSBJ

	W @P1,!?8,"TRANSFER TO (debit) ----------------",!
	S %S=1052 F %J=1:1 D ^INPUT Q:X="END"!(X[%) 
	I X[% G XSBJ

E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSBJ:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_"Acct:"_%_AX1_%_%_O2_%_%_"Acct:"_%_AX2_%_%_%_O5
	S X1=$e(AX1,1),X2=$e(AX2,1),O3=O2 
	I "AE"[X1,"LI"[X2 S O3=-O2
	I "LI"[X1 S (O2,O3)=-O2 I "AE"[X2 S O3=-O2
	;from NL account
	D UPAB^XSXS(AX1,-O2,D1,TT,"",O5)
	;to NL account
	D UPAB^XSXS(AX2,O3,D1,TT,"",O5)
	G XSBJ

	;Amend
A01	W /C(0,5),/EF
	S %S=1065 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBJ:%J=1,A01
	S %S=1017 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G A01

	W @P1,!?8,"TRANSFER TO (debit) ----------------",!
	S %S=1052 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G A01
	G E02

	;Validation - From account
V01	Q:$D(ERR)  S A3=$j($p(^AB(X),%,3),0,2)
	Q
	;To account
V03	Q:$D(ERR)  I X=AX1 S ERR="Cannot be the same account" Q
	S A3=$j($p(^AB(X),%,3),0,2)
	Q

CASE	S X=$zconvert(X,"U") Q

XSBK
XSBK	;GRI,,,;10:17 AM  7 Jan 1998
	;=======================================================
	; Virgo Accounts - Disbursement Receipt
	;
	; Copyright Graham R Irwin 1993-98
	;
	; Screens:       1053
	; Files updated: ^CB ^ML ^SY ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	S E5="Disbursement Receipt",TT="DSP",ONE=$g(^SZ("DOC"),1)
	D X0^XSXS
	S %S=1053 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBK Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSBK:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S E5=D5_" - "_$p(^SC(CX),%,1),E5=$e(E5,1,70)
	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_CX_%_MX_%_DX_%_D2_%_%_%_%_%_D4_%_E5
	S ^ML(CX,MX,YX)=D1_%_D2_%_%_TT_%_D4_%_%_D5_%
	S ML=$G(^ML(CX,MX)),B1=$P(ML,%,1)-D2,B2=$P(ML,%,2,9),^ML(CX,MX)=B1_%_B2
	S ^CB(DX,YX)=D1_%_%_D2_%_TT_%_D4_%_E5_%_"N"_%
	S CB=^CB(DX),B1=$p(CB,%,1,5),B6=$p(CB,%,6)+D2,^CB(DX)=B1_%_B6_%
	D UPAB^XSXS("A120",-D2,D1,TT,D4,E5)
	D UPAB^XSXS("A130",D2,D1,TT,D4,E5)
	D UPAB^XSXS("N070",D2,D1,TT,D4,E5)
	G XSBK

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBK:%J=1,A01
	G E02

	;Validation - Amount
V01	S ML=$g(^ML(CX,MX)),B1=$p(ML,%,1)
	I X>B1 W @P3,?48,"Disbursement Balance in Credit",*7
	Q

CASE	S X=$zconvert(X,"U") Q

XSBL
XSBL	;GRI,,,;04:26 PM  4 Nov 1994
	;=======================================================
	; Virgo Accounts - Matter Transfer
	;
	; Copyright Graham R Irwin 1993-94
	;
	; Screens:       1054, 1055
	; Files updated: ^ML ^SW
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS
	;
	;=======================================================

	S E5="Matter Transfer",TT="OMT"
	D X0^XSXS
	W @P1,?8,"TRANSFER FROM ----------------------",!
	S %S=1054 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBL Q
	W @P1,!?8,"TRANSFER TO ------------------------",!
	S %S=1055 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSBL
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSBL:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update...
U01	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_CX1_%_MX1_%_%_A1_%_%_CX2_%_MX2_%_%_%_D5
	;...from matter ledger
	S ^ML(CX1,MX1,YX)=D1_%_A1_%_%_TT_%_%_%_D5_%
	S ML=$g(^ML(CX1,MX1)),B1=$p(ML,%,1)-A1,B2=$p(ML,%,2,99),^ML(CX1,MX1)=B1_%_B2
	;...to matter ledger
	S ^ML(CX2,MX2,YX)=D1_%_%_A1_%_TT_%_%_%_D5_%
	S ML=$g(^ML(CX2,MX2)),B1=$p(ML,%,1)+A1,B2=$p(ML,%,2,99),^ML(CX2,MX2)=B1_%_B2
	;...from WIP
	S SW=$g(^SW(CX1,MX1,1,"G")),T1=$p(SW,%,1),T2=$p(SW,%,2),T3=$p(SW,%,3)-A2,^SW(CX1,MX1,1,"G")=T1_%_T2_%_T3_%
	S SW=$g(^SW(CX1,MX1)),W1=$p(SW,%,1)-A2,W2=$p(SW,%,2),^SW(CX1,MX1)=W1_%_W2_%
	;...to WIP
	S SW=$g(^SW(CX2,MX2,1,"G")),T1=$p(SW,%,1),T2=$p(SW,%,2),T3=$p(SW,%,3)+A2,^SW(CX2,MX2,1,"G")=T1_%_T2_%_T3_%
	S SW=$g(^SW(CX2,MX2)),W1=$p(SW,%,1)+A2,W2=$p(SW,%,2),^SW(CX2,MX2)=W1_%_W2_%
	G XSBL

	;Amend
A01	W /C(0,5),/EF S %S=1054 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBL:%J=1,A01
	W @P1,!?8,"TRANSFER TO ------------------------",!
	S %S=1055 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G A01
	G E02

	;Validation - From matter code
V01	Q:$D(ERR)  S ML1=$g(^ML(CX1,X)),M1=$j($p(ML1,%,1),0,2),SW1=$g(^SW(CX1,X)),S1=$j($p(SW1,%,1),0,2)
	Q
	;Disbursements
V02	I X>M1 S ERR="Cannot exceed disb balance"
	Q
	;Work-in-progress
V03	I X>S1 S ERR="Cannot exceed WIP balance"
	Q
	;To matter code
V04	Q:$D(ERR)
	I CX1=CX2,MX1=X S ERR="Cannot be the same matter"
	Q

CASE	S X=$zconvert(X,"U") Q

XSBM
XSBM	;GRI,,,;08:56 AM  9 May 2003;
	;=======================================================
	; Virgo Accounts - Batch Disbursements
	;
	; Copyright Graham R Irwin 1993-1998
	;
	; Screens:       1042, 1057
	; Files updated: ^CB ^ML ^SV ^SY ^AB ^AY ^WORK($j)
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	D X0^XSXS
	S TT="DIS",B5="Disbursement",VAT=^SZ("VAT")/100,ONE=$g(^SZ("DOC"),1)
	S %S=1042 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% Q:%J=1  G XSBM
	W @P1,!?4,"Client Matter Net Amount VAT Amount Narrative",!! S WS=1 K ^WORK($j)

I01	W /C(0,16),/EF S WX=WS,F1=1
	S %S=1057 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G I01:%J>1 G XSBM:WX=1 S WS=WX,W1="",F1=0 G N01
E09	W /C(0,22),@P1,"Update, Amend, Delete or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) S W1="" G N01
	I X="A" G A01
	I X="U" G U01
	I X="D" W @P1,"  Are you sure ? ",@P2 D ^GETX,CASE I X="Y" K ^WORK($j,WX) S W1="" G N01
	G E09

	;Update individual entry
U01	S ^WORK($j,WX)=CX_%_MX_%_B3_%_B4_%_B5,WS=WS+1,W1="",(T1,T2)=0
U02	S W1=$o(^WORK($j,W1)) I W1="" G U04
	S WK=^WORK($j,W1),T1=T1+$p(WK,%,3),T2=T2+$p(WK,%,4)
	G U02
U04	W /C(48,5),/EL,@P5,$j(T1+T2,0,2)
	W /C(0,11),/EF,@P1 S W1=WX-5
E03	S W1=$O(^WORK($j,W1)) I W1=""!($Y>20) G I01:F1,L01
E04	S WK=^WORK($j,W1),CX=$p(WK,%,1),MX=$p(WK,%,2),B3=$j($p(WK,%,3),10,2),B4=$p(WK,%,4),B5=$p(WK,%,5) S:B4'="*" B4=$j(B4,10,2) S:B4="*" B4="         *"
	W @P1,W1,?5,CX,?12,MX,?18,B3,?29,B4,?40,B5,! G E03

L01	S UPD=+A1=(T1+T2)
	W /C(0,22),@P1,"Next, Select, Insert" W:UPD ", Update" W " or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Forget entire batch ? ",@P2 D ^GETX,CASE G XSBM:X="Y",L01
	I X="N" G N01
	I X="S"!(X="Z") G S01
	I X="I" G I01
	I UPD,X="U" G U11
	G L01

	;Update records
U11	S WX=""
U30	S WX=$O(^WORK($j,WX)) I WX="" G U99
	S WK=^WORK($j,WX),CX=$P(WK,%,1),MX=$P(WK,%,2),B3=$P(WK,%,3),B4=$P(WK,%,4),B5=$P(WK,%,5)
	D YX^XSXS
	S ^SY(DAT,TT,YX)=A2_%_CX_%_MX_%_DX_%_B3_%_B4_%_%_%_%_A3_%_B5
	S ^ML(CX,MX,YX)=A2_%_%_B3_%_TT_%_A3_%_%_B5
	S MLB=$g(^ML(CX,MX)),MLB1=$p(MLB,%,1)+B3,MLB29=$p(MLB,%,2,9),^ML(CX,MX)=MLB1_%_MLB29
	G U30

U99	S A0="Disbursements batch"
	S ^CB(DX,YX)=A2_%_A1_%_%_TT_%_A3_%_A0_%_"N"_%
	S CBA=^CB(DX),CBA15=$p(CBA,%,1,5),CBA6=$p(CBA,%,6)-A1,^CB(DX)=CBA15_%_CBA6_%
	S SVA=$g(^SV),SVA1=$p(SVA,%,1)+T1,SVA2=$p(SVA,%,2)+T2,SVA39=$p(SVA,%,3,9),^SV=SVA1_%_SVA2_%_SVA39,^SV("P"_YX)="Disb'ts"_%_A2_%_T1_%_T2
	D UPAB^XSXS("L040",-T2,A2,TT,A3,A0)
	D UPAB^XSXS("A120",T1,A2,TT,A3,A0)
	D UPAB^XSXS("N060",T1,A2,TT,A3,A0)
	D UPAB^XSXS("A130",-A1,A2,TT,A3,A0)
	G XSBM

	;Next
N01	W /C(0,11),/EF,@P1 G E04:W1'="",E03

	;Amend
A01	W /C(0,16),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G N01:%J=1,A01
	G E09

	;Select
S01	W @P1,"  Select item ",@P2 D ^GETX S WX=+X I $G(^WORK($j,WX))="" G L01
	S WK=^WORK($j,WX),CX=$P(WK,%,1),MX=$P(WK,%,2),B3=$P(WK,%,3),B4=$P(WK,%,4),B5=$P(WK,%,5)
	W /C(0,16),/EF F %J=1:1 D D^INPUT Q:X="END"!(X[%)
	G E09

	;Validation ----------------------------------------
	;Cashbook
V01	Q:$d(ERR)  I X>99 S ERR="Must be an Office cashbook"
	Q
	;Net amount
V04	Q:$D(ERR)  S SMA6=$p(^SM(CX,MX),%,6),E4=$j(X*VAT*(SMA6="Y"),0,2)
	Q
	;VAT amount
V05	I X="*" K ERR
	I X<0,B3>0 S ERR="Must be a positive number" Q
	I X>0,B3<0 S ERR="Must be a negative number" Q
	Q

CASE	S X=$zconvert(X,"U") Q

XSBN
XSBN	;GRI,,,;07:51 PM  18 Aug 1998;
	;=======================================================
	; Virgo Accounts - Nominal to Disb Transfer
	;
	; Copyright Graham R Irwin 1998
	;
	; Screens:       1017, 1065, 1068
	; Files updated: ^SY ^ML ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS,
	;                UPAB^XSXS, ^XSBF & ^XSBJ validation routines
	;
	;=======================================================

	S TT="N2D",F5="Nominal to Disb Transfer"
	D X0^XSXS
	W @P1,?8,"TRANSFER FROM (credit) -------------",!
	S %S=1065 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBN Q
	S %S=1017 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSBN

	W @P1,!?8,"TRANSFER TO (debit) ----------------",!
	S %S=1068 F %J=1:1 D ^INPUT Q:X="END"!(X[%) 
	I X[% G XSBN

E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSBN:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_"Acct:"_%_AX1_%_%_O2_%_%_CX_%_MX_%_%_%_O5
	S X1=$e(AX1,1),O3=O2
	I "LI"[X1 S O3=-O2
	;from NL account
	D UPAB^XSXS(AX1,-O3,D1,TT,"",O5)
	;to account
	D UPAB^XSXS("A120",O2,D1,TT,"",O5)
	S ^ML(CX,MX,YX)=D1_%_%_O2_%_TT_%_%_%_O5_%
	S ML=$g(^ML(CX,MX)),B1=$p(ML,%,1)+O2,B2=$p(ML,%,2,99),^ML(CX,MX)=B1_%_B2
	G XSBN

	;Amend
A01	W /C(0,5),/EF
	S %S=1065 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBN:%J=1,A01
	S %S=1017 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G A01

	W @P1,!?8,"TRANSFER TO (debit) ----------------",!
	S %S=1068 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G A01
	G E02

CASE	S X=$zconvert(X,"U") Q

XSBP
XSBP	;GRI,,,;03:40 PM  11 Nov 2002;
	;=======================================================
	; Virgo Accounts - Post Creditor Invoice
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1082
	; Files updated: ^SV ^SY ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS, 
	;                UPAB^XSXS, ^XSBF validation routines
	;
	;=======================================================

	S TT="CRI",VAT=^SZ("VAT")/100
	D X0^XSXS
	S %S=1082 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBP Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSBP:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	I ^SZ("VCB")'="N" S O3="*",E2=O2
	D YX^XSXS S ^SY(DAT,TT,YX)=O1_%_"Acct:"_%_AX2_%_%_O2_%_O3_%_"Acct:"_%_AX1_%_%_O4_%_O5
	S T2=O2 I "IL"[$e(AX1,1) S T2=-O2
	D UPAB^XSXS(AX1,T2,O1,TT,O4,O5)
	D UPAB^XSXS(AX2,E2,O1,TT,O4,O5)

	;Update VAT records
	I O3="*"!(^SZ("VCB")="X") G NV1
	S SV=$G(^SV),V1=$P(SV,%,1)+O2,V2=$P(SV,%,2)+O3,V3=$P(SV,%,3,99),^SV=V1_%_V2_%_V3,^SV("P"_YX)="Payment"_%_O1_%_O2_%_O3
	D UPAB^XSXS("L040",-O3,O1,TT,O4,O5)

NV1
	G XSBP

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBP:%J=1,A01
	G E02

	;Validation - Creditor account
V01	Q:$D(ERR)
	I $e(X)'="L" S ERR="Must be a liability account" Q
	S A3=$j($p(^AB(X),%,3),0,2)
	Q
	;Expense account
V03	Q:$D(ERR)  I X=AX2 S ERR="Cannot be the same account" Q
	S A3=$j($p(^AB(X),%,3),0,2)
	Q

CASE	S X=$zconvert(X,"U") Q

XSBQ
XSBQ	;GRI,,,;01:12 PM  11 May 2004;
	;=======================================================
	; Virgo Accounts - Nominal to Bill Transfer
	;
	; Copyright Graham R Irwin 1998-2004
	;
	; Screens:       1065, 1084, 1085
	; Files updated: ^SY ^ML ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS,
	;                UPAB^XSXS, ^XSBA, ^XSBF & ^XSBJ validation routines
	;
	;=======================================================

	S TT="N2B",F5="Nominal to Bill Transfer",VAT=^SZ("VAT")/100
	D X0^XSXS
	W @P1,?8,"TRANSFER FROM (credit) -------------",!
	S %S=1065 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSBQ Q
	S %S=1084 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSBQ

	W @P1,!?8,"TRANSFER TO (debit) ----------------",!
	S %S=1085 F %J=1:1 D ^INPUT Q:X="END"!(X[%) 
	I X[% G XSBQ

E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
o	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSBQ:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_"Acct:"_%_AX1_%_%_D2_%_D3_%_CX_%_MX_%_%_%_O5
	S X1=$e(AX1,1),O3=E2 I "LI"[X1 S O3=-E2
	;from NL account
	D UPAB^XSXS(AX1,-O3,D1,TT,"",O5)

	S ^ML(CX,MX,YX)=D1_%_E2_%_%_TT_%_D4_%_%_O5_%
	S ML=$G(^ML(CX,MX)),M1=$P(ML,%,1),M2=$P(ML,%,2),M2=M2-E2,^ML(CX,MX)=M1_%_M2

	D UPAB^XSXS("A110",-E2,D1,TT,D4,O5)

	;Update bill register (it may be a reverse transfer)
	S BB8=BB8-E2 S:BB8<0 BB8=0 S BB9="P" S:'BB8 BB9="F" S:BB8=(BB4+BB5+BB6) BB9="U"
	S ^SB(BX)=BB1_%_CX_%_MX_%_BB4_%_BB5_%_BB6_%_BB7_%_BB8_%_BB9_%_BB10_%
	I BB9="F" K ^SU(BX)
	I BB9="U"!(BB9="P") S ^SU(BX)=""

	;Update VAT records if cash basis
	I ^SZ("VCB")'="Y" G NV1
	S SV=$G(^SV),V1=$P(SV,%,1),V2=$p(SV,%,2),V3=$P(SV,%,3)+D2,V4=$P(SV,%,4)+D3,^SV=V1_%_V2_%_V3_%_V4,^SV(YX)=BX_%_D1_%_D2_%_D3
	D UPAB^XSXS("L040",D3,D1,TT,D4,O5)
	D UPAB^XSXS("L140",-D3,D1,TT,D4,O5)

NV1	G XSBQ

	;Amend
A01	W /C(0,5),/EF
	S %S=1065 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSBQ:%J=1,A01
	S %S=1084 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G A01

	W @P1,!?8,"TRANSFER TO (debit) ----------------",!
	S %S=1085 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G A01
	G E02

CASE	S X=$zconvert(X,"U") Q

	;Validation - Net amount
V01	S E1=$J(X*VAT,0,2)
	Q

	;Bill number
V03	Q:$D(ERR)  S SB=^SB(X),BB1=$P(SB,%,1),CX=$P(SB,%,2),MX=$P(SB,%,3),BB4=$P(SB,%,4),BB5=$P(SB,%,5),BB6=$J($P(SB,%,6),0,2),BB7=$P(SB,%,7),BB8=$J($P(SB,%,8),0,2),BB9=$P(SB,%,9),BB10=$P(SB,%,10),R2=$J(BB4+BB5,0,2)
	I BB9="F",-D2>(BB4+BB5+BB6) S ERR="Repayment cannot exceed original bill amount" Q
	I E2>BB8 S ERR="Payment cannot exceed outstanding bill amount" Q
	; VAT amount cannot exceed original VAT amount
	I BB10="Y" W @P3,?48,"Includes Unpaid Prof Disb"
	S D4=X
	Q

XSCA
XSCA	;GRI,,,;02:56 PM  4 Feb 1999
	;=======================================================
	; Virgo Accounts - Matter Ledger Enquiry
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:       1013
	; Files updated: none
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, ^GETPGM(XSFG),
	;                ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	S %S=1013 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSCA Q
	S DET=0 I $d(^SD(CX,MX)) S DET=1
	W @P1 D HEAD

	;Display matter ledger
E01	S IX="",E4=0,MOD=0 W /C(0,11),/EF,@P1
E02	S IX=$O(^ML(CX,MX,IX)) I IX=""!($Y>19) G E09
E03	D LINE
	G E02

E09	W /C(0,22),@P1,"Next, "
	W:DET&'MOD "History, " W:MOD "Current, "
	W "Print or Quit ? ",/EL,@P2 D ^GETX S X=$zconvert(X,"U")
	I X="Q"!(X=%) G XSCA
	I X="P" G P01
	I DET,'MOD,X="H" G H01
	I MOD,X="C" G E01
	I X="N" G N01
	I X="\\" G M01
	I X="Z" D ^GETPGM(3,5) G ^@PGM
	G E09

	;Display history
H01	S (X3,X4)="",E4=0,MOD=1 W /C(0,11),/EF,@P1
H02	S X3=$o(^SD(CX,MX,X3)) I X3=""!($Y>19) G E09
H03	S X4=$o(^SD(CX,MX,X3,X4)) G:X4="" H02 I $Y>19 G E09
H04	D HLINE
	G H03

	;Next
N01	W /C(0,11),/EF,@P1
	I MOD G H04:X3'=""&(X4'="") S E4=0 G H03:X3'="" G H02
	G E03:IX'="" S E4=0 G E02

	;Home
M01	W /C(0,11),/EF,@P1
	I MOD S (X3,X4)="",E4=0 G H02
	S IX="",E4=0 G E02

	;Print
P01	D ^OUTPUT G:%OP[% XSCA S N=99,P=1 D HEAD^XSXS
	D PRINT
	D END^XSXS
	G XSCA

	;Validation - Matter number
V01	Q:$D(ERR)  I '$D(^ML(CX,X)) S ERR="No ledger details for this matter" Q
	S ML=^ML(CX,X),M1=$J($P(ML,%,1),0,2),M2=$J($P(ML,%,2),0,2)
	Q

	;Print routine - May be called by other programs

	; needs the following variables defined:-
	; CX, MX (client and matter codes)
	; MOD =0 for current =1 for historic entries
	; M1, M2 (disb and bills balances)

PRINT	W "Client ",CX,?12,$p(^SC(CX),%,1),!
	W "Matter ",MX,?12,$p(^SM(CX,MX),%,1),!
	W "Disb Balance  ",$j(M1,12,2),!
	W "Bills Balance ",$j(M2,12,2)
	I MOD W ?50,"* Historic Entries *"
	W ! D HEAD
	S E4=0,N=N+7,(IX,X3,X4)=""
	I MOD G P12

	;current transactions
P02	S IX=$O(^ML(CX,MX,IX)) I IX="" Q
	D LINE S N=N+2 I N>56 D HEAD^XSXS
	G P02

	;historic transactions
P12	S X3=$o(^SD(CX,MX,X3)) I X3="" Q
P13	S X4=$o(^SD(CX,MX,X3,X4)) I X4="" G P12
	D HLINE S N=N+2 I N>56 D HEAD^XSXS
	G P13

	;Show heading (for print or display)
HEAD	W !?5,"Date",?18,"Ref No   Type",?37,"Debit",?49,"Credit",?62,"Balance",!! Q

LINE	S ML=^ML(CX,MX,IX) D DATA Q

HLINE	S ML=^SD(CX,MX,X3,X4) D DATA Q

DATA	S D1=$P(ML,%,1),D2=$P(ML,%,2),D3=$P(ML,%,3),D4=$P(ML,%,4),D5=$P(ML,%,5),D7=$P(ML,%,7),E4=E4-D2+D3
	W ?5,D1,?18,D5,?27,D4 W:D3 ?33,$J(D3,10,2) W:D2 ?46,$J(D2,10,2) W ?59,$J(E4,10,2),!?6,D7,!
	Q

XSCB
XSCB	;GRI,,,;11:12 AM  13 Mar 2000
	;=======================================================
	; Virgo Accounts - Cashbook Enquiry
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:       1014
	; Files updated:
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, ^OUTPUT, 
	;                HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	S %S=1014 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSCB Q
	S DET=0 I $d(^CA(DX)) S DET=1
	W @P1 D HEAD

	;Display cashbook transactions
E01	S IX="",MOD=0,E4=0 W /c(0,9),/ef,@P1
E02	S IX=$O(^CB(DX,IX)) I IX=""!($Y>19) G E09
E03	D LINE
	G E02

E09	W /C(0,22),@P1,"Next, "
	W:DET&'MOD "History, " W:MOD "Current, "
	W "Print or Quit ? ",/EL,@P2 D ^GETX S X=$zconvert(X,"U")
	I X="Q"!(X=%) G XSCB
	I X="P" G P01
	I DET,'MOD,X="H" G H01
	I MOD,X="C" G E01
	I X="N" G N01
	I X="\\" G M01
	G E09

	;Display history
H01	S IX="",MOD=1,E4=0 W /c(0,9),/ef,@P1
H02	S IX=$O(^CA(DX,IX)) I IX=""!($Y>19) G E09
H03	D HLINE
	G H02

	;Next
N01	W /C(0,9),/EF,@P1
	I MOD G H03:IX'="" S E4=0 G H02
	G E03:IX'="" S E4=0 G E02

	;Home
M01	W /C(0,9),/EF,@P1
	I MOD S IX="",E4=0 G H02
	S IX="",E4=0 G E02

	;Print
P01	D ^OUTPUT G:%OP[% XSCB S N=99,P=1,IX="" D HEAD^XSXS
	D PRINT
	D END^XSXS
	G XSCB

	;Validation - Cashbook
V01	I $D(ERR) Q
	S B6=$J($P(^CB(X),%,6),0,2)
	Q

	;Print routine
PRINT	W "Cashbook ",DX,?14,$p(^CB(DX),%,1),!
	W "Balance ",$j(B6,12,2)
	I MOD W ?50,"* Historic Entries *"
	W ! D HEAD S N=N+4
	I MOD G P12

	;current transactions
P02	S IX=$O(^CB(DX,IX)) I IX="" Q
	D LINE S N=N+2 I N>56 D HEAD^XSXS
	G P02

	;historic transactions
P12	S IX=$O(^CA(DX,IX)) I IX="" Q
	D HLINE S N=N+2 I N>56 D HEAD^XSXS
	G P12

	;Show heading (display or print)
HEAD	W !?5,"Date",?18,"Ref No   Type",?36,"Receipt",?49,"Payment",?62,"Balance",!! S E4=0 Q

LINE	S CB=^CB(DX,IX) D DATA Q

HLINE	S CB=^CA(DX,IX) D DATA Q

DATA	S C1=$P(CB,%,1),C2=$P(CB,%,2),C3=$P(CB,%,3),C4=$P(CB,%,4),C5=$P(CB,%,5),C6=$P(CB,%,6),E4=E4-C2+C3
	W ?5,C1,?18,C5,?27,C4 W:C3 ?33,$J(C3,10,2) W:C2 ?46,$J(C2,10,2) W ?59,$J(E4,10,2),!?6,C6,!
	Q

XSCC
XSCC	;GRI,,,;10:18 AM  1 Nov 2001
	;=======================================================
	; Virgo Accounts - Unpaid Bills Enquiry
	;
	; Copyright Graham R Irwin 1993-2001
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, ^GETX, ^OUTPUT, DMY2H^DATE,
	;                HEAD^XSXS, END^XSXS
	;
	;=======================================================

	; Note, this uses the bill register not the unpaid bill index file

	D X0^XSXS
	W @P1 D HEAD
	S IX=""
E02	S IX=$O(^SB(IX)) I IX=""!($Y>19) G E09
E03	D GET
	I D8=0 G E02
	D LINE
	G E02

E09	W /C(0,22),@P1,"Next, Print or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) Q
	I X="P" G P01
	I X="N" W /C(0,6),/EF,@P1 G E03:IX'="" S E4=0 G E02
	G E09

	;Print
P01	D ^OUTPUT G:%OP[% XSCC S N=99,P=1,IX="",(T1,T2,T3)=0 D HEAD^XSXS
	D HEAD S N=N+2
P02	S IX=$O(^SB(IX)) I IX="" G END
P03	D GET
	I D8=0 G P02
	D LINE W ! S N=N+3 I N>56 D HEAD^XSXS
	S T1=T1+D4+D5,T2=T2+D6,T3=T3+D8
	G P02

END	W "TOTAL",?19,$j(T1,10,2),?31,$j(T2,10,2),?43,$j(T1+T2,10,2),?56,$j(T3,10,2),!
	D END^XSXS
	Q

CASE	S X=$zconvert(X,"U") Q

	;Get bill details
GET	S SB=^SB(IX),D1=$p(SB,%,1),D2=$p(SB,%,2),D3=$p(SB,%,3),D4=$p(SB,%,4),D5=$p(SB,%,5),D6=$p(SB,%,6),D7=$p(SB,%,7),D8=+$p(SB,%,8)
	S %DAT=D1 D DMY2H^DATE S D0=$H-%H
	Q

	;Print/display column headings
HEAD	W "Date",?11,"Bill No",?24,"Net",?36,"VAT",?47,"Gross",?56,"O/S amount  C/L  Age",!!
	Q

	;Print/display bill details (2 lines)
	;get client and matter description
LINE	S E1=$e($p(^SC(D2),%,1),1,30),E2=$e($p(^SM(D2,D3),%,1),1,35)
	;check if client account has funds
	S F1=($g(^CL(D2,D3))>0),F1=$e(" *",F1+1)
	;print/display
	W D1,?12,IX,?19,$J(D4+D5,10,2),?31,$J(D6,10,2),?43,$J(D4+D5+D6,10,2),?56,$J(D8,10,2),?69,F1,?73,D0,!?1,E1,"; ",E2,!
	Q

XSCD
XSCD	;GRI,,,;11:41 AM  26 Jan 2000
	;=======================================================
	; Virgo Accounts - Matter Accounts Enquiry
	;
	; Copyright Graham R Irwin 1993-2000
	;
	; Screens:       1015
	; Files updated: ^WORK($j)
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, ^OUTPUT, 
	;                HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	S %S=1015 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSCD Q
	I MX'="*" G S00

	;Option prompt if matter is *
E08	W /C(0,22),@P1,"Next or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) Q
	I X="N" G XSCD
	G E08

	;Get ML and CL transactions
S00	K ^WORK($j) S IX=""
S01	S IX=$o(^ML(CX,MX,IX)) I IX="" G S03
	S ML=^ML(CX,MX,IX),D1=$p(ML,%,1),D2=$p(ML,%,2),D3=$p(ML,%,3),D4=$p(ML,%,4),D5=$p(ML,%,5),D7=$p(ML,%,7)
	S ^WORK($j,IX)=D1_%_(D3-D2)_%_%_D4_%_D5_%_D7
	G S01

S03	S IX=$o(^CL(CX,MX,IX)) I IX="" G E00
	S ML=^CL(CX,MX,IX),D1=$p(ML,%,1),D2=$p(ML,%,2),D3=$p(ML,%,3),D4=$p(ML,%,4),D5=$p(ML,%,5),D6=$p(ML,%,6)
	S E2="" I $d(^WORK($j,IX)) S E2=$p(^WORK($j,IX),%,2)
	S ^WORK($j,IX)=D1_%_E2_%_(D3-D2)_%_D4_%_D5_%_D6
	G S03

	;Display transactions
E00	W @P1 D HEAD
E02	S IX=$o(^WORK($j,IX)) I IX=""!($Y>20) G E09
E03	D LINE
	G E02

	;Option prompt
E09	W /C(0,22),@P1,"Next, Print or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) G XSCD
	I X="P" G P01
	I X="N" G N01
	I X="\\" G H01
	G E09

	;Next
N01	W /c(0,13),/ef,@P1 G E03:IX'="" S (T1,T2)=0 G E02

	;Home
H01	W /c(0,13),/ef,@P1 S IX="",(T1,T2)=0 G E02

	;Print
P01	D ^OUTPUT G:%OP[% XSCD S N=99,P=1,IX="" D HEAD^XSXS
	W "Client ",CX,?12,$P(^SC(CX),%,1),?47,"Unpaid Bills",?67,$j(M2,10,2),!
	W "Matter ",MX,?12,$P(^SM(CX,MX),%,1),?47,"Unbilled Disb",?67,$j(M1,10,2),!
	W ?47,"Client Ledger",?67,$j(C1,10,2),!
	W ?47,"Work-in-Progress",?67,$j(W1,10,2),!! D HEAD S N=N+7
P02	S IX=$o(^WORK($j,IX)) I IX="" G END
	D LINE S N=N+1 I N>56 D HEAD^XSXS
	G P02

END	D END^XSXS
	G XSCD

HEAD	W ?31,"------ Office ------",?57,"------ Client ------",!,"Date",?13,"Ref No   Type",?33,"Value",?44,"Balance",?59,"Value",?70,"Balance",!! S (T1,T2)=0 Q

LINE	S ML=^WORK($j,IX),D1=$P(ML,%,1),D2=$P(ML,%,2),D3=$P(ML,%,3),D4=$P(ML,%,4),D5=$P(ML,%,5),D6=$P(ML,%,6),T1=T1+D2,T2=T2+D3
	W D1,?13,D5,?22,D4 W:D2 ?28,$J(D2,10,2),?41,$J(T1,10,2) W:D3 ?54,$J(D3,10,2),?67,$J(T2,10,2) W !
	Q

	;Validation - Matter code (also used by XSCJ)
V01	I X="*" K ERR G V1A
	I $D(ERR) Q
	;get all header info
	S ML=$G(^ML(CX,X)),M1=$J($P(ML,%,1),0,2),M2=$J($P(ML,%,2),0,2)
	S SM=$g(^SM(CX,X)),M8=$p(SM,%,8),M9=$p(SM,%,9)
	S CL=$G(^CL(CX,X)),C1=$J($P(CL,%,1),0,2),C2=$j($p(CL,%,2),0,2)
	S SW=$G(^SW(CX,X)),W1=$J($P(SW,%,1),0,2)
	Q
	;matter code="*"
V1A	S XX="",(M1,M2,C1,W1)=0
V1B	S XX=$o(^SM(CX,XX)) I XX="" G V1C
	S ML=$g(^ML(CX,XX)),M1=M1+$p(ML,%,1),M2=M2+$p(ML,%,2),CL=$g(^CL(CX,XX)),C1=C1+$p(CL,%,1),SW=$g(^SW(CX,XX)),W1=W1+$p(SW,%,1)
	G V1B
V1C	S M1=$j(M1,0,2),M2=$j(M2,0,2),C1=$j(C1,0,2),W1=$j(W1,0,2)
	W ?48,"Total for all matters" Q

CASE	S X=$zconvert(X,"U") Q

XSCE
XSCE	;GRI,,,;02:24 PM  31 Mar 1999
	;=======================================================
	; Virgo Accounts - Nominal Account Enquiry
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:       1064
	; Files updated: none
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, ^OUTPUT, 
	;                HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	S %S=1064 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSCE Q
	S IX="" W @P1 D HEAD
E02	S IX=$O(^AY(AX,IX)) I IX=""!($Y>19) G E09
E03	S AY=^AY(AX,IX),D1=$P(AY,%,1),D2=$P(AY,%,2),D3=$P(AY,%,3),D4=$P(AY,%,4),D5=$P(AY,%,5),E4=E4+D2
	D LINE G E02

E09	W /C(0,22),@P1,"Next, Print or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) G XSCE
	I X="P" G P01
	I X="N" G N01
	I X="\\" G H01
	G E09

	;Next
N01	W /C(0,9),/EF,@P1 G E03:IX'="" S E4=0 G E02

	;Home
H01	W /C(0,9),/EF,@P1 S IX="",E4=0 G E02

	;Print
P01	D ^OUTPUT G:%OP[% XSCE S N=99,P=1 D HEAD^XSXS
	D PRINT,END^XSXS
	G XSCE

	;Print routine
PRINT	W "Account ",AX," : ",$P(^AB(AX),%,1),?49,"Balance ",$j($p(^AB(AX),%,3),0,2),! D HEAD S N=N+4,IX=""
P02	S IX=$O(^AY(AX,IX)) I IX="" Q
P03	S AY=^AY(AX,IX),D1=$P(AY,%,1),D2=$P(AY,%,2),D3=$P(AY,%,3),D4=$P(AY,%,4),D5=$P(AY,%,5),E4=E4+D2
	D LINE S N=N+2 I N>56 D HEAD^XSXS
	G P02

	;Validation - A/c code
V01	Q:$D(ERR)  S A3=$j($p(^AB(X),%,3),0,2)
	Q

CASE	S X=$zconvert(X,"U") Q

HEAD	W !?5,"Date",?18,"Ref No   Type",?49,"Amount",?62,"Balance",!! S E4=0 Q

LINE	W ?5,D1,?18,D4,?27,D3,?46,$J(D2,10,2),?59,$J(E4,10,2),!?6,D5,! Q

XSCF
XSCF	;GRI,,,;10:51 AM  31 Jan 2002;
	;=======================================================
	; Virgo Accounts - Transaction Search
	;
	; Copyright Graham R Irwin 1998-2002
	;
	; Screens:       1069
	; Files updated: ^WORK
	; Subroutines:   X0^XSXS, ^INPUT, ^OUTPUT,
	;                HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	S %S=1069 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSCF Q
	S S3=$zconvert(S1,"L")

	W @P1
	K ^WORK($j) S NX=0

	;Search matter ledger
	S (CX,MX,IX)=""
E02	S CX=$o(^ML(CX)) I CX="" G F01
E03	S MX=$o(^ML(CX,MX)) I MX="" G E02
E04	S IX=$o(^ML(CX,MX,IX)) I IX="" G E03
	S ML=^ML(CX,MX,IX),S2=$zconvert(ML,"L")
	I S2'[S3 G E04
	S NX=NX+1,^WORK($j,NX)="ML"_%_CX_"/"_MX_%_ML
	W /c(0,8),NX," item(s) found",!
	G E04

	;Search client ledger
F01	S (CX,MX,IX)=""
F02	S CX=$o(^CL(CX)) I CX="" G G01
F03	S MX=$o(^CL(CX,MX)) I MX="" G F02
F04	S IX=$o(^CL(CX,MX,IX)) I IX="" G F03
	S CL=^CL(CX,MX,IX),S2=$zconvert(CL,"L")
	I S2'[S3 G F04
	S NX=NX+1,^WORK($j,NX)="CL"_%_CX_"/"_MX_%_CL
	W /c(0,8),NX," item(s) found",!
	G F04

	;Search nominal ledger
G01	S (AX,IX)=""
G02	S AX=$o(^AY(AX)) I AX="" G H01
G04	S IX=$o(^AY(AX,IX)) I IX="" G G02
	S AY=^AY(AX,IX),S2=$zconvert(AY,"L")
	I S2'[S3 G G04
	S NX=NX+1,^WORK($j,NX)="NL"_%_AX_%_AY
	W /c(0,8),NX," item(s) found",!
	G G04

	;Search cashbooks
H01	S (KX,IX)=""
H02	S KX=$o(^CB(KX)) I KX="" G J01
H04	S IX=$o(^CB(KX,IX)) I IX="" G H02
	S CB=^CB(KX,IX),S2=$zconvert(CB,"L")
	I S2'[S3 G H04
	S NX=NX+1,^WORK($j,NX)="CB"_%_KX_%_CB
	W /c(0,8),NX," item(s) found",!
	G H04

	;Search archived disbursements
J01	S (CX,MX,BX,IX)=""
J02	S CX=$o(^SD(CX)) I CX="" G K01
J03	S MX=$o(^SD(CX,MX)) I MX="" G J02
J04	S BX=$o(^SD(CX,MX,BX)) I BX="" G J03
J05	S IX=$o(^SD(CX,MX,BX,IX)) I IX="" G J04
	S SD=^SD(CX,MX,BX,IX),S2=$zconvert(SD,"L")
	I S2'[S3 G J05
	S NX=NX+1,^WORK($j,NX)="SD"_%_CX_"/"_MX_%_SD
	W /c(0,8),NX," item(s) found",!
	G J05

	;Search archived cashbooks
K01	S (KX,IX)=""
K02	S KX=$o(^CA(KX)) I KX="" G L01
K04	S IX=$o(^CA(KX,IX)) I IX="" G K02
	S CA=^CA(KX,IX),S2=$zconvert(CA,"L")
	I S2'[S3 G K04
	S NX=NX+1,^WORK($j,NX)="CA"_%_KX_%_CA
	W /c(0,8),NX," item(s) found",!
	G K04

	;Done searching
L01	I NX=0 W /C(0,22),@P1,"Nothing Found. Press any key to continue ",/EL R *X R:'X *X G XSCF
	D ^OUTPUT I %OP[% G XSCF
	S N=99,P=1 D HEAD^XSXS
	W "Searching for ",S1,!!
	S WX=""
L02	S WX=$o(^WORK($j,WX)) I WX="" G M01
	S WK=^WORK($j,WX)
	W $tr(WK,%," "),!
	G L02

M01	W !,"Prefix:",!,"ML = matter ledger",!,"CL = client ledger",!,"NL = nominal ledger",!,"CB = cashbooks",!,"SD = historic disbursements",!,"CA = archived cashbooks"
	D END^XSXS
	Q

XSCJ
XSCJ	;GRI,,,;01:29 PM  12 Jan 2001;
	;=======================================================
	; Virgo Accounts - Matter Transactions Enquiry
	;
	; Copyright Graham R Irwin 1998-2001
	;
	; Screens:       1015
	; Files updated: ^WORK($j)
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, ^OUTPUT, 
	;                HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	S %S=1015 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSCJ Q
	I MX'="*" G S00

	;Option prompt if matter is *
E08	W /C(0,22),@P1,"Next or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) Q
	I X="N" G XSCJ
	G E08

	;Get ^ML ^SD and ^CL transactions
S00	K ^WORK($j) S IX=""
S01	S IX=$o(^ML(CX,MX,IX)) I IX="" G S03
	S ML=^ML(CX,MX,IX),D1=$p(ML,%,1),D2=$p(ML,%,2),D3=$p(ML,%,3),D4=$p(ML,%,4),D5=$p(ML,%,5),D7=$p(ML,%,7)
	S ^WORK($j,IX)=D1_%_(D3-D2)_%_%_D4_%_D5_%_D7
	G S01

S03	S (IX,BX)=""
S04	S BX=$o(^SD(CX,MX,BX)) I BX="" G S06
	S T1=0
S05	S IX=$o(^SD(CX,MX,BX,IX)) I IX="" D SX1 G S04
	S SD=^SD(CX,MX,BX,IX),D1=$p(SD,%,1),D2=$p(SD,%,2),D3=$p(SD,%,3),D4=$p(SD,%,4),D5=$p(SD,%,5),D7=$p(SD,%,7),T1=T1+D2-D3,IX1=IX
	S ^WORK($j,IX)=D1_%_(D3-D2)_%_%_D4_%_D5_%_D7
	G S05

SX1	S D1=$p($g(^SB(BX)),%,1)
	S ^WORK($j,IX1+.5)=D1_%_T1_%_%_"DSB"_%_BX_%_"Disbursements billed"
	Q

S06	S IX=""
S07	S IX=$o(^CL(CX,MX,IX)) I IX="" G E00
	S ML=^CL(CX,MX,IX),D1=$p(ML,%,1),D2=$p(ML,%,2),D3=$p(ML,%,3),D4=$p(ML,%,4),D5=$p(ML,%,5),D6=$p(ML,%,6)
	S E2="" I $d(^WORK($j,IX)) S E2=$p(^WORK($j,IX),%,2)
	S ^WORK($j,IX)=D1_%_E2_%_(D3-D2)_%_D4_%_D5_%_D6
	G S07

	;Display transactions
E00	W @P1 D HEAD
E02	S IX=$o(^WORK($j,IX)) I IX=""!($Y>20) G E09
E03	D LINE
	G E02

	;Option prompt
E09	W /C(0,22),@P1,"Next, Print or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) G XSCJ
	I X="P" G P01
	I X="N" G N01
	I X="\\" G H01
	G E09

	;Next
N01	W /c(0,13),/ef,@P1 G E03:IX'="" S (T1,T2)=0 G E02

	;Home
H01	W /c(0,13),/ef,@P1 S IX="",(T1,T2)=0 G E02

	;Print
P01	D ^OUTPUT G:%OP[% XSCJ S N=99,P=1,IX="" D HEAD^XSXS
	W "Client ",CX,?12,$P(^SC(CX),%,1),?47,"Unpaid Bills",?67,$j(M2,10,2),!
	W "Matter ",MX,?12,$P(^SM(CX,MX),%,1),?47,"Unbilled Disb",?67,$j(M1,10,2),!
	W ?47,"Client Ledger",?67,$j(C1,10,2),!
	W $$^NEWNAME("@F7@")," ",M8,?47,"Work-in-Progress",?67,$j(W1,10,2),!
	W $$^NEWNAME("@F8@")," ",M9,?47,"Nominal Interest",?67,$j(C2,10,2),!
	W ! D HEAD S N=N+8
P02	S IX=$o(^WORK($j,IX)) I IX="" G END
	D LINE S N=N+2 I N>56 D HEAD^XSXS
	G P02

END	D END^XSXS
	G XSCJ

HEAD	W ?31,"------ Office ------",?57,"------ Client ------",!,"Date",?13,"Ref No   Type",?33,"Value",?44,"Balance",?59,"Value",?70,"Balance",!! S (T1,T2)=0 Q

LINE	S ML=^WORK($j,IX),D1=$P(ML,%,1),D2=$P(ML,%,2),D3=$P(ML,%,3),D4=$P(ML,%,4),D5=$P(ML,%,5),D6=$P(ML,%,6),T1=T1+D2,T2=T2+D3
	W D1,?13,D5,?22,D4 W:D2 ?28,$J(D2,10,2),?41,$J(T1,10,2) W:D3 ?54,$J(D3,10,2),?67,$J(T2,10,2) W !,?2,D6,!
	Q

CASE	S X=$zconvert(X,"U") Q

XSCK
XSCK	;GRI,,,;11:48 AM  6 Jun 2002;
	;=======================================================
	; Virgo Accounts - Daily Bankings
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1079
	; Files updated:
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, ^OUTPUT, 
	;                HEAD^XSXS, END^XSXS, DMY2H^DATE
	;
	;=======================================================

	D X0^XSXS
	S ONE=$g(^SZ("DOC"),1)
	S %S=1079 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSCK Q
	S %DAT=D1 D DMY2H^DATE S D2=%H
	S ALL=0 I DX="*" S ALL=1,DX=$o(^CB("")) G P01
	W @P1 D HEAD

	;Display cashbook transactions
E01	S IX="" W /c(0,9),/ef,@P1
E02	S IX=$O(^CB(DX,IX)),TOT=0 I $Y>19 G E09
	I IX="" G E08
E03	D LINE
	G E02

E08	D TOTAL S (E2,E3)=0,TOT=1

E09	W /C(0,22),@P1 W "Next, Print or Quit ? ",/EL,@P2 D ^GETX S X=$zconvert(X,"U")
	I X="Q"!(X=%) G XSCK
	I X="P" G P01
	I X="N" G N01
	I X="\\" G M01
	G E09

	;Next
N01	W /C(0,9),/EF,@P1
	I TOT G M01
	G E03:IX'="" G E08

	;Home
M01	W /C(0,9),/EF,@P1
	S IX="" G E02

	;Print
P01	D ^OUTPUT G:%OP[% XSCK S N=99,P=1,IX="" D HEAD^XSXS
P1A	W "Cashbook ",DX,?14,$p(^CB(DX),%,1),! D HEAD S N=N+4
P02	S IX=$O(^CB(DX,IX)) I IX="" G P04
	D LINE I N>56 D HEAD^XSXS
	G P02
P04	D TOTAL W !! S N=N+3
	I ALL S DX=$o(^CB(DX)) I DX'="" G P1A
	D END^XSXS
	G XSCK

	;Validation - Cashbook
V01	I X="*" K ERR
	Q

	;Show heading (display or print)
HEAD	W !?5,"Date",?18,"Ref No   Type",?36,"Receipt",?49,"Payment",!! S (E2,E3)=0 Q

LINE	S CB=^CB(DX,IX) I IX=0 Q
	S C1=$P(CB,%,1) S %DAT=C1 D DMY2H^DATE I %H'=D2 Q
	S C2=$P(CB,%,2),C3=$P(CB,%,3),C4=$P(CB,%,4),C5=$P(CB,%,5),C6=$P(CB,%,6),E2=E2+C2,E3=E3+C3
	W ?5,C1,?18,C5,?27,C4 W:C3 ?33,$J(C3,10,2) W:C2 ?46,$J(C2,10,2) W !?6,C6,! I $d(N) S N=N+2
	Q

TOTAL	W ?34,"---------",?47,"---------",!?5,"Total",?33,$j(E3,10,2),?46,$j(E2,10,2) Q

XSCOMPACT
XSCOMPACT	;gri,djb:A:B-DSUTIL;03:41 PM  20 Sep 1993;
	;;Copyright (C) 1990,1991 DataTree Inc. All rights reserved.

	new

select	w !!,"Compact dataset files"
	;s dsid=$$^%dsselect q:dsid
	;s dsid=+$p(dsid,$c(22),2)
	s dsid=772 ;USER-GBL
	d dslist^%dsbuild1 s xdsl=dsid_"|"_dsl(dsid) k dsl
	d ^%dsinfo i '$d(vsignature) s vsignature=0
	s dsfile=$p(dsfile,".",1)
	i (dsid=$v(1,250,-2))!(dsid=$v(1,252,-2)) d badds q
	l ^%dsr(dsid):1 e  w !!,"Another compaction in progress" q
	i dsid=$v(2,926,-2) d badds q
	s MaxRLink=$s(vsignature=58838:65535*65537,1:65535)
	s restart=(indexroot=MaxRLink),err=""
	i restart d  q:err'=""  g begin
	. w !!,"Restarting compaction process"
	. zetrap recovererr
	. c 11 o 11:(file=dsfile_".CHK":mode="r") u 11
	. k ^%dsr($znode,$j),^%dsc($znode,$j)
	. f  r x#2 s x=$zwa(x) q:'x  r x#x,y#2 s y=$zwa(y) r y#y s @x=y
	. c 11 u 0
	. q
	;s f="Data packing factor (in %):3\"
	;s f=f_"Empty page ratio (in %):2\"
	;s f=f_"Repair mode:8:YES,NO\"
	;s v="99\0\NO\",v=$$^%mform(f,v),q=$p(v,$c(22),1),v=$p(v,$c(22),2) q:q
	s packfactor=99,packfactor=$s($l(packfactor):packfactor,1:99)/100
	s efactor=0,efactor=efactor/100
	s repair="NO",repair=(repair="YES")
	s ok=$$check^%dscompact0 q:'ok

begin	w !,"Beginning compaction for ",dsfile," at ",$$^%now
	zitrap intrps^%dscompact
	i repair,'restart d sort^%dscompactr i err'="" l  q
	w !,"Dismounting dataset"
	s ctrlc=0
	zitrap intrp^%dscompact
	d dsmount i err'="" w err q
	s named=dsfile_".DAT",namei=dsfile_".IND"
	s fhd=$$openfile^%dsfileop(named,"w")
	s fhi=$$openfile^%dsfileop(namei,"w")
	i 'fhd!('fhi) w !,"Unable to reopen files!" g closefiles
	s chunksize=$$size^%dsfileop(fhi) ;how much room in "temp" index file?
	i chunksize<datasize!(chunksize<8192) d  g closefiles
	. w "Index file too small"
	c 11 o 11:(file=dsfile_".CHK":mode="w") ;checkpoint file for intrp
	w !,"Compacting data file"
	d ^%dscompact1

closefiles
	b 0
	i fhd s fhd=$$closefile^%dsfileop(fhd)
	i fhi s fhi=$$closefile^%dsfileop(fhi)
	k dbuf,mcache
	s err=$$remount^%dsbuild1(xdsl)
	i err w $zename(err)
	lock
	b 1
	q

dsmount	s err="" zetrap dsmounterr
	zc dsclose(dsid)
	q

dsmounterr
	i $zerr=49187 q
	s err="Can't dismount dataset: "_$zename
	q

recovererr
	. s err="Error reading recovery file "_dsfile_".CHK"
	. u 0 w !,err c 11 q

badds	w !!,"Can't compact that dataset"
	q

intrp	w !!,"Interrupting... please wait"
	s ctrlc=1
	q

intrps	w !!,"Interrupting the sort phase - dataset has not been modified",!
	zquit

exit	q

XSDA
XSDA	;GRI,,,;09:08 AM  18 Jan 2002
	;=======================================================
	; Virgo Accounts - Draft Bill
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1016
	; Files updated: selected DOS file (.txt extension)
	; Subroutines:   X0^XSXS, HEAD^XSXS, END^XSXS,
	;                ^INPUT, ^OUTPUT, ^DOSDEV
	;
	;=======================================================

	D X0^XSXS S PEE="P"
	S %S=1016 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSDA Q
	;get client details
	S SC=^SC(CX),C1=$P(SC,%,1),C7=$P(SC,%,2),C8=$P(SC,%,3)
	S SCA=^SC(CX,"A"),C2=$P(SCA,%,1),C3=$P(SCA,%,2),C4=$P(SCA,%,3),C5=$P(SCA,%,4),C6=$P(SCA,%,5)
	S SCN=^SC(CX,"N"),C9=$P(SCN,%,1),C10=$P(SCN,%,2),C11=$P(SCN,%,3)
	;get matter details
	S SM=^SM(CX,MX),M1=$P(SM,%,1),M2=$P(SM,%,2),M3=$P(SM,%,3),M4=$P(SM,%,4),M5=$P(SM,%,5),M6=$P(SM,%,6),M7=$P(SM,%,7),M8=$P(SM,%,8),M9=$P(SM,%,9),M10=$P(SM,%,10),M14=$p(SM,%,11)
	S SMN=^SM(CX,MX,"N"),M11=$P(SMN,%,1),M12=$P(SMN,%,2),M13=$P(SMN,%,3)
	I PW="P" D ^OUTPUT G:%OP[% XSDA S N=99,P=1 D HEAD^XSXS
	I PW'="P" D ^DOSDEV G:X[% XSDA S N=0
	W "Ref ",CX," ",MX,"  ",M1,!!,C1,!,C2,!,C3,!,C4,!,C5,!,C6,!!!
	I $e(H0,1)'="X" W "Client Account ",$j($g(^CL(CX,MX)),10,2),! S N=N+1
	W "Unpaid Bills   ",$j($p($g(^ML(CX,MX)),%,2),10,2),!
	I +M14'=0 W "Budget (time+disbursements) ",M14,! S N=N+1

	;Time section
	W !!,"Time:",!,?35,"Hours",?45,"Items" S FX="",(T1,T2,T3)=0,N=N+15
W01	S FX=$O(^SW(CX,MX,FX)) I FX="" G D00
	S X3="" W !,$p($g(^SF(FX)),%,1),!! S N=N+3
W03	S X3=$O(^SW(CX,MX,FX,X3)) I X3="" G W01
	S SW=^SW(CX,MX,FX,X3),W1=$P(SW,%,1),W2=$P(SW,%,2),W3=$P(SW,%,3),S1=$G(^SA(X3)),T0=W1 D TCON
	W X3,?5,S1 W:W1 ?39-$L(T0),T0 W:W2 ?45,$J(W2,5,0) W ?53,$J(W3,10,2),! S N=N+1 I PW="P",N>56 D HEAD^XSXS
	S T1=T1+W1,T2=T2+W2,T3=T3+W3 G W03
D00	S T0=T1 D TCON W !,"Total time",?39-$L(T0),T0,?45,$J(T2,5,0),?53,$J(T3,10,2)

	;Disbursements section
	W !!,"Disbursements:",!! S X3="",T1=0,N=N+5
D01	S X3=$O(^ML(CX,MX,X3)) I X3="" G E00
	S ML=^ML(CX,MX,X3),D1=$P(ML,%,1),D2=$P(ML,%,2),D3=$P(ML,%,3),D4=$P(ML,%,4),D7=$P(ML,%,7)
	I "DIS,DSW,DSP,DSA,TOC,COD,OMT,N2D"'[D4 G D01
	I "DSW,DSP,DSA,COD"[D4 S D3=-D2
	I "OMT"=D4,D3="" S D3=-D2
	S T1=T1+D3
	W D1,?12,$e(D7,1,40),?53,$J(D3,10,2),! S N=N+1 I PW="P",N>56 D HEAD^XSXS
	G D01
E00	W !,"Total disbursements",?53,$J(T1,10,2),!!,"Total time and disbursements",?53,$j((T1+T3),10,2),!!,"Matter is VATable ? "
	W:M6="N" "No" W:M6="Y" "Yes" W !

	I PW="P" D END^XSXS
	E  C 10 U 0
	G XSDA

TCON	S A=T0\60_":",T0=T0#60 S:T0<10 T0=0_T0 S T0=A_T0 Q

	;Validation - Print or WP
V01	S X=$zconvert(X,"U") I "PW"'[X S ERR="Must be 'P' or 'W'"
	Q

XSDC
XSDC	;GRI,,,;05:08 PM  15 Aug 1994
	;=======================================================
	; Virgo Accounts - Financial Status Report
	;
	; Copyright Graham R Irwin 1993-94
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS,^OUTPUT Q:%OP[%  S N=99,P=1
	D HEAD^XSXS S XX="----------" W "As at ",DAT,!?36,"Year-to-Date",?58,"Last Year Total",!!
	S AX="N0",(T2,T3)=0
A01	S AX=$O(^AB(AX)) I AX=""!(AX]"N999") G B01
	S AB=^AB(AX),A1=$P(AB,%,1),A3=$P(AB,%,3),A4=$P(AB,%,4)
	W A1,?36,$J(A3,10,2),?60,$J(A4,10,2),!
	S T2=T2+A3,T3=T3+A4 G A01

B01	S CX="" W !!,"Cashbook Balances",!!
B02	S CX=$O(^CB(CX)) I CX="" G END
	S CB=^CB(CX),A1=$P(CB,%,1),A6=$P(CB,%,6)
	W CX,?5,A1,?32,$J(A6,10,2),! G B02

END	D END^XSXS
	Q

XSDD
XSDD	;GRI,,,;04:04 PM  1 Aug 2005
	;=======================================================
	; Virgo Accounts - VAT Report
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1077
	; Files updated: ^SV
	; Subroutines:	 X0^XSXS, ^OUTPUT, HEAD^XSXS, END^XSXS,
	;                ^GETX
	;
	;=======================================================

	D X0^XSXS
	S %S=1077 F %J=1:1 D ^INPUT Q:X="END"!(X[%)

	D ^OUTPUT Q:%OP[%
	S N=99,P=1,XX="----------"
	D HEAD^XSXS

	I A1="Y" G C01

	;Print detail
	W "Sales",!,"=====",!!?5,"Bill",?16,"Date",?35,"Net Amount",?55,"VAT Amount",!! S N=N+5
	S VX="",(T3,T4)=0
A01	S VX=$O(^SV(VX)) G:VX]"P" B01 G:VX="" B01
	S SV=^SV(VX),V1=$P(SV,%,1),V2=$P(SV,%,2),V3=$P(SV,%,3),V4=$P(SV,%,4)
	W ?5,V1,?16,V2,?35,$J(V3,10,2),?55,$J(V4,10,2),! S N=N+1 I N>58 D HEAD^XSXS
	S T3=T3+V3,T4=T4+V4 G A01
B01	W ?35,XX,?55,XX,!?35,$J(T3,10,2),?55,$J(T4,10,2),!! S N=N+3 I N>58 D HEAD^XSXS

	W "Purchases",!,"=========",!!?5,"Ref",?16,"Date",?35,"Net Amount",?55,"VAT Amount",!! S N=N+5 I N>58 D HEAD^XSXS
	S VX="P",(T3,T4)=0
P01	S VX=$O(^SV(VX)) I VX="" G Q01
	S SV=^SV(VX),V1=$P(SV,%,1),V2=$P(SV,%,2),V3=$P(SV,%,3),V4=$P(SV,%,4)
	W ?5,V1,?16,V2,?35,$J(V3,10,2),?55,$J(V4,10,2),! S N=N+1 I N>58 D HEAD^XSXS
	S T3=T3+V3,T4=T4+V4 G P01
Q01	W ?35,XX,?55,XX,!?35,$J(T3,10,2),?55,$J(T4,10,2),!
	D HEAD^XSXS

	;print VAT summary
C01	S SV=$G(^SV),V1=$P(SV,%,1),V2=$P(SV,%,2),V3=$P(SV,%,3),V4=$P(SV,%,4)
	W "VAT Summary",!,"==========="
	W !!," [1]   VAT on Sales",?35,$j($fn(V4,",p",2),16)
	W !!," [2] *",?46,"0.00"
	W !!," [3]   Total VAT due",?35,$j($fn(V4,",p",2),16)
	W !!," [4]   VAT on Purchases",?35,$j($fn(V2,",p",2),16)
	W !!," [5]   Net VAT to be Paid",?37,"-------------"
	W !?7,"(or Reclaimed)",?35,$j($fn(V4-V2,",p",2),16)
	W !?37,"============="
	W !!," [6]   Total Value of Sales",?35,$j($fn(V3,",p",2),16)
	W !!," [7]   Total Value of Purchases",?35,$j($fn(V1,",p",2),16)
	W !!,"* Please note Virgo Accounts does not identify acquisitons or supplies from/to",!,"other EU member states. You must maintain a separate record for these.",!!

	D END^XSXS

E01	W /C(0,22),@P1,"Delete VAT file ? ",/el,@P2 D ^GETX S X=$zconvert(X,"U")
	I X="Y" K ^SV Q
	I X="N" Q
	G E01

XSDE
XSDE	;GRI,,,;02:22 PM  20 Jan 1995
	;=======================================================
	; Virgo Accounts - P&L Account
	;
	; Copyright Graham R Irwin 1993-94
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS,^OUTPUT Q:%OP[%  S N=99,P=1
	D HEAD^XSXS S XX="--------------"
	W "Period Ending ",DAT,!?34,"This Period",?49,"Year-to-Date",?64,"Last Yr Total",!!,"Income",!,"======",!! S N=N+6
	S AX="I0",(T1,T2,T3)=0
A01	S AX=$O(^AB(AX)) I AX=""!(AX]"I999") G B01
	S AB=^AB(AX),A1=$P(AB,%,1),A2=$P(AB,%,2),A3=$P(AB,%,3),A4=$P(AB,%,4)
	D LINE
	S T1=T1+A2,T2=T2+A3,T3=T3+A4
	G A01

B01	W ! S A1="Total Income",A2=T1,A3=T2,A4=T3
	D LINE
	W !,"Expenses",!,"========",!! S N=N+4
	S AX="E0",(T4,T5,T6)=0
C01	S AX=$O(^AB(AX)) I AX=""!(AX]"E999") G D01
	S AB=^AB(AX),A1=$P(AB,%,1),A2=$P(AB,%,2),A3=$P(AB,%,3),A4=$P(AB,%,4)	D LINE
	S T4=T4+A2,T5=T5+A3,T6=T6+A4
	G C01

D01	W ! S A1="Total Expenses",A2=T4,A3=T5,A4=T6
	D LINE
	W !?31,XX,?47,XX,?63,XX,!
	S A1="Profit",A2=T1-T4,A3=T2-T5,A4=T3-T6
	D LINE

	D END^XSXS Q

LINE	W A1,?30,$j($fn(A2,",p",2),15),?46,$j($fn(A3,",p",2),15),?62,$j($fn(A4,",p",2),15),! S N=N+1
	Q

XSDF
XSDF	;GRI,,,;02:21 PM  20 Jan 1995
	;=======================================================
	; Virgo Accounts - Balance Sheet
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS,^OUTPUT Q:%OP[%  S N=99,P=1
	D HEAD^XSXS W "As at ",DAT,!?39,"To-date",?56,"Last Year",!,"Fixed Assets",!,"------------",!! S N=N+6

	S AX="A0",(T1,T2)=0
A01	S AX=$O(^AB(AX)) I AX=""!(AX]"A099") G B01
	S AB=^AB(AX),A1=$P(AB,%,1),A2=$P(AB,%,2),A4=$P(AB,%,4) D LINE
	S T1=T1+A2,T2=T2+A4
	G A01

B01	W ! S A1="Total Fixed Assets",A2=T1,A4=T2 D LINE
	W !,"Current Assets",!,"--------------",!! S N=N+5
	S AX="A1",(T4,T5)=0
C01	S AX=$O(^AB(AX)) I AX=""!(AX]"A999") G D01
	S AB=^AB(AX),A1=$P(AB,%,1),A2=$P(AB,%,2),A4=$P(AB,%,4) D LINE
	S T4=T4+A2,T5=T5+A4
	G C01

D01	W ! S A1="Total Current Assets",A2=T4,A4=T5 D LINE
	W !,"Current Liabilities",!,"-------------------",!! S N=N+4
	S AX="L039",(T7,T8)=0
E01	S AX=$O(^AB(AX)) I AX=""!(AX]"L499") G F01
	S AB=^AB(AX),A1=$P(AB,%,1),A2=$P(AB,%,2),A4=$P(AB,%,4) D LINE
	S T7=T7+A2,T8=T8+A4
	G E01

F01	W ! S A1="Total Current Liabilities",A2=T7,A4=T8 D LINE
	W ! S A1="Net Current Assets",A2=T4-T7,A4=T5-T8 D LINE
	W "==================",!
	W ! S A1="Net Assets",A2=T1+T4-T7,A4=T2+T5-T8 D LINE
	W "==========",!
	W !,"Represented by:",!! S N=N+8

	S AX="L500",(T9,T10)=0
G01	S AX=$O(^AB(AX)) I AX=""!(AX]"L999") G H01
	S AB=^AB(AX),A1=$P(AB,%,1),A2=$P(AB,%,2),A4=$P(AB,%,4) D LINE
	S T9=T9+A2,T10=T10+A4
	G G01

H01	W ! S A1="Total Capital",A2=T9,A4=T10 D LINE

	D END^XSXS Q

LINE	W A1,?32,$j($fn(A2,",p",2),15),?51,$j($fn(A4,",p",2),15),! S N=N+1
	Q

XSDG
XSDG	;GRI,,,;02:13 PM  30 Sep 2001
	;=======================================================
	; Virgo Accounts - Trial Balance
	;
	; Copyright Graham R Irwin 1993-94
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	D ^OUTPUT Q:%OP[%  S N=99,P=1
	D HEAD^XSXS
	W "Account",?44,"Period",?55,"Year-to-Date",!! S N=N+2

	;Income
	S AX="I0",(T1,T2)=0
A01	S AX=$O(^AB(AX)) I AX=""!(AX]"I999") G A10
	D DOIT
	G A01

A10	W ! S N=N+1

	;Expenses
	S AX="E0"
B01	S AX=$O(^AB(AX)) I AX=""!(AX]"E999") G B10
	D DOIT
	G B01

B10	W ! S N=N+1

	;Fixed Assets
	S AX="A0"
C01	S AX=$O(^AB(AX)) I AX=""!(AX]"A099") G C10
	D DOIT
	G C01

C10	W ! S N=N+1

	;Current Assets
	S AX="A1"
D01	S AX=$O(^AB(AX)) I AX=""!(AX]"A999") G D10
	D DOIT
	G D01

D10	W ! S N=N+1

	;Current Liabilities
	S AX="L039"
E01	S AX=$O(^AB(AX)) I AX=""!(AX]"L499") G E10
	D DOIT
	G E01

E10	W ! S N=N+1

	;Long-term Liabilities
	S AX="L500"
F01	S AX=$O(^AB(AX)) I AX=""!(AX]"L999") G X01
	D DOIT
	G F01

X01	W !,"Total:",?40,$J(T1,10,2),?55,$J(T2,10,2),!
	D END^XSXS
	Q

	;Process one account line
DOIT	S AB=^AB(AX),A1=$P(AB,%,1),A2=$P(AB,%,2),A3=$P(AB,%,3)
	W AX," ",A1,?40,$j(A2,10,2),?55,$J(A3,10,2),!
	S N=N+1 I N>56 D HEAD^XSXS
	I "IL"[$e(AX,1) S A2=-A2,A3=-A3
	S T1=T1+A2,T2=T2+A3
	Q

XSDH
XSDH	;GRI,,,;01:47 PM  28 Sep 2000
	;=======================================================
	; Virgo Accounts - Unpaid Disbursements List
	;
	; Copyright Graham R Irwin 1993-98
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS,^OUTPUT Q:%OP[%
	S N=99,P=1 D HEAD^XSXS,HEAD
	S E4=0,(CX,MX,IX)=""
P02	S CX=$o(^ML(CX)) I CX="" G END
P03	S MX=$o(^ML(CX,MX)) I MX="" G P02
P04	S IX=$o(^ML(CX,MX,IX)) I IX="" G P03
	S ML=^ML(CX,MX,IX),D1=$P(ML,%,1),D2=$P(ML,%,2),D3=$P(ML,%,3),D4=$P(ML,%,4),D7=$P(ML,%,7),C1=$p(^SC(CX),%,1)
	I "DIS,DSW,DSP,DSA,TOC,COD,OMT,N2D"[D4 D LINE
	G P04

END	D END^XSXS
	Q

	;Write header line
HEAD	W !,"  Client Matter  Date",?32,"Type",?42,"Debit",?54,"Credit",?67,"Balance",!!
	Q
	;Write detail line
LINE	S E4=E4+D3-D2 W ?3,CX,?10,MX,?17,D1,?32,D4 W:D3 ?38,$J(D3,10,2) W:D2 ?51,$J(D2,10,2) W ?64,$J(E4,10,2),!?2,C1," - ",D7,!!
	S N=N+3 I N>56 D HEAD^XSXS,HEAD
	Q

XSDI
XSDI	;GRI,,,;03:45 PM  4 Jun 2003
	;=======================================================
	; Virgo Accounts - Bills Analysis
	;
	; Copyright Graham R Irwin 1993-2003
	;
	; Screens:       1018, 1041, 1070
	; Files updated: ^WORK($j)
	; Subroutines:	 X0^XSXS, ^INPUT, DMY2H^DATE, ^OUTPUT,
	;                HEAD^XSXS, END^XSXS, ^NEWNAME
	;
	;=======================================================

	; A1 - From date		A2 - To date
	; A3 - Option			A4 - Legal aid option
	; T1 - Total time		T2 - Total disbs
	; T3 - Total no of bills
	; T9 - Total time		T0 - Total time + disbs

	D X0^XSXS
	S %S=1018 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSDI Q

	W !?8,@P1,"Do you wish to analyse bills by:-"
	W !!?8,"A - ",$$^NEWNAME("@F7@")
	W !?8,"B - ",$$^NEWNAME("@F8@")
	W !?8,"C - Client",!!
	S %S=1041 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSDI
	S %S=1070 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSDI
	I A4="" S A4="YN"

	W /C(0,22),@P1,"Sorting, please wait ..."
	;walk the bills register and create work file
	K ^WORK($j) S BX="",(T0,T9)=0
G01	S BX=$o(^SB(BX)) I BX="" G H01
	S SB=^SB(BX),B1=$p(SB,%,1),CX=$p(SB,%,2),MX=$p(SB,%,3),B4=$p(SB,%,4),B5=$p(SB,%,5),B9=$p(SB,%,9)
	I B9="C" G G01
	I '$d(^SM(CX,MX)) S (M8,M9)="??",M3=""
	E  S SM=^SM(CX,MX),M8=$p(SM,%,8),M9=$p(SM,%,9),M3=$p(SM,%,3)
	I A4'[M3 G G01
	S:A3="A" X1=M8 S:A3="B" X1=M9 S:A3="C" X1=CX S:X1="" X1="null"
	S %DAT=B1 D DMY2H^DATE
	I D1'>%H,%H'>D2 D ADDIN
	G G01

	;Print
H01	D ^OUTPUT G:%OP[% XSDI
	S (T1,T2,T3)=0,N=99,P=1 D HEAD^XSXS
	W "From ",A1," to ",A2," - ",$$^NEWNAME("@F1@")," = " W:$L(A4)=1 A4 W:$L(A4)=2 "Y or N" W !!
	W ?19,"Billed Time  Billed Disb  Total Bills  No Bills    Average",!
	W ?19,"/% of Total",?45,"/% of Total  /Matters",!
	S N=N+4,X1=""
H02	S X1=$o(^WORK($j,X1)) I X1="" G J01
	S WK=^WORK($j,X1),W1=$p(WK,%,1),W2=$p(WK,%,2),W3=$p(WK,%,3),W4=$p(WK,%,4)
	I A3="A" W $$^NEWNAME("@F7@")," = ",X1
	I A3="B" W $$^NEWNAME("@F8@")," = ",X1
	I A3="C" W "Client ",X1 I $d(^SC(X1)) W " - ",$p(^SC(X1),%,1) 
	W !?18,$j($fn(W1,",",2),12),?31,$j($fn(W2,",",2),12),?44,$j($fn(W1+W2,",",2),12),?64-$l(W3),W3,?65,$j($fn(W1+W2/W3,",",2),12),!
	W ?19,$j(W1*100/T9,10,1),"%",?45,$j((W1+W2)*100/T0,10,1),"%",?64-$l(W4),W4,?65,$j($fn(W1+W2/W4,",",2),12),!
	S T1=T1+W1,T2=T2+W2,T3=T3+W3,N=N+3 I N>56 D HEAD^XSXS
	G H02

J01	W ?19,"-----------  -----------  -----------  ------",!
	W "Totals",?18,$j($fn(T1,",",2),12),?31,$j($fn(T2,",",2),12),?44,$j($fn(T1+T2,",",2),12),?64-$l(T3),T3,!
	D END^XSXS
	Q

	;Add to work file
	; ^WORK($j,X1) = W1, W2, W3 where:
	; X1 = client code, analysis A or analysis B
	; W1 = total fees, W2 = total disbs, W3 = no of bills
ADDIN	S WK=$g(^WORK($j,X1))
	S W1=$p(WK,%,1)+B4,W2=$p(WK,%,2)+B5,W3=$p(WK,%,3)+1,W4=$p(WK,%,4)
	I '$d(^WORK($j,X1,CX,MX)) S W4=W4+1
	S ^WORK($j,X1)=W1_%_W2_%_W3_%_W4,^WORK($j,X1,CX,MX)=""
	S T0=T0+B4+B5,T9=T9+B4
	Q

	;Validation - also used by several other routines!
	;From date
V01	Q:$d(ERR)  S %DAT=X D DMY2H^DATE S D1=%H
	I %H>+$H S ERR="May not be in the future"
	Q
	;To date
V02	Q:$d(ERR)  S %DAT=X D DMY2H^DATE S D2=%H
	I D1>%H S ERR="May not be earlier than From date"
	Q
	;Option
V03	I "ABC"'[X S ERR="Must be 'A', 'B' or 'C'"
	Q

XSDJ
XSDJ	;GRI,,,;01:53 PM  9 Jul 2002
	;=======================================================
	; Virgo Accounts - Exception Report
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1062
	; Files updated: ^WORK($j)
	; Subroutines:   X0^XSXS, ^INPUT, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	; A1, A2 bills < >		M2, V2 bills balance
	; A3, A4 disbs < >		M1, V1 disbs balance
	; A5, A6 client acct < >	C1, V4 client acct bal
	; A7, A8 WIP < >		W1, V3 WIP balance
	; A9 some/all		A10 'or'/'and'
	; A11, A12 client int		C2, V5 client int

	D X0^XSXS
	S (A5,A6,A11,A12)="",(T1,T2,T3,T4,T5)=0
	S %S=1062 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSDJ Q
	S A10="and" I A9="S" S A10="or"

	W /C(0,22),@P1,"Searching, please wait ..."
	K ^WORK($j) S (CX,MX)="",NC=0
G01	S CX=$o(^SM(CX)) I CX="" G H01
G02	S MX=$o(^SM(CX,MX)) I MX="" G G01
	S ML=$g(^ML(CX,MX)),M1=$p(ML,%,1),M2=$p(ML,%,2),W1=+$g(^SW(CX,MX))
	S CL=$g(^CL(CX,MX)),C1=$p(CL,%,1),C2=$p(CL,%,2)
	I A9="A" G G20

	;Process Some option
	I M2>A1,M2<A2 D SETWK G G02
	I M1>A3,M1<A4 D SETWK G G02
	I $e(H0,1)'="X",C1>A5,C1<A6 D SETWK G G02
	I $e(H0,1)'="X",C2>A11,C2<A12 D SETWK G G02
	I W1>A7,W1<A8 D SETWK G G02
	G G02

	; Handle All option
G20	I M2>A1,M2<A2,M1>A3,M1<A4,($e(H0,1)'="X"&(C1>A5)&(C1<A6)&(C2>A11)&(C2<A12)),W1>A7,W1<A8 D SETWK G G02
	G G02

H01	I NC=0 W /C(0,22),@P1,"No matters found. Press any key to continue " R *X R:'X *X G XSDJ
	W /C(8,16),NC," matter" W:NC>1 "s" W " found"
	D ^OUTPUT G:%OP[% XSDJ S N=99,P=1 D HEAD^XSXS
	W "List of matters with: Bills > ",A1," and < ",A2,!
	W ?18,A10," Disbursements > ",A3," and < ",A4,!
	I $e(H0,1)'="X" W ?18,A10," Client account > ",A5," and < ",A6,!
	I $e(H0,1)'="X" W ?18,A10," Client interest > ",A11," and < ",A12,!
	W ?18,A10," Work-in-Progress > ",A7," and < ",A8,!!
	S (CX,MX)="",N=N+5
H02	S CX=$o(^WORK($j,CX)) I CX="" G J01
H03	S MX=$o(^WORK($j,CX,MX)) I MX="" G H02
	S SC=^SC(CX),C1=$p(SC,%,1),SM=^SM(CX,MX),M1=$P(SM,%,1),ML=$g(^ML(CX,MX)),V1=$p(ML,%,1),V2=$p(ML,%,2),V3=+$g(^SW(CX,MX)),CL=$g(^CL(CX,MX)),V4=$p(CL,%,1),V5=$p(CL,%,2)
	W "Client ",CX,?12,C1,?56,"Bills",$j(V2,12,2),! S T2=T2+V2,N=N+1
	W "Matter ",MX,?12,M1,?56,"Disbs",$j(V1,12,2),! S T1=T1+V1,N=N+1
	I $e(H0,1)'="X" W ?56,"CL",$j(V4,15,2),! S T4=T4+V4,N=N+1
	I $e(H0,1)'="X" W ?56,"C Int",$j(V5,12,2),! S T5=T5+V5,N=N+1
	W ?56,"WIP",$j(V3,14,2),!! S T3=T3+V3,N=N+2 I N>58 D HEAD^XSXS
	G H03

J01	W "Totals for matters listed",?56,"Bills",$j(T2,12,2),!
	W ?56,"Disbs",$j(T1,12,2),!
	I $e(H0,1)'="X" W ?56,"CL",$j(T4,15,2),!
	I $e(H0,1)'="X" W ?56,"C Int",$j(T5,12,2),!
	W ?56,"WIP",$j(T3,14,2),!
	D END^XSXS
	Q

SETWK	I '$d(^WORK($j,CX,MX)) S ^WORK($j,CX,MX)="",NC=NC+1
	Q

	;Validation - Bills <
V02	I X<A1 S ERR="Cannot be less than previous figure"
	Q
	;Disb <
V04	I X<A3 S ERR="Cannot be less than previous figure"
	Q
	;CL <
V06	I X<A5 S ERR="Cannot be less than previous figure"
	Q
	;Client int <
V07	I X<A11 S ERR="Cannot be less than previous figure"
	Q
	;WIP <
V08	I X<A7 S ERR="Cannot be less than previous figure" Q
	I A1="",A2="",A3="",A4="",A5="",A6="",A7="",X="" S ERR="Select one or more of the above options" Q
	Q
	;All/Some
V09	I $d(ERR) Q
	D CASE I "AS"'[X S ERR="Must be 'A' or 'S'"
	Q

CASE	S X=$zconvert(X,"U") Q

XSDK
XSDK	;GRI,,,;05:40 PM  29 Mar 1999;
	;=======================================================
	; Virgo Accounts - Matter Balances Analysis
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:       1041, 1067
	; Files updated: ^WORK($j)
	; Subroutines:	 X0^XSXS, ^INPUT, ^OUTPUT, HEAD^XSXS,
	;                END^XSXS, ^NEWNAME
	;
	;=======================================================

	D X0^XSXS S YES="Y"

	W !?8,@P1,"Do you wish to list matters by:-"
	W !!?8,"A - ",$$^NEWNAME("@F7@")
	W !?8,"B - ",$$^NEWNAME("@F8@")
	W !?8,"C - Client",!!
	S %S=1041 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSDK Q
	S %S=1067 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSDK

	W /C(0,22),@P1,"Sorting, please wait ..."
	K ^WORK($j) S (CX,MX)=""
G01	S CX=$o(^SM(CX)) I CX="" G H01
G02	S MX=$o(^SM(CX,MX)) I MX="" G G01
	S SM=^SM(CX,MX),M8=$p(SM,%,8),M9=$p(SM,%,9)
	S:A3="A" X1=M8 S:A3="B" X1=M9 S:A3="C" X1=CX S:X1="" X1="null"
	S ML=$g(^ML(CX,MX)),M1=$p(ML,%,1),M2=$p(ML,%,2),CL=+$g(^CL(CX,MX)),SW=+$g(^SW(CX,MX))
	I A1="N"!(M1!M2!CL!SW) S ^WORK($j,X1,CX,MX)=(M1+M2)_%_CL_%_SW
	G G02

	;Print
H01	D ^OUTPUT G:%OP[% XSDK
	S N=99,P=1 D HEAD^XSXS
	S (X1,CX,MX)="",(T1,T2,T3,T4,T5,T6)=0
	D HEAD
H02	S X1=$o(^WORK($j,X1)) I X1="" G J01
H03	S CX=$o(^WORK($j,X1,CX)) I CX="" G H10
H04	S MX=$o(^WORK($j,X1,CX,MX)) I MX="" G H03
	S WK=^WORK($j,X1,CX,MX),W1=$p(WK,%,1),W2=$p(WK,%,2),W3=$p(WK,%,3)
	W CX,"/",MX,?10,$p(^SC(CX),%,1) D LINE I N>56 D HEAD^XSXS
	S T1=T1+W1,T2=T2+W2,T3=T3+W3
	G H04

H10	F I=1:1:78 W "-"
	W !,"Total for "
	I A3="A" W $$^NEWNAME("@F7@")," = "
	I A3="B" W $$^NEWNAME("@F8@")," = "
	I A3="C" W "Client "
	S W1=T1,W2=T2,W3=T3 W X1 D LINE
	F I=1:1:78 W "="
	W !! S N=N+3,T4=T4+T1,T5=T5+T2,T6=T6+T3,(T1,T2,T3)=0
	;I ... D HEAD^XSXS,HEAD
	I N>56 D HEAD^XSXS
	G H02

J01	W "TOTAL FOR FIRM" S W1=T4,W2=T5,W3=T6 D LINE
	D END^XSXS
	Q

LINE	W ?41,$j($fn(W3,"t",2),12),?53,$j($fn(W1,"t",2),12),?65,$j($fn(W2,"t",2),12),! S N=N+1
	Q

HEAD	W "Client/Matter Code & Client Name",?47,"W-I-P",?58,"Office",?70,"Client",!! S N=N+2
	Q

XSDL
XSDL	;GRI,,,;11:33 AM  16 Mar 1999;
	;=======================================================
	; Virgo Accounts - Legal Aid Work Analysis
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:       1018, 1041
	; Files updated: ^WORK($j)
	; Subroutines:	 X0^XSXS, ^INPUT, DMY2H^DATE, ^OUTPUT,
	;                HEAD^XSXS, END^XSXS, ^NEWNAME
	;
	;=======================================================

	D X0^XSXS
	S %S=1018 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSDL Q

	W !?8,@P1,"Do you wish to analyse bills by:-"
	W !!?8,"A - ",$$^NEWNAME("@F7@")
	W !?8,"B - ",$$^NEWNAME("@F8@")
	W !?8,"C - Client",!!
	S %S=1041 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSDL

	W /C(0,22),@P1,"Sorting, please wait ..."
	;walk the bills register and create work file
	K ^WORK($j) S BX=""
G01	S BX=$o(^SB(BX)) I BX="" G H01
	S SB=^SB(BX),B1=$p(SB,%,1),CX=$p(SB,%,2),MX=$p(SB,%,3),B4=$p(SB,%,4),B5=$p(SB,%,5),B9=$p(SB,%,9)
	I B9="C" G G01
	I '$d(^SM(CX,MX)) S (M3,M8,M9)="??"
	E  S SM=^SM(CX,MX),M8=$p(SM,%,8),M9=$p(SM,%,9),M3=$p(SM,%,3)
	I M3'="Y" G G01
	S Q1=$g(^SM(CX,MX,"C")) I Q1="" G G01
	S:A3="A" X1=M8 S:A3="B" X1=M9 S:A3="C" X1=CX S:X1="" X1="null"
	S %DAT=Q1 D DMY2H^DATE
	I D1'>%H,%H'>D2 D ADDIN
	G G01

	;Print
H01	D ^OUTPUT G:%OP[% XSDL
	S (T1,T2,T3)=0,N=99,P=1 D HEAD^XSXS
	W "From ",A1," to ",A2,?40,$$^NEWNAME("@F1@")," = Y",!!
	W ?19,"Billed Time  Billed Disb  Total Bills  No Bills    Average",!
	W ?59,"Matters",!
	S N=N+4,X1=""
H02	S X1=$o(^WORK($j,X1)) I X1="" G J01
	S WK=^WORK($j,X1),W1=$p(WK,%,1),W2=$p(WK,%,2),W3=$p(WK,%,3),W4=$p(WK,%,4)
	I A3="A" W $$^NEWNAME("@F7@")," = ",X1
	I A3="B" W $$^NEWNAME("@F8@")," = ",X1
	I A3="C" W "Client ",X1 I $d(^SC(X1)) W " - ",$p(^SC(X1),%,1) 
	W !?18,$j($fn(W1,",",2),12),?31,$j($fn(W2,",",2),12),?44,$j($fn(W1+W2,",",2),12),?64-$l(W3),W3,?65,$j($fn(W1+W2/W3,",",2),12),!
	W ?64-$l(W4),W4,?65,$j($fn(W1+W2/W4,",",2),12),!
	S T1=T1+W1,T2=T2+W2,T3=T3+W3,N=N+3 I N>56 D HEAD^XSXS
	G H02

J01	W ?19,"-----------  -----------  -----------  ------",!
	W "Totals",?18,$j($fn(T1,",",2),12),?31,$j($fn(T2,",",2),12),?44,$j($fn(T1+T2,",",2),12),?64-$l(T3),T3,!
	D END^XSXS
	Q

	;Add to work file
	; ^WORK($j,X1) = W1, W2, W3 where:
	; X1 = client code, analysis A or analysis B
	; W1 = total fees, W2 = total disbs, W3 = no of bills
ADDIN	S WK=$g(^WORK($j,X1))
	S W1=$p(WK,%,1)+B4,W2=$p(WK,%,2)+B5,W3=$p(WK,%,3)+1,W4=$p(WK,%,4)
	I '$d(^WORK($j,X1,CX,MX)) S W4=W4+1
	S ^WORK($j,X1)=W1_%_W2_%_W3_%_W4,^WORK($j,X1,CX,MX)=""
	Q

	;Validation - From date
V01	Q:$d(ERR)  S %DAT=X D DMY2H^DATE S D1=%H
	I %H>+$H S ERR="May not be in the future"
	Q
	;To date
V02	Q:$d(ERR)  S %DAT=X D DMY2H^DATE S D2=%H
	I D1>%H S ERR="May not be earlier than From date"
	Q
	;Option
V03	I "ABC"'[X S ERR="Must be 'A', 'B' or 'C'"
	Q

XSDM
XSDM	;GRI,,,;09:33 AM  4 Jun 2003;
	;=======================================================
	; Virgo Accounts - Matter Balances Listing
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1076
	; Files updated: ^WORK($j)
	; Subroutines:   X0^XSXS, ^INPUT, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	S YES="Y" D X0^XSXS
	S %S=1076 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSDM Q
	D ^OUTPUT I %OP[% Q
	S N=99,P=1 D HEAD^XSXS

	S (CX,MX)="",(T1,T2,T3,T4)=0

G01	S CX=$o(^SM(CX)) I CX="" G END
G02	S MX=$o(^SM(CX,MX)) I MX="" G G01
	I A9="Y",$d(^SM(CX,MX,"C")) G G02
	S SC=^SC(CX),C1=$p(SC,%,1),SM=^SM(CX,MX),M1=$p(SM,%,1)
	S ML=$g(^ML(CX,MX)),V1=$p(ML,%,1),V2=$p(ML,%,2),V3=+$g(^SW(CX,MX)),V4=+$g(^CL(CX,MX))
	W "Client ",CX,?12,C1,?49,"Bills Balance",$j($fn(V2,",t",2),14),! S T2=T2+V2,N=N+1
	W "Matter ",MX,?12,M1,?49,"Disbursements",$j($fn(V1,",t",2),14),! S T1=T1+V1,N=N+1
	I $e(H0,1)'="X" W ?49,"Client Ledger",$j($fn(V4,",t",2),14),! S T4=T4+V4,N=N+1
	W ?49,"Work-in-Progr",$j($fn(V3,",t",2),14),! S T3=T3+V3,N=N+1
	D DATECHK
	W ?49,"Last movement   " W:DL'=0 %DAT W !! S N=N+2 I N>58 D HEAD^XSXS
	G G02

END	D END^XSXS
	Q

	;get date of last movement
DATECHK	S DL=0,X3=""
D01	S X3=$o(^ML(CX,MX,X3)) I X3="" G D11
	S ML=^ML(CX,MX,X3),%DAT=$p(ML,%,1) D DMY2H^DATE I %H>DL S DL=%H
	G D01
D11	S X3=$o(^CL(CX,MX,X3)) I X3="" G D21
	S CL=^CL(CX,MX,X3),%DAT=$p(CL,%,1) D DMY2H^DATE I %H>DL S DL=%H
	G D11
D21	S %H=DL D H2DMY^DATE
	Q

CASE	S X=$zconvert(X,"U") Q

XSDN
XSDN	;GRI,,,;01:10 PM  14 Feb 2002;
	;=======================================================
	; Virgo Accounts - Budget Variance Report
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1078
	; Files updated: none
	; Subroutines:   X0^XSXS, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	S %S=1078 F %J=1:1 D ^INPUT Q:X="END"!(X[%)

	D ^OUTPUT Q:%OP[%  S N=99,P=1
	D HEAD^XSXS
	W "Period ",Q1,!! S N=N+2
	W "Account",?40,"Balance",?52,"Budget   Variance   Var %",!! S N=N+2

	;Income
	S AX="I0",(T1,T2)=0
A01	S AX=$O(^AB(AX)) I AX=""!(AX]"I999") G A10
	D DOIT
	G A01

A10	W !,"Total Income" S XX=1 D DTOT

	;Expenses
	S AX="E0",(T1,T2)=0
B01	S AX=$O(^AB(AX)) I AX=""!(AX]"E999") G B10
	D DOIT
	G B01

B10	W !,"Total Expenses" S XX=0 D DTOT

	;Fixed Assets
	S AX="A0",(T1,T2)=0
C01	S AX=$O(^AB(AX)) I AX=""!(AX]"A099") G C10
	D DOIT
	G C01

C10	W !,"Total Fixed Assets" S XX=0 D DTOT

	;Current Assets
	S AX="A1",(T1,T2)=0
D01	S AX=$O(^AB(AX)) I AX=""!(AX]"A999") G D10
	D DOIT
	G D01

D10	W !,"Total Current Assets" D DTOT


	;Current Liabilities
	S AX="L039",(T1,T2)=0
E01	S AX=$O(^AB(AX)) I AX=""!(AX]"L499") G E10
	D DOIT
	G E01

E10	W !,"Total Current Liabilities" S XX=1 D DTOT

	;Long-term Liabilities
	S AX="L500",(T1,T2)=0
F01	S AX=$O(^AB(AX)) I AX=""!(AX]"L999") G F10
	D DOIT
	G F01

F10	W !,"Total Long-term Liabilities" D DTOT

X01	D END^XSXS
	Q

	;Process one account line
DOIT	S AB=^AB(AX),A1=$P(AB,%,1),A3=$P(AB,%,3)
	I AX="L530" S A3=$p(AB,%,2)	;what can I say!
	S B1=$g(^NB(AX))*Q1,V1=B1-A3,V2="--"
	I "IL"[$e(AX,1) S V1=-V1
	I +B1'=0 S V2=V1/B1*100
	W AX," ",A1,?37,$j(A3,10,2),?48,$j(B1,10,2),?59,$j(V1,10,2)
	I V2'="--" W ?70,$j(V2,7,1),!
	I V2="--" W ?74,V2,!
	S N=N+1 I N>56 D HEAD^XSXS
	S T1=T1+A3,T2=T2+B1
	Q

	;Process total line
DTOT	S V1=T2-T1,V2="--"
	I XX S V1=-V1
	I T2'=0 S V2=V1/T2*100
	W ?37,$j(T1,10,2),?48,$j(T2,10,2),?59,$j(V1,10,2)
	I V2'="--" W ?70,$j(V2,7,1),!!
	I V2="--" W ?74,V2,!!
	S N=N+3 I N>56 D HEAD^XSXS
	Q

XSDO
XSDO	;GRI,,,;05:14 PM  2 Nov 2000;
	;=======================================================
	; Virgo Accounts - Cash Received Analysis
	;
	; Copyright Graham R Irwin 1993-2000
	;
	; Screens:       1018, 1041
	; Files updated: ^WORK($j)
	; Subroutines:	 X0^XSXS, ^INPUT, DMY2H^DATE, ^OUTPUT,
	;                HEAD^XSXS, END^XSXS, ^NEWNAME
	;
	;=======================================================

	D X0^XSXS
	S %S=1018 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSDO Q

	W !?8,@P1,"Do you wish to analyse cash received by:-"
	W !!?8,"A - ",$$^NEWNAME("@F7@")
	W !?8,"B - ",$$^NEWNAME("@F8@")
	W !?8,"C - Client",!!
	S %S=1041 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSDO

	W /C(0,22),@P1,"Sorting, please wait ..."
	;walk the matter ledger and create work file
	K ^WORK($j) S (CX,MX,X3)=""
G01	S CX=$o(^ML(CX)) I CX="" G H01
G02	S MX=$o(^ML(CX,MX)) I MX="" G G01
G03	S X3=$o(^ML(CX,MX,X3)) I X3="" G G02
	S ML=^ML(CX,MX,X3),MLA1=$p(ML,%,1),MLA2=$p(ML,%,2),MLA4=$p(ML,%,4)
	I "COB;BPY"'[MLA4 G G03
	S SM=^SM(CX,MX),M8=$p(SM,%,8),M9=$p(SM,%,9)
	S:A3="A" X1=M8 S:A3="B" X1=M9 S:A3="C" X1=CX S:X1="" X1="null"
	S %DAT=MLA1 D DMY2H^DATE
	I D1'>%H,%H'>D2 D ADDIN
	G G03

	;Print
H01	D ^OUTPUT G:%OP[% XSDO
	S T1=0,N=99,P=1 D HEAD^XSXS
	W "From ",A1," to ",A2,!!
	W ?2,"Date",?14,"Client/Matter",?66,"Amount",!
	S N=N+3,X1=""

H02	S X1=$o(^WORK($j,X1)) I X1="" G J01
	S WK=^WORK($j,X1),W1=$p(WK,%,1)
	W !
	I A3="A" W $$^NEWNAME("@F7@")," = ",X1
	I A3="B" W $$^NEWNAME("@F8@")," = ",X1
	I A3="C" W "Client ",X1 I $d(^SC(X1)) W " - ",$p(^SC(X1),%,1) 
	W ?49,"Total",?62,$j($fn(W1,",",2),12),?31,!!
	S T1=T1+W1,N=N+3 I N>56 D HEAD^XSXS
H10	S CX=$o(^WORK($j,X1,CX)) I CX="" G H02
H11	S MX=$o(^WORK($j,X1,CX,MX)) I MX="" G H10
H12	S X3=$o(^WORK($j,X1,CX,MX,X3)) I X3="" G H11
	S WKA=^WORK($j,X1,CX,MX,X3),WKA1=$p(WKA,%,1),WKA2=$p(WKA,%,2)
	S CLA1=$p(^SC(CX),%,1)
	W ?2,WKA1,?14,CX,"/",MX,?25,CLA1,?60,$j($fn(WKA2,",",2),12),! S N=N+1 I N>56 D HEAD^XSXS
	G H12

J01	W !?62,"------------",!
	W ?49,"TOTAL",?62,$j($fn(T1,",",2),12),?31,!
	D END^XSXS
	Q

	;Add to work file
	; ^WORK($j,X1) = W1 where:
	; X1 = client code, analysis A or analysis B
	; W1 = total fees
ADDIN	S WK=$g(^WORK($j,X1))
	S W1=$p(WK,%,1)+MLA2
	S ^WORK($j,X1)=W1,^WORK($j,X1,CX,MX,X3)=MLA1_%_MLA2
	Q

XSEA
XSEA	;GRI,,,;02:46 PM  9 Jun 1997
	;=======================================================
	; Virgo Accounts - Post Time Sheet
	;
	; Copyright Graham R Irwin 1993-1997
	;
	; Screens:       1022, 1045
	; Files updated: ^SW ^TE ^WORK($j) ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, DMY2H^DATE, 
	;                YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	D X0^XSXS
	S TT="TS",D5="Time sheet",ONE=1
	S %S=1022 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% Q:%J=1  G XSEA

	W @P1,!?8,"Client Matter Activity",?52,"Units",?60,"Items",!!
	S WS=1 K ^WORK($j)
I01	W /C(0,16),/EF S WX=WS,F1=1,(A4,A5)=""
	S %S=1045 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G I01:%J>1 S WS=WX,A1="",F1=0 G N01
E09	W /C(0,22),@P1,"Update, Amend, Delete or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) S A1="" G N01
	I X="A" G A01
	I X="U" G U01
	I X="D" W @P1,"  Are you sure ? ",@P2 D ^GETX,CASE I X="Y" K ^WORK($j,WX) S A1="" G N01
	G E09

	;Update individual time entry
U01	S ^WORK($j,WX)=CX_%_MX_%_A3_%_A4_%_A5_%_RX,WS=WS+1
	W /C(0,9),/EF,@P1 S A1=WX-6
U03	S A1=$O(^WORK($j,A1)) I A1=""!($Y>20) G I01:F1,L01
U04	S WK=^WORK($j,A1),CX=$P(WK,%,1),MX=$P(WK,%,2),A3=$P(WK,%,3),A4=$P(WK,%,4),A5=$P(WK,%,5),B1=$P($G(^SA(A3)),%,1)
	W @P1,A1,?9,CX,?16,MX,?22,A3,?25,B1,?53,A4,?61,A5,!
	G U03

L01	W /C(0,22),@P1,"Next, Select, Insert, Update or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Forget entire batch ? ",@P2 D ^GETX,CASE G XSEA:X="Y",L01
	I X="N" G N01
	I X="S"!(X="Z") G S01
	I X="I" G I01
	I X="U" G U11
	G L01

	;Update time records
U11	S WX="",T0=0,U1=^SZ("TU")
U20	S WX=$O(^WORK($j,WX)) I WX="" G U99
	S WK=^WORK($j,WX),CX=$P(WK,%,1),MX=$P(WK,%,2),W3=$P(WK,%,3),W4=$P(WK,%,4),W5=$P(WK,%,5),RX=$p(WK,%,6)
	S SR=^SR(RX,W3),S1=$P(SR,%,1),S2=$P(SR,%,2)
	S SW=$G(^SW(CX,MX,FX,W3)),T1=$P(SW,%,1),T2=$P(SW,%,2),T3=$P(SW,%,3)
	S W4=W4*U1,T9=$J(W4*S1/60+(W5*S2),0,2),T1=T1+W4,T2=T2+W5,T3=T3+T9,T0=T0+T9,^SW(CX,MX,FX,W3)=T1_%_T2_%_T3_%
	S SW=$G(^SW(CX,MX)),W1=$P(SW,%,1)+T9,(W2,%DAT)=$P(SW,%,2) D DMY2H^DATE
	S AY=%H,%DAT=A0 D DMY2H^DATE S:%H>AY W2=A0 S ^SW(CX,MX)=W1_%_W2_%
	;detailed time?
	I $g(^SZ("DTE"))="Y" S IX=$g(^TE)+1,^TE=IX,^TE(CX,MX,FX,IX)=A0_%_W3_%_W4_%_W5_%_T9
	G U20

U99	D YX^XSXS
	D UPAB^XSXS("N020",T0,A0,TT,"",D5)
	D UPAB^XSXS("N030",T0,A0,TT,"",D5)
	G XSEA

	;Next
N01	W /C(0,9),/EF,@P1 G U04:A1'="",U03

	;Amend
A01	W /C(0,16),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G N01:%J=1,A01
	G E09

	;Select
S01	W @P1,"  Select item ",@P2 D ^GETX S WX=+X I $G(^WORK($j,WX))="" G L01
	S WK=^WORK($j,WX),CX=$P(WK,%,1),MX=$P(WK,%,2),A3=$P(WK,%,3),A4=$P(WK,%,4),A5=$P(WK,%,5),RX=$p(WK,%,6)
	W /C(0,16),/EF F %J=1:1 D D^INPUT Q:X="END"!(X[%)
	G E09

	;Validation ----------------------------------------
	;Matter code
V01	Q:$D(ERR)  S SM=^SM(CX,X),M7=$P(SM,%,7),M7A=$p(M7,"/",1),M7B=$p(M7,"/",2)
	Q
	;Activity
V02	Q:$D(ERR)  I '$D(^SR(RX,X)) S ERR="Not defined for this charge group" Q
	S SR=^SR(RX,X),S1=$P(SR,%,1),S2=$P(SR,%,2),SA=^SA(X)
	Q
	;No of units
V03	S A5=""
	Q
	;No of items
V04	S A4=""
	Q

CASE	S X=$zconvert(X,"U") Q

XSEB
XSEB	;GRI,,,;02:24 PM  3 Nov 1994
	;=======================================================
	; Virgo Accounts - Write Off Time
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:       1023
	; Files updated: ^SW ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	S TT="TWO",D5="Time write-off"
	D X0^XSXS
	S %S=1023 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSEB Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSEB:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S SW=$G(^SW(CX,MX,1,"Z!")),T1=$P(SW,%,1),T2=$P(SW,%,2),T3=$P(SW,%,3)-D1,^SW(CX,MX,1,"Z!")=T1_%_T2_%_T3_%
	S ^SW(CX,MX)=(W1-D1)_%_W2_%
	D YX^XSXS
	D UPAB^XSXS("N020",-D1,DAT,TT,"",D5)
	D UPAB^XSXS("N050",D1,DAT,TT,"",D5)
	G XSEB

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSEB:%J=1,A01
	G E02

	;Validation - Amount
V01	Q:$D(ERR)  S SW=$G(^SW(CX,MX)),W1=$P(SW,%,1),W2=$P(SW,%,2)
	I X>W1 S ERR="Cannot have negative W-I-P"
	Q

CASE	S X=$zconvert(X,"U") Q

XSEC
XSEC	;GRI,,,;01:06 PM  3 May 2000
	;=======================================================
	; Virgo Accounts - WIP Enquiry
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:       1024
	; Files updated: none
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, ^OUTPUT, 
	;                HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	S %S=1024 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSEC Q
	S DET=0 I $d(^TE(CX,MX)) S DET=1

	;Summary display
E01	S (X3,X4)="",MOD=0 W /c(0,7),/ef,@P1,!?5,"Activity",?43,"Time",?52,"Items",?65,"Value",!
E02	S X3=$O(^SW(CX,MX,X3)) I X3=""!($Y>19) G E09
	W !?5,"Fee Earner ",X3,?19,$p($g(^SF(X3)),%,1),!!
E03	S X4=$O(^SW(CX,MX,X3,X4)) I X4="" G E02
E04	D LINE I $Y>19 G E09
	G E03

E09	W /C(0,22),@P1,"Next, " W:DET&'MOD "Detail, " W:MOD "Summary, " W "Print or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) G XSEC
	I X="P" G P01
	I DET,'MOD,X="D" G D01
	I MOD,X="S" G E01
	I X="N" G N01
	I X="\\" G H01
	G E09

	;Detail display
D01	S (X3,X4)="",MOD=1 W /c(0,7),/ef,@P1,!?2,"Date",?14,"Description",?56,"Time",?63,"Items",?72,"Value",!
D02	S X3=$O(^TE(CX,MX,X3)) I X3=""!($Y>19) G E09
	W !?2,"Fee Earner ",X3,?16,$p($g(^SF(X3)),%,1),!!
D03	S X4=$O(^TE(CX,MX,X3,X4)) I X4="" G D02
D04	D DLINE I $Y>19 G E09
	G D03

	;Next
N01	W /c(0,9),/ef,@P1
	I MOD G D02:X3="" W ! G D03
	G E02:X3="" W ! G E03

	;Home
H01	W /c(0,9),/ef,@P1
	I MOD G D01
	G E01

	;Print
P01	D ^OUTPUT G:%OP[% XSEC S N=99,P=1 D HEAD^XSXS
	D PRINT
	D END^XSXS
	G XSEC

	;Validation - Matter code
V01	Q:$D(ERR)  I '$d(^SW(CX,X)),'$d(^TE(CX,X)) S ERR="No W.I.P details for this matter" Q
	S SW=$g(^SW(CX,X)),M1=$J($P(SW,%,1),0,2),M2=$J($P(SW,%,2),0,2) Q

CASE	S X=$zconvert(X,"U") Q

	;Print details
	;may be called by other programs (?unlikely)
	;needs CX, MX, M1 and MOD defined
PRINT	W "Client ",CX,?12,$P(^SC(CX),%,1),!
	W "Matter ",MX,?12,$P(^SM(CX,MX),%,1),!
	W "WIP Balance",?12,M1 S N=N+2,(X3,X4)=""
	I MOD G P12
	;summary
P02	S X3=$O(^SW(CX,MX,X3)) I X3="" Q
	W !!?5,"Fee Earner ",X3,?19,$p($g(^SF(X3)),%,1),!!?5,"Activity",?43,"Time",?52,"Items",?65,"Value",!! S N=N+6
P03	S X4=$O(^SW(CX,MX,X3,X4)) I X4="" G P02
	D LINE S N=N+1 I N>56 D HEAD^XSXS
	G P03
	;detail
P12	S X3=$O(^TE(CX,MX,X3)) I X3="" Q
	W !!?5,"Fee Earner ",X3,?19,$p($g(^SF(X3)),%,1),!!?2,"Date",?14,"Description",?56,"Time",?63,"Items",?72,"Value",!! S N=N+6
P13	S X4=$O(^TE(CX,MX,X3,X4)) I X4="" G P12
	D DLINE S N=N+1 I N>56 D HEAD^XSXS
	G P13

TCON	S A=T0\60_":",T0=T0#60 S:T0<10 T0=0_T0 S T0=A_T0 Q

LINE	S SW=^SW(CX,MX,X3,X4),D1=$P(SW,%,1),D2=$P(SW,%,2),D3=$P(SW,%,3),T0=D1 D TCON
	W ?5,X4,?8,$P($G(^SA(X4)),%,1) W:D1 ?47-$L(T0),T0 W:D2 ?52,$J(D2,5,0) W ?60,$J(D3,10,2),! Q

DLINE	S TE=^TE(CX,MX,X3,X4),E1=$p(TE,%,1),E2=$p(TE,%,2),D1=$P(TE,%,3),D2=$P(TE,%,4),D3=$P(TE,%,5),T0=D1 D TCON
	W ?2,E1,?14,E2," ",$g(^SA(E2)) W:D1 ?60-$L(T0),T0 W:D2 ?62,$J(D2,5,0) W ?67,$J(D3,10,2),! Q

XSED
XSED	;GRI,,,;03:40 PM  12 Jan 2001;
	;=======================================================
	; Virgo Accounts - WIP Analysis
	;
	; Copyright Graham R Irwin 1993-2001
	;
	; Screens:       1041, 1070
	; Files updated: ^WORK($j)
	; Subroutines:	 X0^XSXS, ^INPUT, ^OUTPUT, HEAD^XSXS,
	;                END^XSXS, ^NEWNAME
	;
	;=======================================================

	D X0^XSXS

	W !?8,@P1,"Do you wish to analyse WIP by:-"
	W !!?8,"A - ",$$^NEWNAME("@F7@")
	W !?8,"B - ",$$^NEWNAME("@F8@")
	W !?8,"C - Client",!!
	S %S=1041 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSED Q
	S %S=1070 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSED
	I A4="" S A4="YN"

	W /C(0,22),@P1,"Sorting, please wait ..."
	K ^WORK($j) S (CX,MX)="",NC=0
G01	S CX=$o(^SM(CX)) I CX="" G H01
G02	S MX=$o(^SM(CX,MX)) I MX="" G G01
	S SM=^SM(CX,MX),M8=$p(SM,%,8),M9=$p(SM,%,9),M3=$p(SM,%,3)
	I A4'[M3 G G02
	S:A3="A" X1=M8 S:A3="B" X1=M9 S:A3="C" X1=CX S:X1="" X1="null"
	S SWB=$g(^SW(CX,MX)),SWB1=$p(SWB,%,1)
	S ^WORK($j,X1)=$g(^WORK($j,X1))+SWB1
	G G02

	;Print
H01	D ^OUTPUT G:%OP[% XSED
	S T1=0,N=99,P=1 D HEAD^XSXS
	W "Unbilled Work-in-Progress - ",$$^NEWNAME("@F1@")," = " W:$l(A4)=1 A4 W:$l(A4)=2 "Y or N" W !!! S N=N+3,X1=""
H02	S X1=$o(^WORK($j,X1)) I X1="" G J01
	S WK=^WORK($j,X1)
	I A3="C",+WK=0 G H02	;ignore if total by client=0
	W ?8 W:A3="A" $$^NEWNAME("@F7@")," = " W:A3="B" $$^NEWNAME("@F8@")," = " W:A3="C" "Client " W X1 W:A3="C" "  ",$p(^SC(X1),%,1) W ?50,$j(WK,10,2),!!
	S T1=T1+WK,N=N+2 I N>56 D HEAD^XSXS
	G H02

J01	W ?50,"----------",!?8,"Total",?50,$j(T1,10,2),!
	D END^XSXS
	Q

	;Validation - option
V03	I "ABC"'[X S ERR="Must be 'A', 'B' or 'C'"
	Q

XSEE
XSEE	;GRI,,,;02:25 PM  3 Nov 1994;
	;=======================================================
	; Virgo Accounts - Remove Historic Time
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:       1040
	; Files updated: ^TE
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	D X0^XSXS
	S %S=1040 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSEE Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSEE:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	

	G XSEE

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSEE:%J=1,A01
	G E02

	;Validation - Client code
V01	I X="*" K ERR
	Q:$d(ERR)
	Q

CASE	S X=$zconvert(X,"U") Q

XSEF
XSEF	;GRI,,,;10:03 AM  6 Dec 2007;
	;=======================================================
	; Virgo Accounts - Remove Detailed Time
	;
	; Copyright Graham R Irwin 1993-1995
	;
	; Screens:       1058

	; Files updated: ^TE
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, DMY2H^DATE
	;
	;=======================================================

	D X0^XSXS
	S %S=1058 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSEF Q
	S %DAT=A1 D DMY2H^DATE S A2=%H

E09	W /C(0,22),@P1,"Update or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) G XSEF
	I X="U" G D01
	G E09

	;Remove transactions
D01	S (X3,X4)=""
D02	S X3=$O(^TE(CX,MX,X3)) I X3="" G XSEF
D03	S X4=$O(^TE(CX,MX,X3,X4)) I X4="" G D02
	S TE=^TE(CX,MX,X3,X4),%DAT=$p(TE,%,1) D DMY2H^DATE S D2=%H
	I A2'<D2 K ^TE(CX,MX,X3,X4)
	G D03

	;Validation - Matter code
V01	Q:$D(ERR)  I '$d(^TE(CX,X)) S ERR="No details for this matter"
	Q

CASE	S X=$zconvert(X,"U") Q

XSEG
XSEG	;GRI,,,;04:54 PM  5 Jun 2002;
	;=======================================================
	; Virgo Accounts - Time Recorded by F/E
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1018, 1041
	; Files updated: ^WORK ^WORK2
	; Subroutines:   X0^XSXS, ^INPUT, DMY2H^DATE, ^OUTPUT,
	;                HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	K ^WORK($j),^WORK2($j)
	S %S=1018 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSEG Q
	S %DAT=A1 D DMY2H^DATE S D1=%H
	S %DAT=A2 D DMY2H^DATE S D2=%H

	W !?8,@P1,"Do you wish to sort by:-"
	W !!?8,"A - ",$$^NEWNAME("@F7@")
	W !?8,"B - ",$$^NEWNAME("@F8@")
	W !?8,"C - Client",!!
	S %S=1041 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSEG Q

	;Create work files
	W /C(0,22),@P1,"Sorting, please wait ..."
	S (CX,MX,X3,X4)=""
D02	S CX=$o(^TE(CX)) I CX="" G E01
D03	S MX=$o(^TE(CX,MX)) I MX="" G D02
D04	S X3=$o(^TE(CX,MX,X3)) I X3="" G D03
D05	S X4=$o(^TE(CX,MX,X3,X4)) I X4="" G D04
	S TEA=^TE(CX,MX,X3,X4),%DAT=$p(TEA,%,1) D DMY2H^DATE S D3=%H
	I D1>D3 G D05
	I D2<D3 G D05
	S WK=$g(^WORK($j,X3,CX,MX))
	S SM=^SM(CX,MX),M8=$p(SM,%,8),M9=$p(SM,%,9)
	S:A3="A" X1=M8 S:A3="B" X1=M9 S:A3="C" X1=CX S:X1="" X1="null"
	S TEA3=$p(TEA,%,3),TEA4=$p(TEA,%,4),TEA5=$p(TEA,%,5)
	S WK1=$p(WK,%,1)+TEA3,WK2=$p(WK,%,2)+TEA4,WK3=$p(WK,%,3)+TEA5
	S ^WORK($j,X3,CX,MX)=WK1_%_WK2_%_WK3
	S WKA=$g(^WORK2($j,X1))
	S WK1=$p(WKA,%,1)+TEA3,WK2=$p(WKA,%,2)+TEA4,WK3=$p(WKA,%,3)+TEA5
	S ^WORK2($j,X1)=WK1_%_WK2_%_WK3
	G D05

	;Print report
E01	D ^OUTPUT G:%OP[% XSEG S N=99,P=1 D HEAD^XSXS
	W ?25,"From ",A1," to ",A2,!! S N=N+2
	S (X3,CX,MX)=""
E03	S X3=$o(^WORK($j,X3)) I X3="" G F01
	W !?6,"Fee Earner ",X3," ",$p($g(^SF(X3)),%,1),!! S N=N+3,(T1,T2,T3)=0
E04	S CX=$o(^WORK($j,X3,CX)) I CX="" G E10
E05	S MX=$o(^WORK($j,X3,CX,MX)) I MX="" G E04
	S WK=^WORK($j,X3,CX,MX),WK1=$p(WK,%,1),WK2=$p(WK,%,2),WK3=$p(WK,%,3)
	S T1=T1+WK1,T2=T2+WK2,T3=T3+WK3,T0=WK1 D TCON
	W ?6,"Client/matter ",CX,"/",MX,?33,"Hours ",T0,?47,"Items ",WK2,?60,"Value ",$j(WK3,0,2),!! S N=N+2 I N>58 D HEAD^XSXS
	G E05
E10	S T0=T1 D TCON
	W ?6,"Totals F/e ",X3,?33,"Hours ",T0,?47,"Items ",T2,?60,"Value ",$j(T3,0,2),!! S N=N+2 I N>58 D HEAD^XSXS
	G E03

	;Print summary
F01	D HEAD^XSXS
	W ?29,"Summary by " W:A3="A" $$^NEWNAME("@F7@") W:A3="B" $$^NEWNAME("@F8@") W:A3="C" "Client" W !!! S N=N+3
	S X1=""
F02	S X1=$o(^WORK2($j,X1)) I X1="" G G01
	S WKA=^WORK2($j,X1),WK1=$p(WKA,%,1),WK2=$p(WKA,%,2),WK3=$p(WKA,%,3)
	S T0=WK1 D TCON
	W ?6 W:A3="A" $$^NEWNAME("@F7@")," = " W:A3="B" $$^NEWNAME("@F8@")," = " W:A3="C" "Client " W X1,?33,"Hours ",T0,?47,"Items ",WK2,?60,"Value ",$j(WK3,0,2),!! S N=N+2 I N>58 D HEAD^XSXS
	G F02

G01	D END^XSXS
	Q

	;convert time subroutine
TCON	S A=T0\60_":",T0=T0#60 S:T0<10 T0=0_T0 S T0=A_T0
	Q

XSEH
XSEH	;GRI,,,;01:26 PM  6 Dec 2007;
	;=======================================================
	; Virgo Accounts - Summarise Detailed Time
	;
	; Copyright Graham R Irwin 1993-2007
	;
	; Screens:       1024
	; Files updated: ^WORK($j)
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, ^OUTPUT, 
	;                HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	S %S=1024 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSEH Q
	k ^WORK($j)

	;Get detailed transactions
D01	S (X3,X4)=""
D02	S X3=$O(^TE(CX,MX,X3)) I X3="" G P01
D03	S X4=$O(^TE(CX,MX,X3,X4)) I X4="" G D02
	S TE=^TE(CX,MX,X3,X4),E1=$p(TE,%,1),E2=$p(TE,%,2),E3=$P(TE,%,3),E4=$P(TE,%,4),E5=$P(TE,%,5)
	S WK=$g(^WORK($j,X3,E2)),W1=$p(WK,%,1),W2=$p(WK,%,2),W3=$p(WK,%,3)
	S ^WORK($j,X3,E2)=(W1+E3)_%_(W2+E4)_%_(W3+E5)
	G D03

	;Print
P01	D ^OUTPUT G:%OP[% XSEH S N=99,P=1 D HEAD^XSXS
	W "Client ",CX,?12,$P(^SC(CX),%,1),!
	W "Matter ",MX,?12,$P(^SM(CX,MX),%,1),!
	W "WIP Balance",?12,M1 S N=N+2,(X3,X4)=""

P02	S X3=$O(^WORK($j,X3)) I X3="" G P09
	W !!?5,"Fee Earner ",X3,?19,$p($g(^SF(X3)),%,1),!!?5,"Activity",?43,"Time",?52,"Items",?65,"Value",!! S N=N+6
P03	S X4=$O(^WORK($j,X3,X4)) I X4="" G P02
	S SW=^WORK($j,X3,X4),D1=$P(SW,%,1),D2=$P(SW,%,2),D3=$P(SW,%,3),T0=D1
	S A=T0\60_":",T0=T0#60 S:T0<10 T0=0_T0 S T0=A_T0
	W ?5,X4,?8,$P($G(^SA(X4)),%,1) W:D1 ?47-$L(T0),T0 W:D2 ?52,$J(D2,5,0) W ?60,$J(D3,10,2),!
	S N=N+1 I N>56 D HEAD^XSXS
	G P03

P09	W !!,"Please note that if any time has been billed or any detailed time entries",!,"removed the total shown will not equal the WIP balance."
	D END^XSXS
	Q

	;Validation - Matter code
V01	Q:$D(ERR)  I '$d(^SW(CX,X)),'$d(^TE(CX,X)) S ERR="No W.I.P details for this matter" Q
	S SW=$g(^SW(CX,X)),M1=$J($P(SW,%,1),0,2),M2=$J($P(SW,%,2),0,2) Q

CASE	S X=$zconvert(X,"U") Q

XSFA
XSFA	;GRI,,,;01:28 PM  3 Nov 1994
	;=======================================================
	; Virgo Accounts - Post Client Receipt
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:	 1025
	; Files updated: ^CB ^CL ^SY ^AB ^AY
	; Subroutines:	 X0^XSXS, ^INPUT, ^GETX, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	D X0^XSXS
	S E5="Client Receipt",TT="CLR"

	i '$d(^CI),$g(^SZ("ADE"))'="N" w @P5,!?8,"Cannot post client money until Interest Rate Table is set up",!?8,"Please refer to your manual",!!?8,"Press any key to continue " R *X R:'X *X Q

	S %S=1025 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSFA Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSFA:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S E5=D5_" - "_$p(^SC(CX),%,1),E5=$e(E5,1,70)
	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_CX_%_MX_%_DX_%_D2_%_%_%_%_%_D4_%_E5
	S ^CL(CX,MX,YX)=D1_%_%_D2_%_TT_%_D4_%_D5_%
	S B1=B1+D2,^CL(CX,MX)=B1_%_B2
	S ^CB(DX,YX)=D1_%_%_D2_%_TT_%_D4_%_E5_%_"N"_%
	S CB=^CB(DX),B1=$P(CB,%,1,5),B6=$P(CB,%,6)+D2,^CB(DX)=B1_%_B6_%
	S AX="L010" S:M5="Y" AX="L020" D UPAB^XSXS(AX,D2,D1,TT,D4,E5)
	G XSFA

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSFA:%J=1,A01
	G E02

	;Validation - Matter code
V01	Q:$D(ERR)  S SM=^SM(CX,X),M5=$P(SM,%,5),DX=$P(SM,%,10)
	S ERR="No cashbook defined for this matter" Q:DX=""  Q:'$D(^CB(DX))  K ERR
	S CL=$G(^CL(CX,X)),B1=$J($P(CL,%,1),0,2),B2=$P(CL,%,2,9) Q
	;CL Balance
V02	I M5="Y" W @P3,?48,"Trust Matter"
	Q

CASE	S X=$zconvert(X,"U") Q

XSFB
XSFB	;GRI,,,;09:44 AM  2 Feb 2003
	;=======================================================
	; Virgo Accounts - Post Client Payment
	;
	; Copyright Graham R Irwin 1993-2003
	;
	; Screens:	 1026
	; Files updated: ^CB ^CL ^SY ^AB ^AY
	; Subroutines:	 X0^XSXS, ^INPUT, ^GETX, ^XSFX,
	;                YX^XSXS, UPAB^XSXS, CHEQUE
	;
	;=======================================================

	S E5="Client Payment",TT="CLP"
	D X0^XSXS
	S %S=1026 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSFB Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSFB:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S E5=D5_" - "_$p(^SC(CX),%,1),E5=$e(E5,1,70)
	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_CX_%_MX_%_DX_%_D2_%_%_%_%_%_D4_%_E5
	S ^CL(CX,MX,YX)=D1_%_D2_%_%_TT_%_D4_%_D5_%
	S B1=B1-D2,^CL(CX,MX)=B1_%_B2
	S ^CB(DX,YX)=D1_%_D2_%_%_TT_%_D4_%_E5_%_"N"_%
	S CB=^CB(DX),B1=$P(CB,%,1,5),B6=$P(CB,%,6)-D2,^CB(DX)=B1_%_B6_%
	S AX="L010" S:M5="Y" AX="L020" D UPAB^XSXS(AX,-D2,D1,TT,D4,E5)
	;Write cheque
	D ^CHEQUE(DX,CX,MX,D1,D2,D4,E5,TT)
	G XSFB

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSFB:%J=1,A01
	G E02

	;Validation - Matter code
V01	Q:$D(ERR)  S SM=^SM(CX,X),M5=$P(SM,%,5),DX=$P(SM,%,10)
	S ERR="No cashbook defined for this matter" Q:DX=""  Q:'$D(^CB(DX))  K ERR
	S CL=$G(^CL(CX,X)),B1=$J($P(CL,%,1),0,2),B2=$P(CL,%,2,9)
	Q
	;CL Balance
V09	I M5="Y" W @P3,?48,"Trust Matter"
	Q

CASE	S X=$zconvert(X,"U") Q

XSFC
XSFC	;GRI,,,;11:04 AM  12 Feb 2002
	;=======================================================
	; Virgo Accounts - Client to Office Transfer
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:	 1027, 1048
	; Files updated: ^CB ^CL ^ML ^SY ^AB ^AY ^SB ^SD ^SV
	; Subroutines:	 X0^XSXS, ^INPUT, ^GETX, ^XSFX,
	;                YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	S E5="Client-Office Transfer",NO="N",(BX,A2)="",ONE=$g(^SZ("DOC"),1)
	D X0^XSXS
	S %S=1027 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSFC Q
	I A1="N" G E02

	;If transfer is to pay a bill
	S %S=1048 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSFC

E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSFC:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S E5=D5_" - "_$p(^SC(CX),%,1),E5=$e(E5,1,70)
	S TT="COB" I A1="N" S TT="COD"
	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_CX_%_MX_%_DX1_%_D2_%_%_%_%_%_D4_%_E5

	S ^CL(CX,MX,YX)=D1_%_D2_%_%_TT_%_D4_%_D5_%
	S B1=B1-D2,^CL(CX,MX)=B1_%_B2

	S ^CB(DX1,YX)=D1_%_D2_%_%_TT_%_D4_%_E5_%_"N"_%
	S CB=^CB(DX1),B1=$P(CB,%,1,5),B6=$P(CB,%,6)-D2,^CB(DX1)=B1_%_B6_%

	S ^ML(CX,MX,YX)=D1_%_D2_%_%_TT_%_D4_%_%_D5_%
	S ML=$G(^ML(CX,MX)),M1=$P(ML,%,1),M2=$P(ML,%,2)
	S:A1="Y" M2=M2-D2 S:A1="N" M1=M1-D2 S ^ML(CX,MX)=M1_%_M2

	S ^CB(DX,YX)=D1_%_%_D2_%_TT_%_D4_%_E5_%_"N"_%
	S CB=^CB(DX),C1=$P(CB,%,1,5),C6=$P(CB,%,6)+D2,^CB(DX)=C1_%_C6_%

	S AX="L010" S:M5="Y" AX="L020" D UPAB^XSXS(AX,-D2,D1,TT,D4,E5)
	D UPAB^XSXS("A130",D2,D1,TT,D4,E5)
	I A1="N" D UPAB^XSXS("N070",D2,D1,TT,D4,E5)
	S AX="A110" S:A1="N" AX="A120" D UPAB^XSXS(AX,-D2,D1,TT,D4,E5)

	;If transfer is to pay a bill, update bill register
	;(it may be a reverse transfer)
	I A1="N" G XSFC
	S BB8=BB8-D2 S:BB8<0 BB8=0 S BB9="P" S:'BB8 BB9="F" S:BB8=(BB4+BB5+BB6) BB9="U"
	S ^SB(BX)=BB1_%_CX_%_MX_%_BB4_%_BB5_%_BB6_%_BB7_%_BB8_%_BB9_%_BB10_%
	I BB9="F" K ^SU(BX)
	I BB9="U"!(BB9="P") S ^SU(BX)=""

	;Update VAT records if cash basis
	I ^SZ("VCB")'="Y" G NV1
	S SV=$G(^SV),V1=$P(SV,%,1),V2=$p(SV,%,2),V3=$P(SV,%,3)+R2,V4=$P(SV,%,4)+R3,^SV=V1_%_V2_%_V3_%_V4,^SV(YX)=BX_%_D1_%_R2_%_R3
	D UPAB^XSXS("L040",R3,D1,TT,D4,E5)
	D UPAB^XSXS("L140",-R3,D1,TT,D4,E5)

NV1	G XSFC

	;Amend
A01	W /C(0,4),/EF S %S=1027 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSFC:%J=1,A01
	I A1="N" G E02
	S R3=$g(R3),%S=1048 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	G A01:X[%,E02

	;Validation - Matter number
V01	Q:$D(ERR)  S SM=^SM(CX,X),M5=$P(SM,%,5),DX1=$P(SM,%,10)
	S ERR="No cashbook defined for this matter" Q:DX1=""  Q:'$D(^CB(DX1))  K ERR
	S CL=$G(^CL(CX,X)),B1=$J($P(CL,%,1),0,2),B2=$P(CL,%,2,9)
	Q
	;CL Balance
V09	I M5="Y" W @P3,?48,"Trust Matter"
	Q
	;Bill number
V03	Q:$D(ERR)  S SB=^SB(X),BB1=$P(SB,%,1),BCX=$P(SB,%,2),BMX=$P(SB,%,3),BB4=$P(SB,%,4),BB5=$P(SB,%,5),BB6=$J($P(SB,%,6),0,2),BB7=$P(SB,%,7),BB8=$J($P(SB,%,8),0,2),BB9=$P(SB,%,9),BB10=$P(SB,%,10),E2=$J(BB4+BB5,0,2)
	I BCX'=CX!(BMX'=MX) S ERR="Wrong client matter" Q
	I BB9="F",-D2>(BB4+BB5+BB6) S ERR="Cannot exceed original bill amount" Q
	I D2>BB8 S ERR="Cannot exceed outstanding bill amount" Q
	I BB10="Y" W @P3,?48,"Includes Unpaid Prof Disb"
	Q
	;VAT Amount
V04	I D2<0,X>0 S ERR="Must be negative or zero" Q
	I X>BB6 S ERR="Cannot exceed original VAT amount" Q
	I D2>0,X>D2 S ERR="Cannot exceed amount of transfer" Q
	I D2<0,X<D2 S ERR="Cannot exceed amount of transfer" Q
	S R2=D2-X
	Q
	;To cashbook
V05	Q:$d(ERR)  I X>99 S ERR="Must be an office cashbook"
	Q
	;To pay bill?
V06	S ML=$g(^ML(CX,MX)),M1=$p(ML,%,1)
	I X="N",D2>M1 W @P3,?48,"Disbursement Balance in Credit",*7
	Q
CASE	S X=$zconvert(X,"U") Q

XSFD
XSFD	;GRI,,,;11:37 AM  17 Nov 1996
	;=======================================================
	; Virgo Accounts - Office to Client Transfer
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:       1028
	; Files updated: ^CB ^CL ^ML ^SY ^AB ^AY
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	S E5="Office-Client Transfer",TT="TOC",ONE=$g(^SZ("DOC"),1)
	D X0^XSXS
	S %S=1028 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSFD Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSFD:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S E5=D5_" - "_$p(^SC(CX),%,1),E5=$e(E5,1,70)
	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_CX_%_MX_%_DX1_%_D2_%_%_%_%_%_D4_%_E5
	;update client ledger
	S ^CL(CX,MX,YX)=D1_%_%_D2_%_TT_%_D4_%_D5_%
	S CLB=$g(^CL(CX,MX)),CLB1=$p(CLB,%,1)+D2,CLB2=$p(CLB,%,2,9),^CL(CX,MX)=CLB1_%_CLB2
	;update to cashbook
	S ^CB(DX1,YX)=D1_%_%_D2_%_TT_%_D4_%_E5_%_"N"_%
	S CB=^CB(DX1),B1=$P(CB,%,1,5),B6=$P(CB,%,6)+D2,^CB(DX1)=B1_%_B6_%
	;update matter ledger
	S ^ML(CX,MX,YX)=D1_%_%_D2_%_TT_%_D4_%_%_D5_%
	S ML=$g(^ML(CX,MX)),M1=$p(ML,%,1)+D2,M2=$p(ML,%,2),^ML(CX,MX)=M1_%_M2
	;update from cashbook
	S ^CB(DX,YX)=D1_%_D2_%_%_TT_%_D4_%_E5_%_"N"_%
	S CB=^CB(DX),B1=$P(CB,%,1,5),B6=$P(CB,%,6)-D2,^CB(DX)=B1_%_B6_%
	;update nominal ledger
	S AX="L010" S:M5="Y" AX="L020" D UPAB^XSXS(AX,D2,D1,TT,D4,E5)
	D UPAB^XSXS("A130",-D2,D1,TT,D4,E5)
	D UPAB^XSXS("A120",D2,D1,TT,D4,E5)
	G XSFD

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSFD:%J=1,A01
	G E02

	;Validation - Matter number
V01	Q:$D(ERR)  S SM=^SM(CX,X),M5=$P(SM,%,5),DX1=$P(SM,%,10)
	S ERR="No cashbook defined for this matter" Q:DX1=""  Q:'$D(^CB(DX1))  K ERR
	S MLB=$g(^ML(CX,X)),MLB1=$j($p(MLB,%,1),0,2),MLB2=$p(MLB,%,2,9) Q
	;CL Balance
V02	I M5="Y" W @P3,?48,"Trust Matter"
	Q
	;From Cashbook
V03	Q:$d(ERR)  I X>99 S ERR="Must be an office cashbook"
	Q

CASE	S X=$zconvert(X,"U") Q

XSFE
XSFE	;GRI,,,;01:47 PM  3 Nov 1994
	;=======================================================
	; Virgo Accounts - Client to Client Transfer
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:	 1029, 1051
	; Files updated: ^CB ^CL ^SY ^AB ^AY
	; Subroutines:	 X0^XSXS, ^INPUT, ^GETX, ^XSFX,
	;                YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	S E5="Client-Client Transfer",TT="TCC"
	D X0^XSXS
	W @P1,?8,"TRANSFER FROM ----------------------",!
	S %S=1029 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSFE Q
	W @P1,!?8,"TRANSFER TO ------------------------",!
	S %S=1051 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSFE
E01	I DX1'=DX2 W !!?8,"Remember to instruct the bank to make the transfer"
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSFE:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_CX1_%_MX1_%_DX1_%_D2_%_%_CX2_%_MX2_%_DX2_%_D4_%_D5
	S ^CL(CX1,MX1,YX)=D1_%_D2_%_%_TT_%_D4_%_D5_%
	S B11=B11-D2,^CL(CX1,MX1)=B11_%_B12
	S ^CL(CX2,MX2,YX)=D1_%_%_D2_%_TT_%_D4_%_D5_%
	S B21=B21+D2,^CL(CX2,MX2)=B21_%_B22
	I DX1=DX2 G XSFE
	S ^CB(DX1,YX)=D1_%_D2_%_%_TT_%_D4_%_D5_%_"N"_%
	S CB=^CB(DX1),B1=$P(CB,%,1,5),B6=$P(CB,%,6)-D2,^CB(DX1)=B1_%_B6_%
	S ^CB(DX2,YX)=D1_%_%_D2_%_TT_%_D4_%_D5_%_"N"_%
	S CB=^CB(DX2),B1=$P(CB,%,1,5),B6=$P(CB,%,6)+D2,^CB(DX2)=B1_%_B6_%
	I M15=M25 G XSFE
	S AX="L010" S:M15="Y" AX="L020" D UPAB^XSXS(AX,-D2,D1,TT,D4,D5)
	S AX="L010" S:M25="Y" AX="L020" D UPAB^XSXS(AX,D2,D1,TT,D4,D5)
	G XSFE

	;Amend
A01	W /C(0,5),/EF S %S=1029 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSFE:%J=1,A01
	W @P1,!?8,"TRANSFER TO ------------------------",!
	S %S=1051 F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G A01
	G E01

	;Validation - From matter code
V01	Q:$D(ERR)  S SM=^SM(CX1,X),M15=$P(SM,%,5),DX1=$P(SM,%,10)
	S ERR="No cashbook defined for this matter" Q:DX1=""  Q:'$D(^CB(DX1))  K ERR
	S CL=$G(^CL(CX1,X)),B11=$J($P(CL,%,1),0,2),B12=$P(CL,%,2,9) Q
	;CL Balance
V09	I M15="Y" W @P3,?48,"Trust Matter"
	Q
	;To matter code
V04	Q:$D(ERR)  S SM=^SM(CX2,X),M25=$P(SM,%,5),DX2=$P(SM,%,10)
	S ERR="No cashbook defined for this matter" Q:DX2=""  Q:'$D(^CB(DX2))  K ERR
	I CX1=CX2,MX1=X S ERR="Cannot be the same matter" Q
	S CL=$G(^CL(CX2,X)),B21=$J($P(CL,%,1),0,2),B22=$P(CL,%,2,9) Q
	;CL Balance
V05	I M25="Y" W @P3,?48,"Trust Matter"
	Q

CASE	S X=$zconvert(X,"U") Q

XSFF
XSFF	;GRI,,,;04:53 PM  5 Jun 2002
	;=======================================================
	; Virgo Accounts - Client A/c Reconciliation
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:	 1067
	; Files updated: ^WORK($j)
	; Subroutines:	 X0^XSXS, ^INPUT, ^OUTPUT, HEAD^XSXS,
	;                END^XSXS
	;
	;=======================================================

	D X0^XSXS S YES="Y"
	S %S=1067 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% Q

	D ^OUTPUT I %OP[% G XSFF
	S N=99,P=1 D HEAD^XSXS
	K ^WORK($j) S (CX,MX)=""
A01	S CX=$O(^CL(CX)) I CX="" G B01
A02	S MX=$O(^CL(CX,MX)) I MX="" G A01
	S C1=+^CL(CX,MX),DX=$P(^SM(CX,MX),%,10)
	S ^WORK($j,DX,CX,MX)=C1
	G A02

B01	S DX=99,T2=0
B02	S DX=$O(^CB(DX)) I DX="" G END
	S CB=^CB(DX),C1=$P(CB,%,1),C6=$P(CB,%,6)
	W "Cashbook ",DX,"  ",C1,?50,"Balance ",$J(C6,10,2),!! S N=N+2
	S (CX,MX)="",T1=0
B05	S CX=$O(^WORK($j,DX,CX)) I CX="" G B08
B06	S MX=$O(^WORK($j,DX,CX,MX)) I MX="" G B05
	S W1=^WORK($j,DX,CX,MX),T1=T1+W1
	I A1="N"!(+W1'=0) W ?2,CX,"/",MX,?12,$p(^SC(CX),%,1),?44,$j(W1,10,2),! S N=N+1 I N>58 D HEAD^XSXS
	G B06

B08	W ?44,"----------",!," Total for cashbook ",DX,?44,$j(T1,10,2)
	I C6=T1 W ?56,"Cashbook balances",!!!
	E  W ?56,"DOES NOT BALANCE",!!!
	S T2=T2+T1,N=N+4 I N>58 D HEAD^XSXS
	G B02

END	W ?44,"==========",!," TOTAL CLIENT MONEY HELD",?44,$j(T2,10,2),!
	D END^XSXS
	Q

XSFG
XSFG	;GRI,,,;08:25 AM  18 Jun 2004
	;=======================================================
	; Virgo Accounts - Client Account Enquiry
	;
	; Copyright Graham R Irwin 1993-2004
	;
	; Screens:       1032
	; Files updated: none
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, ^GETPGM(XSCA),
	;                ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	S %S=1032 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSFG Q
	S IX="" W @P1 D HEAD
E02	S IX=$O(^CL(CX,MX,IX)) I IX=""!($Y>19) G E09
E03	D LINE
	G E02

E09	W /C(0,22),@P1,"Next, Print, Export or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) G XSFG
	I X="P" G P01
	I X="N" G N01
	I X="E" G EXPORT
	I X="\\" G H01
	I X="Z" D ^GETPGM(3,1) G ^@PGM
	G E09

	;Next
N01	W /C(0,11),/EF,@P1 G E03:IX'="" S E4=0 G E02

	;Home
H01	W /C(0,11),/EF,@P1 S IX="",E4=0 G E02

	;Print
P01	D ^OUTPUT G:%OP[% XSFG S N=99,P=1 D HEAD^XSXS
	D PRINT
	D END^XSXS
	G XSFG

	;Print a client account
	;may be called by another program (needs CX, MX defined)
PRINT	W "Client ",CX,?12,$P(^SC(CX),%,1),!,"Matter ",MX,?12,$P(^SM(CX,MX),%,1),!,"Current balance ",B1,?34,"Cashbook ",KX,?47,CBA1,!
	D HEAD S N=N+6,IX=""
P02	S IX=$O(^CL(CX,MX,IX)) I IX="" Q
	D LINE S N=N+2 I N>56 D HEAD^XSXS
	G P02

HEAD	W !?5,"Date",?18,"Ref No   Type",?36,"Payment",?49,"Receipt",?62,"Balance",!! S E4=0
	Q

LINE	S CL=^CL(CX,MX,IX),D1=$P(CL,%,1),D2=$P(CL,%,2),D3=$P(CL,%,3),D4=$P(CL,%,4),D5=$P(CL,%,5),D6=$P(CL,%,6),E4=E4-D2+D3
	W ?5,D1,?18,D5,?27,D4 W:D2 ?33,$J(D2,10,2) W:D3 ?46,$J(D3,10,2) W ?59,$J(E4,10,2),!?6,D6,!
	Q

	;Export
EXPORT	W @P1,"  File name: ",/ef,@P2
	D ^GETX,CASE
	I X'?1.8an G E09
	S FN=X_".txt",X3=""
	S %OP=10 O %OP:(file=FN:mode="w") U %OP
	W "Date^Payment^Receipt^Type^Ref No^Narrative",!
EX4	S X3=$o(^CL(CX,MX,X3)) I X3="" C %OP U 0 G XSFG
	W ^CL(CX,MX,X3),!
	G EX4

	;Validation - Matter number
V01	Q:$D(ERR)  I '$D(^CL(CX,X)) S ERR="No Client Ledger details for this matter" Q
	S SM=^SM(CX,X),M5=$p(SM,%,5),KX=$p(SM,%,10),CBA1=$p(^CB(KX),%,1)
	S CL=$g(^CL(CX,X)),B1=$j($p(CL,%,1),0,2),B2=$j($p(CL,%,2),0,2)
	Q
	;CL Balance
V02	I M5="Y" W @P3,?48,"Trust Matter"
	Q
	;Nominal int
V03	W @P2,?48,CBA1
	Q

CASE	S X=$zconvert(X,"U") Q

XSFH
XSFH	;GRI,,,;11:38 AM  17 Nov 1996
	;=======================================================
	; Virgo Accounts - Post Client Interest
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:	 1033
	; Files updated: ^CB ^CL ^SY ^AB ^AY
	; Subroutines:	 X0^XSXS, ^INPUT, ^GETX, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	S D5="Client Interest",TT="CLI",ONE=$g(^SZ("DOC"),1)
	D X0^XSXS
	S %S=1033 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSFH Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSFH:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S E5=D5_" - "_$p(^SC(CX),%,1),E5=$e(E5,1,70)
	D YX^XSXS S ^SY(DAT,TT,YX)=D1_%_CX_%_MX_%_DX1_%_D2_%_%_%_%_1_%_%_E5
	S ^CL(CX,MX,YX)=D1_%_%_D2_%_TT_%_D4_%_D5_%
	S B1=B1+D2,^CL(CX,MX)=B1_%_0_%_DAT_%
	S ^CB(DX1,YX)=D1_%_%_D2_%_TT_%_D4_%_E5_%_"N"_%
	S CB=^CB(DX1),B1=$P(CB,%,1,5),B6=$P(CB,%,6)+D2,^CB(DX1)=B1_%_B6_%
	S ^CB(DX,YX)=D1_%_D2_%_%_TT_%_D4_%_E5_%_"N"_%
	S CB=^CB(DX),B1=$P(CB,%,1,5),B6=$P(CB,%,6)-D2,^CB(DX)=B1_%_B6_%
	S AX="L010" S:M5="Y" AX="L020" D UPAB^XSXS(AX,D2,D1,TT,D4,E5)
	D UPAB^XSXS("A130",-D2,D1,TT,D4,E5)
	D UPAB^XSXS("E340",D2,D1,TT,D4,E5)
	G XSFH

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSFH:%J=1,A01
	G E02

	;Validation - Matter code
V01	Q:$D(ERR)  S SM=^SM(CX,X),M5=$P(SM,%,5),DX1=$P(SM,%,10)
	S ERR="NO CASHBOOK DEFINED FOR THIS MATTER" Q:DX1=""  Q:'$D(^CB(DX1))  K ERR
	I '$D(^CL(CX,X)) S ERR="No client account defined for this matter" Q
	S CL=^CL(CX,X),B1=$J($P(CL,%,1),0,2),B2=$J($P(CL,%,2),0,2),B3=$P(CL,%,3) Q
	;CL Balance
V02	I M5="Y" W @P3,?48,"Trust Matter"
	Q
	;From cashbook
V03	I X>99 S ERR="Must be an office cashbook"
	Q

CASE	S X=$zconvert(X,"U") Q 

XSFI
XSFI	;GRI,,,;02:28 PM  3 Nov 1994
	;=======================================================
	; Virgo Accounts - Maintain Client Interest Bands
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:       1034
	; Files updated: ^CN
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	D X0^XSXS S %S=1034
	W @P1,!?18,"Min Amount",?35,"No of Days",!! S NX=""
E03	S NX=$O(^CN(NX)) I NX=""!($Y>20) G L01
E04	S A1=^CN(NX) W @P1,?20,NX,?40,A1,! G E03
L01	W /C(0,22),@P1,"Next, Select, Insert or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) Q
	I X="N" G N01
	I X="S"!(X="Z") G S01
	I X="I" G I01
	G L01

	;Insert
I01	W /C(0,18),/EF F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSFI

E09	W /C(0,22),@P1,"Update, Amend, Delete or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSFI:X="Y",E09
	I X="A" G A01
	I X="U" G U01
	I X="D" W @P1,"  Are you sure ? ",@P2 D ^GETX D CASE I X="Y" K ^CN(NX) S NX="" G N01
	G E09

	;Update
U01	S ^CN(NX)=A1 W /C(0,7),/EF,@P1 S NX="" G E03

	;Next
N01	W /C(0,7),/EF,@P1 G E04:NX'="",E03

	;Amend
A01	W /C(0,18),/EF S %J=1 D D^INPUT
	F %J=2:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSFI:%J=2,A01
	G E09

	;Select
S01	W @P1,"  Amount ",@P2 D ^GETX S NX=+X I $G(^CN(NX))="" G L01
	S A1=^CN(NX)
	W /C(0,18),/EF F %J=1:1 D D^INPUT Q:X="END"!(X[%)
	G E09

CASE	S X=$zconvert(X,"U") Q

XSFJ
XSFJ	;GRI,,,;02:29 PM  3 Nov 1994
	;=======================================================
	; Virgo Accounts - Maintain Client Interest Rates
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:       1035
	; Files updated: ^CI
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	D X0^XSXS S %S=1035
	W @P1,!?18,"Min Amount",?36,"Int Rate",!! S IX=""
E03	S IX=$O(^CI(IX)) I IX=""!($Y>20) G L01
E04	S A1=^CI(IX) W @P1,?20,IX,?40,A1,! G E03
L01	W /C(0,22),@P1,"Next, Select, Insert or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) Q
	I X="N" G N01
	I X="S"!(X="Z") G S01
	I X="I" G I01
	G L01

	;Insert
I01	W /C(0,18),/EF F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSFJ

E09	W /C(0,22),@P1,"Update, Amend, Delete or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSFJ:X="Y",E09
	I X="A" G A01
	I X="U" G U01
	I X="D" W @P1,"  Are you sure ? ",@P2 D ^GETX D CASE I X="Y" K ^CI(IX) S IX="" G N01
	G E09

	;Update
U01	S ^CI(IX)=A1 W /C(0,7),/EF,@P1 S IX="" G E03

	;Next
N01	W /C(0,7),/EF,@P1 G E04:IX'="",E03

	;Amend
A01	W /C(0,18),/EF S %J=1 D D^INPUT
	F %J=2:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSFJ:%J=2,A01
	G E09

	;Select
S01	W @P1,"  Amount ",@P2 D ^GETX S IX=+X I $G(^CI(IX))="" G L01
	S A1=^CI(IX)
	W /C(0,18),/EF F %J=1:1 D D^INPUT Q:X="END"!(X[%)
	G E09

CASE	S X=$zconvert(X,"U") Q

XSFK
XSFK	;GRI,,,;04:44 PM  22 Mar 2002
	;=======================================================
	; Virgo Accounts - List All Client Ledger Transactions
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1063
	; Files updated: none
	; Subroutines:   X0^XSXS, ^INPUT, ^OUTPUT, HEAD^XSXS,
	;                END^XSXS, DMY2H^DATE
	;
	;=======================================================

	D X0^XSXS S NO="N"
	S %S=1063 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSFK Q
	W !?8,"WARNING: This could be a long report. You are advised to print it",!?8,"to the Spooler or to a File - if unsure please refer to the manual",!?8,"for details.",!

	D ^OUTPUT I %OP[% G XSFK
	S N=99,P=1 D HEAD^XSXS
	S (CX,MX)=""
	W ?5,"Transactions from ",A2 W:A2="" "Start" W " to ",A3 W:A4="Y" " - interest only" W !! S N=N+2

A01	S CX=$o(^CL(CX)) I CX="" G END
A02	S MX=$o(^CL(CX,MX)) I MX="" G A01
	S SMA=^SM(CX,MX),SMA1=$p(SMA,%,1),KX=$p(SMA,%,10),CBA1=$p(^CB(KX),%,1),CLB=$g(^CL(CX,MX)),CLB1=$j($p(CLB,%,1),0,2)
	U 0 W /c(0,23),@P1,"Processing ",CX," / ",MX,/el U %OP
	D PRINT I NT=0 W ?5,"(no relevant transactions)",! S N=N+1
	W ! S N=N+1
	G A02

END	D END^XSXS
	Q

	;Print all details for a client
PRINT	W "Client ",CX,?12,$P(^SC(CX),%,1),?72,"-----",!
	W "Matter ",MX,?12,SMA1,!
	W "Current balance ",CLB1,?34,"Cashbook ",KX,?47,CBA1,!!
	W ?5,"Date",?18,"Ref No   Type",?36,"Receipt",?49,"Payment",?62,"Balance",!!
	S N=N+6,IX="",(E4,NT)=0
P02	S IX=$O(^CL(CX,MX,IX)) I IX="" Q
	D LINE I N>56 D HEAD^XSXS
	G P02

	;print a transaction
LINE	S CL=^CL(CX,MX,IX),D1=$P(CL,%,1),D2=$P(CL,%,2),D3=$P(CL,%,3),D4=$P(CL,%,4),D5=$P(CL,%,5),D6=$P(CL,%,6),E4=E4-D2+D3
	I A4="Y",D4'="CLI" Q
	S %DAT=D1 D DMY2H^DATE S E1=%H
	I E1'<B1,E1'>B2 W ?5,D1,?18,D5,?27,D4 W:D3 ?33,$J(D3,10,2) W:D2 ?46,$J(D2,10,2) W ?59,$J(E4,10,2),!?6,D6,! S N=N+2,NT=NT+1
	Q

	;Validation - Confirm
V01	I X'="ALL" S ERR="Wrong confirmation"
	Q
	;From date
V02	I X="" K ERR
	I $d(ERR) Q
	S %DAT=X D DMY2H^DATE S B1=%H S:X="" B1=0
	Q
	;To date
V03	I $d(ERR) Q
	S %DAT=X D DMY2H^DATE S B2=%H
	I B2<B1 S ERR="Cannot be earlier than From Date"
	Q

XSFL
XSFL	;GRI,,,;08:39 AM  18 Jun 2004;
	;=======================================================
	; Virgo Accounts - Post CL Correction (one-sided CL entry)
	;
	; Copyright Graham R Irwin 1993-2004
	;
	; Screens:       1083
	; Files updated: ^CL ^SY
	; Subroutines:   X0^XSXS, ^INPASSW, ^INPUT, ^GETX, YX^XSXS
	;
	;=======================================================

	S H2="Client Ledger Correction",A4="Adjustment/correction",TT="CLR"
	D X0^XSXS W @P1
	D ^INPASSW I x'="WOZZAP" Q
	S %S=1083 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSFL Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSFL:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	D YX^XSXS S ^SY(DAT,TT,YX)=DAT_%_CX_%_MX_%_%_A2_%_%_%_%_%_%_A5
	S ^CL(CX,MX,YX)=DAT_%_%_A2_%_TT_%_%_A5
	Q

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSFL:%J=1,A01
	G E02

	;Validation - Matter
V02	Q:$D(ERR)  I '$D(^CL(CX,X)) S ERR="No ledger details for this matter" Q
	S CL=^CL(CX,X),E1=$j($p(CL,%,1),0,2)
	Q

CASE	S X=$zconvert(X,"U") Q

XSFM
XSFM	;GRI,,,;07:52 AM  25 Jul 2007;
	;=======================================================
	; Virgo Accounts - Cashbook Transfers
	;
	; Copyright Graham R Irwin 1993-2007
	;
	; Screens:       1086
	; Files updated:
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, ^OUTPUT, 
	;                HEAD^XSXS, END^XSXS, DMY2H^DATE
	;
	;=======================================================

	D X0^XSXS
	S %S=1086 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSFM Q
	W @P1 D HEAD

	;Display cashbook transactions
E01	S IX="" W /c(0,10),/ef,@P1
E02	S IX=$O(^CB(DX,IX)),TOT=0 I $Y>19 G E09
	I IX="" G E08
E03	D LINE
	G E02

E08	D TOTAL S (E2,E3)=0,TOT=1

E09	W /C(0,22),@P1 W "Next, Print or Quit ? ",/EL,@P2 D ^GETX S X=$zconvert(X,"U")
	I X="Q"!(X=%) G XSFM
	I X="P" G P01
	I X="N" G N01
	I X="\\" G M01
	G E09

	;Next
N01	W /C(0,10),/EF,@P1
	I TOT G M01
	G E03:IX'="" G E08

	;Home
M01	W /C(0,10),/EF,@P1
	S IX="" G E02

	;Print
P01	D ^OUTPUT G:%OP[% XSFM S N=99,P=1,IX="" D HEAD^XSXS
P1A	W "Cashbook ",DX,?14,$p(^CB(DX),%,1),! D HEAD S N=N+4
P02	S IX=$O(^CB(DX,IX)) I IX="" G P04
	D LINE I N>56 D HEAD^XSXS
	G P02
P04	D TOTAL W !! S N=N+3
	I ALL S DX=$o(^CB(DX)) I DX'="" G P1A
	D END^XSXS
	G XSFM

	;Validation - Cashbook
V01	I X<99 S ERR="Must be a client cashbook"
	Q

	;From date
V02	S %DAT=D1 D DMY2H^DATE S DH1=%H
	S %DAT=X D DMY2H^DATE S DH2=%H
	I DH2<DH1 S ERR="Must not be earlier than to date"
	Q

	;Show heading (display or print)
HEAD	W !?5,"Date",?18,"Ref No   Type",?36,"Receipt",?49,"Payment",!! S (E2,E3)=0 Q

LINE	S CB=^CB(DX,IX) I IX=0 Q
	S C1=$P(CB,%,1) S %DAT=C1 D DMY2H^DATE I %H<DH1 Q
	I %H>DH2 Q
	S C4=$P(CB,%,4) I "COD,COB,TOC,TCO"'[C4 Q
	S C2=$P(CB,%,2),C3=$P(CB,%,3),C5=$P(CB,%,5),C6=$P(CB,%,6),E2=E2+C2,E3=E3+C3
	W ?5,C1,?18,C5,?27,C4 W:C3 ?33,$J(C3,10,2) W:C2 ?46,$J(C2,10,2) W !?6,C6,! I $d(N) S N=N+2
	Q

TOTAL	W ?34,"---------",?47,"---------",!?5,"Total",?33,$j(E3,10,2),?46,$j(E2,10,2) Q

XSFX
XSFX(S1,S2)	;GRI,,,;01:20 PM  11 Dec 2002;
	;=======================================================
	; Virgo Accounts - Subroutine to check amount of
	;                    withdrawal from client account
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Files updated: none
	; Subroutines:   none
	;
	;=======================================================

	; Parameters:-
	;  S1 - client code
	;  S2 - matter code
	;  X  - amount of withdrawal

	; if there's already an error, quit
	I $d(ERR) Q

	I $e(H0,1,2)="SS" G C00

	; Normal check
	I X'>+$g(^CL(S1,S2)) Q
	S ERR="Cannot overdraw client account"
	I $g(^SZ("ODC"))="Y" W /C(0,22),@P1,"Overdraw client account - Are you sure ? ",/el,@P2 R Y S Y=$zconvert(Y,"U") Q:Y'="Y"  K ERR
	Q

	; Scottish solicitors - check all matters
C00	S S3="",S9=0
C01	S S3=$o(^CL(S1,S3)) I S3="" G C02
	S S9=S9+^CL(S1,S3)
	G C01
C02	I X>S9 S ERR="Cannot overdraw client account"
	Q

XSGA
XSGA	;GRI,,,;07:07 PM  12 Apr 2002
	;=======================================================
	; Virgo Accounts - Maintain System Parameters
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1036
	; Files updated: ^SZ
	; Subroutines:   X0^XSXS, H2DMY^DATE, DMY2H^DATE, 
	;                ^INPUT, ^GETX
	;
	;=======================================================

	D X0^XSXS
	L ^SZ
	S A1=^SZ("PN")			; Practise name
	S A3=^SZ("SW")			; S/W licence
	S A4=^SZ("VCB")			; VAT cash basis Y/N
	S A5=^SZ("TU")			; Time units
	S A9=^SZ("VAT")			; VAT rate
	S A10=$g(^SZ("ADE"),"Y")	; Auto day-end Y/N
	S A11=$g(^SZ("DTE"),"N")	; Detailed time Y/N
	S A12=$g(^SZ("D30"),"Y")	; 30 day check Y/N
	S A13=$g(^SZ("DOC"),1)		; Default office CB
	S A14=^SY			; Last item number
	S A15=$g(^SZ("ODC"),"N")	; Overdraw client Y/N
	S A16=$g(^SZ("WOD"),"Y")	; Write-off disb Y/N
	S A17=$g(^SZ("DCG"))		; Default charge group
	S %H=^SZ("LDE") D H2DMY^DATE S A6=%DAT	; Last day-end
	S %H=^SZ("LPE") D H2DMY^DATE S A7=%DAT	; Last period-end
	S %H=^SZ("NYE") D H2DMY^DATE S A8=%DAT	; Next year-end

	S AMD=0
	S %S=1036 F %J=1:1 D D^INPUT Q:X="END"!(X[%)

	;Option prompt
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) Q:'AMD  W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE Q:X="Y"  G E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S ^SZ("PN")=A1
	S (^SZ("SW"),H0)=A3
	S ^SZ("VCB")=A4
	S ^SZ("TU")=A5
	S ^SZ("VAT")=A9
	S ^SZ("ADE")=A10
	S ^SZ("DTE")=A11
	S ^SZ("D30")=A12
	S ^SZ("DOC")=A13
	S ^SZ("ODC")=A15
	S ^SZ("WOD")=A16
	S ^SZ("DCG")=A17
	S %DAT=A8 D DMY2H^DATE S ^SZ("NYE")=%H
	Q

	;Amend
A01	S AMD=1 W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSGA:%J=1,A01
	G E02

	;Validation - VAT cash base
V01	Q:$D(ERR)  I $D(^SV),A4'=X S ERR="Cannot amend, VAT file exists"
	Q
	;Licence no (also used by XSINSTAL)
V02	I X'?2U3UN4N1UN S ERR="Invalid licence no"
	I $e(X,10)'=$$^gtest($e(X,1,9)) S ERR="Invalid licence no"
	Q
	;Cashbook
V03	Q:$D(ERR)  I X>99 S ERR="Must be an office cashbook"
	Q

CASE	S X=$zconvert(X,"U") Q

XSGB
XSGB	;GRI,,,;10:09 AM  16 Mar 1999
	;=======================================================
	; Virgo Accounts - Maintain Fee Earners
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:       1037
	; Files updated: ^SF ^SEARCH
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	D X0^XSXS
	L ^SF
	S %J=1,%S=1037 D ^INPUT Q:X[%  I $D(^SF(X)) G D01
	W /C(48,4),"* NEW RECORD *",!
	F %J=2:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSGB
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSGB:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S ^SF(FX)=F1_%
	S F1L=$zconvert(F1,"L"),^SEARCH("SF",F1L,FX)=""
	I $d(F1X) S F1XL=$zconvert(F1X,"L") I F1L'=F1XL K ^SEARCH("SF",F1XL,FX)

	G XSGB

	;Display
D01	S SF=^SF(X),F1=$P(SF,%,1),F1X=F1,DEL=1,AMD=0 S:X=1 DEL=0
D02	F %J=2:1 D D^INPUT Q:X="END"
D03	W /C(0,22),@P1,"Amend" W:DEL ", Delete" W " or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) G XSGB:'AMD W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSGB:X="Y",D03
	I X="A" G A01
	I X="D",DEL W @P1,"  Are you sure ? ",@P2 D ^GETX D CASE I X="Y" K ^SF(FX),^SEARCH("SF",$zconvert(F1,"L"),FX) G XSGB
	G D03

	;Amend
A01	S AMD=1 W /C(0,5),/EF F %J=2:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSGB:%J=2,A01
	G E02

CASE	S X=$zconvert(X,"U") Q

XSGC
XSGC	;GRI,,,;10:09 AM  16 Mar 1999
	;=======================================================
	; Virgo Accounts - Maintain Fee Rate Table
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:       1038, 1043
	; Files updated: ^SR ^SEARCH
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, AMOUNT^INPUT
	;
	;=======================================================

	D X0^XSXS
	L ^SR
	S %J=1,%S=1038 D ^INPUT Q:X[%
	I $D(^SR(X)) S R1=^SR(RX),%J=2 D D^INPUT G E02
	W /C(48,4),"* NEW RECORD *",!
	F %J=2:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSGC
E02	W @P1,!?8,"Activity",?44,"Hourly Rate",?60,"Rate per Item",!! S A1=""
E03	S A1=$O(^SR(RX,A1)) I A1=""!($Y>20) G L01
E04	S SR=^SR(RX,A1),A2=$P(SR,%,1),A3=$P(SR,%,2),B1=$P($G(^SA(A1)),%,1)
	W @P1,?8,A1,?13,B1 W:A2 ?46,$J(A2,6,2) W:A3 ?62,$J(A3,6,2) W ! G E03
L01	W /C(0,22),@P1,"Next, Select, Insert, Amend, Delete or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) G XSGC
	I X="N" G N01
	I X="A" G A51
	I X="S"!(X="Z") G S01
	I X="I" G I01
	I X="D" W @P1,"  Delete entire table ? ",@P2 D ^GETX,CASE I X="Y" K ^SR(RX),^SEARCH("SR",$zconvert(R1,"L"),RX) G XSGC
	G L01

	;Insert item
I01	D CUP S %S=1043 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G I01:%J>1 S A1="" D CUP G L01
E09	W /C(0,22),@P1,"Update, Amend, Delete or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="A" G A01
	I X="U" G U01
	I X="Q"!(X=%) D CUP G L01
	I X="D" W @P1,"  Are you sure ? ",@P2 D ^GETX,CASE I X="Y" K ^SR(RX,A1) S A1="" G N01
	G E09

	;Update item
U01	S ^SR(RX)=R1,^SEARCH("SR",$zconvert(R1,"L"),RX)=""
	S ^SR(RX,A1)=A2_%_A3_% W /C(0,9),/EF,@P1 S A1="" G E03

	;Next
N01	W /C(0,9),/EF,@P1 G E04:A1'="",E03

	;Amend item
A01	D CUP S %S=1043,%J=1 D D^INPUT
	F %J=2:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSGC:%J=2,A01
	G E09

	;Amend charge group
A51	S R1X=R1,%J=2,%S=1038 W /c(0,5) D A^INPUT G XSGC:X[%
	I R1X'=RX S ^SR(RX)=R1,^SEARCH("SR",R1,RX)="" K ^SEARCH("SR",R1X,RX)
	G E02

	;Select item
S01	W @P1,"  Activity ",@P2 D ^GETX,CASE G:X="" L01 I $G(^SR(RX,X))="" G L01
	S A1=X,SR=^SR(RX,A1),A2=$P(SR,%,1),A3=$P(SR,%,2),%S=1043
	S:A2=0 A2="" S:A3=0 A3=""
	D CUP F %J=1:1 D D^INPUT Q:X="END"!(X[%)
	G E09

	;Validation - Activity
V01	I $D(^SR(RX,X)) S ERR="Already used"
	Q
	;Rate per item
V02	I X="",A2="" S ERR="Either hourly rate or rate/item must be defined" Q
	I X'="" D AMOUNT^INPUT Q:$D(ERR)  I A2'="" S ERR="Cannot both be defined"
	Q

CASE	S X=$zconvert(X,"U") Q

CUP	W /C(0,18),/EF Q

XSGD
XSGD	;GRI,,,;05:19 PM  1 Oct 2004
	;=======================================================
	; Virgo Accounts - Maintain Account Codes
	;
	; Copyright Graham R Irwin 1993-2004
	;
	; Screens:       1039
	; Files updated: ^AB ^SEARCH ^NB
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	D X0^XSXS
	L ^AB
	S (A2,A3,A4)=0,A5="N"
	S %J=1,%S=1039 D ^INPUT Q:X[%  I $D(^AB(X)) G D01
	W /C(48,4),"* NEW RECORD *",!
	F %J=2:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSGD
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSGD:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S ^AB(AX)=A1_%_A2_%_A3_%_A4_%_A5_%
	S A1L=$zconvert(A1,"L"),^SEARCH("AB",A1L,AX)=""
	I $d(A1X) S A1XL=$zconvert(A1X,"L") I A1L'=A1XL K ^SEARCH("AB",A1XL,AX)
	I B1="" K ^NB(AX)
	I B1'="" S ^NB(AX)=B1
	G XSGD

	;Display
D01	S AB=^AB(X),A1=$P(AB,%,1),A2=$P(AB,%,2),A3=$J($P(AB,%,3),0,2),A4=$P(AB,%,4),A5=$P(AB,%,5),A1X=A1,DEL=0,AMD=0
	S B1=$g(^NB(X))
	;allow deletion if not a system account and both current balance and
	;year-to-date balances are zero and there are no transactions
	I A5'="Y",A2=0,+A3=0,'$d(^AY(AX)) S DEL=1
D02	F %J=2:1 D D^INPUT Q:X="END"
D03	W /C(0,22),@P1,"Amend" W:DEL ", Delete" W " or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) G XSGD:'AMD W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSGD:X="Y",D03
	I X="A" G A01
	I X="D",DEL W @P1,"  Are you sure ? ",@P2 D ^GETX,CASE I X="Y" K ^AB(AX),^SEARCH("AB",$zconvert(A1,"L"),AX) G XSGD
	G D03

	;Amend
A01	S AMD=1 W /C(0,5),/EF F %J=2:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSGD:%J=2,A01
	G E02

	;Validation - Account code
V01	D CASE I "EIALN"'[$E(X,1) S ERR="First character must be 'I', 'A', 'E' or 'L'" Q
	;I $E(X,4)'=0 S ERR="Last character must be 0"
	Q

	;Budget
V02	I X="" K ERR
	Q

CASE	S X=$zconvert(X,"U") Q

XSGE
XSGE	;GRI,,,;12:45 PM  12 May 1999
	;=======================================================
	; Virgo Accounts - Maintain Cashbooks
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:       1030
	; Files updated: ^CB ^CA ^SEARCH
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	D X0^XSXS
	L ^CB
	S (C4,NO)="N"
	S %J=1,%S=1030 D ^INPUT Q:X[%
	I $D(^CB(X)) G D01

	W /C(48,4),"* NEW RECORD *",!
	F %J=2:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSGE

	S (C5,C6)=0
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSGE:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S ^CB(DX)=C1_%_C2_%_C3_%_C4_%_C5_%_C6_%
	S C1L=$zconvert(C1,"L"),^SEARCH("CB",C1L,DX)=""
	I $d(C1X) S C1XL=$zconvert(C1X,"L") I C1L'=C1XL K ^SEARCH("CB",C1XL,DX)
	G XSGE

	;Display
D01	S CB=^CB(DX),C1=$P(CB,%,1),C2=$P(CB,%,2),C3=$P(CB,%,3),C4=$P(CB,%,4),C5=$P(CB,%,5),C6=$P(CB,%,6),C1X=C1,DEL=0,AMD=0,REM=0
	;allow deletion if balance is zero, there are no transactions other
	;than a balance forward trans. and it is not cashbook 1, 100 or 101
	I C6=0,$o(^CB(DX,0))="",DX'=1,DX'=100,DX'=101 S DEL=1
	I $d(^CA(DX)) S REM=1
D02	F %J=2:1 D D^INPUT Q:X="END"
D03	W /C(0,22),@P1,"Amend" W:DEL ", Delete" W:REM ", Remove" W " or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) G XSGE:'AMD W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSGE:X="Y",D03
	I X="A" G A01
	I X="D",DEL W @P1,"  Are you sure ? ",@P2 D ^GETX,CASE I X="Y" K ^CB(DX),^CA(DX),^SEARCH("CB",$zconvert(C1,"L"),DX) G XSGE
	I X="R",REM W @P1,"  Are you sure ? ",@P2 D ^GETX,CASE I X="Y" K ^CA(DX) G XSGE
	G D03

	;Amend
A01	S AMD=1 W /C(0,5),/EF F %J=2:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSGE:%J=2,A01
	G E02

	;Validation - Trust account
V01	D CASE I "YN"'[X S ERR="Must be 'Y' or 'N'" Q
	I DX<100,X'="N" S ERR="Must be 'N'" Q
	I %I,C4'=X S ERR="May not be amended"
	Q

CASE	S X=$zconvert(X,"U") Q

XSGF
XSGF	;GRI,,,;05:36 PM  18 Dec 2000;
	;=======================================================
	; Virgo Accounts - Maintain Standing Entries
	;
	; Copyright Graham R Irwin 1999-2000
	;
	; Screens:       1075
	; Files updated: ^SO ^SX
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX, DMY2H^DATE
	;
	;=======================================================

	D X0^XSXS
	L ^SO
	S VAT=^SZ("VAT")/100,ONE=$g(^SZ("DOC"),1),TT="STO",NEW=0,AMD=0
	S %J=1,%S=1075 D ^INPUT Q:X[%
	I $D(^SO(X)) G D01

	W /C(48,4),"* NEW RECORD *",! S NEW=1
	F %J=2:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSGF

E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSGF:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S %DAT=D1 D DMY2H^DATE
	I NEW S XH=%H
	I 'NEW,D1X'=D1 K ^SX(XH,D0) S XH=%H
	S ^SO(D0)=D1_%_D2_%_D3_%_D4_%_DX_%_O2_%_O3_%_O4_%_AX_%_O5_%_XH_%
	S ^SX(XH,D0)=""
	G XSGF

	;Display
D01	S SO=^SO(D0),(D1,D1X)=$P(SO,%,1),D2=$P(SO,%,2),D3=$P(SO,%,3),D4=$P(SO,%,4),DX=$P(SO,%,5),O2=$P(SO,%,6),O3=$P(SO,%,7),O4=$P(SO,%,8),AX=$P(SO,%,9),O5=$P(SO,%,10),XH=$p(SO,%,11),E2=$j(O2+O3,0,2),AMD=0
D02	F %J=2:1 D D^INPUT Q:X="END"
D03	W /C(0,22),@P1,"Amend, Delete or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) G XSGF:'AMD W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSGF:X="Y",D03
	I X="A" G A01
	I X="D" W @P1,"  Are you sure ? ",@P2 D ^GETX,CASE I X="Y" K ^SO(D0) D KSX G XSGF
	G D03

	;Amend
A01	S AMD=1 W /C(0,5),/EF F %J=2:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSGF:%J=2,A01
	G E02

	;Validation - End date
V01	I $d(ERR) Q
	S %DAT=X D DMY2H^DATE S XX=%H,%DAT=D1 D DMY2H^DATE
	I XX<%H S ERR="Cannot be earlier than start date" Q
	Q
	;Payment/receipt
V02	D CASE
	I "PR"'[X S ERR="Must be 'P' or 'R'"
	Q
	;Frequency
V03	D CASE
	I X'?1.3N1U S ERR="Wrong format" Q
	I "DM"'[$e(X,$l(X)) S ERR="Must be days or months"
	Q
	;Net amount
V04	S E3=$J(X*VAT,0,2)
	Q
	;VAT amount
V05	I X="*" K ERR
	S E2=$J(O2+X,0,2)
	Q

CASE	S X=$zconvert(X,"U") Q

	;Kill off index entry
KSX	S (X1,X2)=""
KSX1	S X1=$o(^SX(X1)) I X1="" Q
KSX2	S X2=$o(^SX(X1,X2)) I X2="" G KSX1
	I X2=D0 K ^SX(X1,X2)
	G KSX2

XSGI
XSGI	;GRI,,,;10:11 AM  16 Mar 1999
	;=======================================================
	; Virgo Accounts - Maintain Activity Codes
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:       1044
	; Files updated: ^SA
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	D X0^XSXS
	L ^SA
	S %J=1,%S=1044 D ^INPUT Q:X[%  I $D(^SA(X)) G D01
	W /C(48,4),"* NEW RECORD *",!
	F %J=2:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSGI
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSGI:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S ^SA(SX)=A1
	G XSGI

	;Display
D01	S SA=^SA(X),A1=$P(SA,%,1),A1X=A1,DEL=0,AMD=0,RX=""
D02	F %J=2:1 D D^INPUT Q:X="END"
	I SX["!" G D03
D2A	S RX=$o(^SR(RX)) I RX="" S DEL=1 G D03
	I $d(^SR(RX,SX)) G D03
	G D2A
D03	W /C(0,22),@P1,"Amend" W:DEL ", Delete" W " or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) G XSGI:'AMD W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSGI:X="Y",D03
	I X="A" G A01
	I X="D",DEL W @P1,"  Are you sure ? ",@P2 D ^GETX D CASE I X="Y" K ^SA(SX) G XSGI
	G D03

	;Amend
A01	S AMD=1 W /C(0,5),/EF F %J=2:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSGI:%J=2,A01
	G E02

CASE	S X=$zconvert(X,"U") Q

XSGJ
XSGJ	;GRI,,,;03:07 PM  20 Nov 2001
	;=======================================================
	; Virgo Accounts - Post Correction (one-sided NL entry)
	;
	; Copyright Graham R Irwin 1993-1994
	;
	; Screens:       1019
	; Files updated: ^AB ^AY ^SY
	; Subroutines:   X0^XSXS, ^INPASSW, ^INPUT, ^GETX, YX^XSXS
	;
	;=======================================================

	S TT="ADJ",H2="Post Correction",A4="Adjustment/correction"
	D X0^XSXS W @P1
	D ^INPASSW I x'="ELECTRA" Q
	S %S=1019 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSGJ Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSGJ:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	D YX^XSXS S ^SY(DAT,TT,YX)=DAT_%_"Acct:"_%_AX_%_%_A2_%_%_%_%_%_%_A5
	S T1=A2 D UPAB
	Q

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSGJ:%J=1,A01
	G E02

	;Validation - Period/year
V02	I "PYBL"'[X S ERR="Must be 'P' 'Y' 'B' or 'L'"
	Q
	;Account code
V03	Q:$D(ERR)  S A0=$j($p(^AB(X),%,3),0,2)
	Q

UPAB	S AB=^AB(AX),AB1=$p(AB,%,1),AB2=$p(AB,%,2),AB3=$p(AB,%,3),AB4=$p(AB,%,4),AB5=$p(AB,%,5,99) S:"PB"[A3 AB2=AB2+T1 S:"YB"[A3 AB3=AB3+T1 S:A3="L" AB4=AB4+T1 S ^AB(AX)=AB1_%_AB2_%_AB3_%_AB4_%_AB5
	S S1=T1 I A3="L" S S1=0,A5=A5_": last yr adj = "_T1
	S ^AY(AX,YX)=DAT_%_S1_%_TT_%_%_A5
	Q

CASE	S X=$zconvert(X,"U") Q

XSGK
XSGK	;GRI,,,;04:43 PM  16 Jan 1995;
	;=======================================================
	; Virgo Accounts - Input Historic Bills
	;
	; Copyright Graham R Irwin 1993-95
	;
	; Screens:       1056
	; Files updated: ^SB
	; Subroutines:   X0^XSXS, ^INPUT, ^GETX
	;
	;=======================================================

	S H2="Input Historic Bills",DFF="F",NIL="0.00"
	D X0^XSXS
	S %S=1056,%J=1 D ^INPUT Q:X[%
	I $D(^SB(X)) G D01
	W /C(48,4),"* NEW RECORD *",!
	S B7=0,B10="N"
	F %J=2:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G XSGK
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSGK:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	S ^SB(BX)=B1_%_B2_%_B3_%_B4_%_B5_%_B6_%_B7_%_B8_%_B9_%_B10
	G XSGK

	;Display
D01	S SB=^SB(X),B1=$p(SB,%,1),B2=$p(SB,%,2),B3=$p(SB,%,3),B4=$p(SB,%,4),B5=$p(SB,%,5),B6=$p(SB,%,6),B7=$p(SB,%,7),B8=$p(SB,%,8),B9=$p(SB,%,9),B10=$p(SB,%,10)
	S AMD=0,E0=$j(B4+B5+B6,0,2)
D02	F %J=2:1 D D^INPUT Q:X="END"
D03	W /C(0,22),@P1,"Amend, Delete or Quit ? ",/EL,@P2 D ^GETX D CASE
	I X="Q"!(X=%) G XSGK:'AMD W @P1,"  Quit without update ? ",@P2 D ^GETX D CASE G XSGK:X="Y",D03
	I X="A" G A01
	I X="D" W @P1,"  Are you sure ? ",@P2 D ^GETX D CASE I X="Y" K ^SB(BX) G XSGK
	G D03

	;Amend
A01	S AMD=1 W /C(0,5),/EF F %J=2:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSGK:%J=2,A01
	G E02

QUIT	Q

	;Validation -----------------------------------
	;VAT amount
V01	S E0=$j(B4+B5+X,0,2) Q

CASE	S X=$zconvert(X,"U") Q

XSGL
XSGL	;GRI,,,;10:21 AM  19 Aug 2003;
	;=======================================================
	; Virgo Accounts - Post ML Correction (one-sided ML entry)
	;
	; Copyright Graham R Irwin 1993-2003
	;
	; Screens:       1080
	; Files updated: ^ML ^SY
	; Subroutines:   X0^XSXS, ^INPASSW, ^INPUT, ^GETX, YX^XSXS
	;
	;=======================================================

	S H2="Matter Ledger Correction",A4="Adjustment/correction"
	D X0^XSXS W @P1
	D ^INPASSW I x'="WOZZAP" Q
	S %S=1080 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSGL Q
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X=%) W @P1,"  Quit without update ? ",@P2 D ^GETX,CASE G XSGL:X="Y",E02
	I X="A" G A01
	I X="U" G U01
	G E02

	;Update
U01	D YX^XSXS S ^SY(DAT,TT,YX)=DAT_%_CX_%_MX_%_%_A2_%_%_%_%_%_%_A5
	S ^ML(CX,MX,YX)=DAT_%_%_A2_%_TT_%_%_%_A5
	Q

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	I X[% G XSGL:%J=1,A01
	G E02

	;Validation - Matter
V02	Q:$D(ERR)  I '$D(^ML(CX,X)) S ERR="No ledger details for this matter" Q
	S ML=^ML(CX,X),E1=$j($p(ML,%,1),0,2),E2=$j($p(ML,%,2),0,2)
	Q

	;Disb/bill
V03	I "DB"'[X S ERR="Must be 'D' or 'B'"
	S TT="DIS" I X="B" S TT="BIL"
	Q

CASE	S X=$zconvert(X,"U") Q

XSHA
XSHA	;GRI,,,;04:18 PM  15 Jan 2003
	;=======================================================
	; Virgo Accounts - Print System Parameters
	;
	; Copyright Graham R Irwin 1993-2003
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, H2DMY^DATE, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	S %H=^SZ("LDE") D H2DMY^DATE S A6=%DAT
	S %H=^SZ("LPE") D H2DMY^DATE S A7=%DAT
	S %H=^SZ("NYE") D H2DMY^DATE S A8=%DAT

	D ^OUTPUT Q:%OP[%  S N=99,P=1
	D HEAD^XSXS
	W ?5,"Practice Name",?25,^SZ("PN"),!
	W ?5,"Software Licence No",?25,^SZ("SW")
	W ?45,"Software Version",?65,$g(^MENU),!
	W ?5,"VAT Cash Basis",?25,^SZ("VCB")
	W ?45,"VAT Rate",?65,^SZ("VAT"),"%",!
	W ?5,"Detailed Time",?25,$g(^SZ("DTE"),"N")
	W ?45,"Time Units (mins)",?65,^SZ("TU"),!
	W ?5,"Auto Day-end",?25,$g(^SZ("ADE"),"Y")
	W ?45,"Date Check",?65,$g(^SZ("D30"),"Y"),!
	W ?5,"Last Day-end",?25,A6
	I $e(H0,1)'="X" W ?45,"Overdraw Client",?65,$g(^SZ("ODC"),"N")
	W !
	W ?5,"Last Period-end",?25,A7
	W ?45,"Write-off Disbs",?65,$g(^SZ("WOD"),"Y"),!
	W ?5,"Next Year-end",?25,A8,! S N=N+8

	S FX="" W !,"Fee Earners :",!! S N=N+3
A01	S FX=$O(^SF(FX)) I FX="" G A03
	W ?5,"F/e No ",FX,?15,$P(^SF(FX),%,1),! S N=N+1 D CKLINE
	G A01

A03	S (X1,X2)="" W !,"Fee Rates (* = default) :",! S N=N+2
A04	S X1=$O(^SR(X1)) I X1="" G A08
	W ! I $g(^SZ("DCG"))=X1 W ?3,"*"
	W ?5,"Charge Group ",X1," : ",$P(^SR(X1),%,1),"  " F I=1:1 W "-" Q:$X>65
	W !!?7,"Activity",?37,"Rate/hour  Rate/item",!! S N=N+5
A06	S X2=$O(^SR(X1,X2)) I X2="" G A04
	S SR=^SR(X1,X2),A1=$P(SR,%,1),A2=$P(SR,%,2),B1=$P($G(^SA(X2)),%,1)
	W ?7,X2,?10,B1 W:A1 ?39,$J(A1,6,2) W:A2 ?49,$J(A2,6,2) W ! S N=N+1 D CKLINE G A06

A08	S CX="" W !,"Cashbooks (* = default) :",!
	W ?41,"Sort code  Account No.",?66,$$^NEWNAME("@F3@"),"?",!! S N=N+4
A09	S CX=$O(^CB(CX)) I CX="" G A10
	S CB=^CB(CX),A1=$P(CB,%,1),A2=$P(CB,%,2),A3=$P(CB,%,3),A4=$P(CB,%,4)
	I $g(^SZ("DOC"),1)=CX W ?3,"*"
	W ?5,CX,?10,A1,?42,A2,?52,A3,?69,A4,!! S N=N+2 D CKLINE G A09

A10	

END	D END^XSXS Q
 
CKLINE	I N>56 D HEAD^XSXS
	Q

XSHB
XSHB	;GRI,,,;03:42 PM  18 May 2004
	;=======================================================
	; Virgo Accounts - Consistency
	;
	; Copyright Graham R Irwin 1993-2004
	;
	; Screens:       1050
	; Files updated: ^CB ^CL ^ML ^SU ^SV ^SW ^AB ^SEARCH
	; Subroutines:   X0^XSXS, ^INPUT, ^%mdslist, ^%dsverify,
	;                ^%now, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	L (^SY,^SZ,^SC,^SM)
	S YES="Y",NO="N",NC=0,XXX="* BALANCE AMENDED *"
	W @P2,?8,"** HAVE YOU REMEMBERED TO TAKE A BACKUP? **",!!
	S %S=1050 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSHB Q
	D ^OUTPUT Q:%OP[%  S N=99,P=1 D HEAD^XSXS

	W "Part 1 : checking five datasets for data errors...",!,$$^%now
	s dsid=$$^%mdslist,dslist=""
	f i=1:1 s ds=$p(dsid,",",i) q:ds=""  s dslist=dslist_$p(ds,":",2)_":"_$p(ds,":",1)_","
	s res=$$results^%dsverify(dslist,0,%OP)
	w !!,"IMPORTANT: If any errors are shown above, please report these to the support",!,"desk and DO NOT continue with any further inputting.",!!

	I C1="N" G H01
	W !,"Part 2 : checking integrity of accounts data...",!,$$^%now,!
	;Cashbooks ----------------------------------------------------------
	S (CX,X2)="",(T1,T2,T3)=0
C01	S (A2,A3)=0,CX=$O(^CB(CX)) I CX="" G C07
	S CB=$g(^CB(CX)),B1=$P(CB,%,1,3),B4=$P(CB,%,4),B5=$P(CB,%,5),B6=$P(CB,%,6),B7=$P(CB,%,7,99)
	I CX<100 S T1=T1+B6
	I CX>99 S:B4="N" T2=T2+B6 S:B4="Y" T3=T3+B6
C03	S X2=$O(^CB(CX,X2)) I X2="" G C05
	S CB=$g(^CB(CX,X2)),A2=$P(CB,%,2)+A2,A3=$P(CB,%,3)+A3
	G C03
C05	S Q6=A3-A2 I Q6=+B6 G C01
	W !,"Cashbook ",CX," balance does not equal sum of transactions",?60,XXX,!?2,"Balance = ",B6,?25," Sum of trans = ",Q6,?55," Diff = ",B6-Q6,! D CHK S ^CB(CX)=B1_%_B4_%_B5_%_Q6_%_B7,NC=NC+1
	G C01

C07	S XXY="CB",AX="A130",X=T1 D ACNEQ
	S AX="L010",X=T2 D ACNEQ
	S AX="L020",X=T3 D ACNEQ

	;Matter Ledger ------------------------------------------------------
	S (CX,MX,X3)="",(T3,T4)=0
D01	S CX=$O(^ML(CX)) I CX="" G D08
D02	S (T1,T2)=0,MX=$O(^ML(CX,MX)) I MX="" G D01
	S ML=$g(^ML(CX,MX)),B1=$p(ML,%,1),B2=$p(ML,%,2)
	d checkit
D04	S X3=$O(^ML(CX,MX,X3)) I X3="" G D06
	S ML=$g(^ML(CX,MX,X3)),A2=$p(ML,%,2),A3=$p(ML,%,3),A4=$p(ML,%,4)
	I "BIL,BPY,BCR,TCO,COB,N2B"[A4 S T2=T2+A3-A2
	I "DIS,DSW,DSP,DSA,TOC,COD,OMT,N2D"[A4 S T1=T1+A3-A2
	G D04
D06	I T1'=+B1 W !,"Matter ",CX,"/",MX," disbs balance not equal to sum of trans",?60,XXX,!?2,"Balance = ",B1,?25," Sum of trans = ",T1,?55," Diff = ",B1-T1,! D CHK S ^ML(CX,MX)=T1_%_B2_%,NC=NC+1
	I T2'=+B2 W !,"Matter ",CX,"/",MX," bills balance not equal to sum of trans",?60,XXX,!?2,"Balance = ",B2,?25," Sum of trans = ",T2,?55," Diff = ",B2-T2,! D CHK S ^ML(CX,MX)=T1_%_T2_%,NC=NC+1
	S T3=T3+T1,T4=T4+T2
	G D02
D08	S XXY="ML",AX="A120",X=T3 D ACNEQ
	S AX="A110",X=T4 D ACNEQ

	;W-I-P --------------------------------------------------------------
	S (CX,MX,FX,X4)="",T4=0
E01	S CX=$O(^SW(CX)) I CX="" G E08
E02	S T2=0,MX=$O(^SW(CX,MX)) I MX="" G E01
	S SW=$g(^SW(CX,MX)),S1=$P(SW,%,1),S2=$P(SW,%,2)
	d checkit
E04	S FX=$O(^SW(CX,MX,FX)) I FX="" G E06
E05	S X4=$O(^SW(CX,MX,FX,X4)) I X4="" G E04
	S SW=$g(^SW(CX,MX,FX,X4)),W1=$P(SW,%,1,2),W3=$P(SW,%,3),T2=T2+W3
	G E05
E06	I T2'=S1 W !,"Matter ",CX,"/",MX," WIP balance not equal to sum of trans",?60,XXX,!?2,"Balance = ",S1,?25," Sum of trans = ",T2,?55," Diff = ",S1-T2,! D CHK S ^SW(CX,MX)=T2_%_S2_%,NC=NC+1
	S T4=T4+T2
	G E02
E08	S XXY="WIP",AX="N020",X=T4 D ACNEQ

	;Client Ledger ------------------------------------------------------
	S (CX,MX,X3)="",(T3,T4)=0
F01	S CX=$O(^CL(CX)) I CX="" G F08
F02	S T1=0,MX=$O(^CL(CX,MX)) I MX="" G F01
	S CL=$g(^CL(CX,MX)),B1=$P(CL,%,1),B2=$P(CL,%,2,99)
	d checkit
F04	S X3=$O(^CL(CX,MX,X3)) I X3="" G F06
	S CL=$g(^CL(CX,MX,X3)),A1=$P(CL,%,1),A2=$P(CL,%,2),A3=$P(CL,%,3),A4=$P(CL,%,4,99)
	S T1=T1+A3-A2
	G F04
F06	I T1'=B1 W !,"Matter ",CX," / ",MX," client balance not equal to sum of trans",?60,XXX,!?2,"Balance = ",B1,?25," Sum of trans = ",T1,?55," Diff = ",B1-T1,! D CHK S ^CL(CX,MX)=T1_%_B2,NC=NC+1
	I $P($g(^SM(CX,MX)),%,5)="Y" S T4=T4+T1
	E  S T3=T3+T1
	G F02
F08	S XXY="CL",AX="L010",X=T3 D ACNEQ
	S AX="L020",X=T4 D ACNEQ

	;VAT File -----------------------------------------------------------
	;S SV=$g(^SV),V1=$P(SV,%,1,2),V3=$P(SV,%,3),V4=$P(SV,%,4),(T3,T4)=0,X1=""
G01	;S X1=$O(^SV(X1)) I X1="" G G08
	;S SV=^SV(X1),T3=T3+$P(SV,%,3),T4=T4+$P(SV,%,4)
	;G G01
G08	;I T3'=+V3 W !,"VAT o/p not equal to sum of transactions",?60,XXX,!?2,"Balance = ",V3,?25," Sum of trans = ",T3,?55," Diff = ",V3-T3,! D CHK S NC=NC+1
	;I T4'=+V4 W !,"Outputs not equal to sum of transactions",?60,XXX,!?2,"Balance = ",V4,?25," Sum of trans = ",T4,?55," Diff = ",V4-T4,! D CHK S NC=NC+1
	;S ^SV=V1_%_T3_%_T4

	;Nominal ledger -----------------------------------------------------
	S AX=""
J01	S AX=$o(^AB(AX)) I AX=""!(AX]"M999") G J09 ;ignore non-financial a/cs
	S X2="",T1=0,AB=$g(^AB(AX)),A14=$p(AB,%,1,4),A5=$p(AB,%,5)
	;check for system a/cs
	i "A110;A120;A130;E280;E330;E340;I020;L010;L020;L040;L140;L510;L520;L530"[AX,A5'="Y" S ^AB(AX)=A14_%_"Y"_%
J02	S X2=$o(^AY(AX,X2)) I X2="" G J04
	S AY=^AY(AX,X2),T1=T1+$p(AY,%,2)
	G J02
J04	S XXY="AY",X=T1 D ACNEQ
	G J01
J09
	;Bill register ------------------------------------------------------
	S BX=""
K01	S BX=$o(^SB(BX)) I BX="" G K09
	S SB=$g(^SB(BX)),B1=$p(SB,%,1),CX=$p(SB,%,2),MX=$p(SB,%,3),B4=$p(SB,%,4,7),B8=$p(SB,%,8)
	I $d(^SM(CX,MX))!(+B8=0) G K01
	W !,"Unpaid bill ",BX," matter unknown",?60,"* MARKED AS PAID *",!?5,"Client ",CX," matter ",MX,! D CHK S NC=NC+1
	S ^SB(BX)=B1_%_CX_%_MX_%_B4_%_0_%_"F"_%_"N"_%
	G K01
K09
	;Standing orders ----------------------------------------------------
	S (X1,X2)=""
L02	S X1=$o(^SX(X1)) I X1="" G L09
L03	S X2=$o(^SX(X1,X2)) I X2="" G L02
	I '$d(^SO(X2)) K ^SX(X1,X2)
	G L03
L09
	;Check clients without matters
	S CX=""
M01	S CX=$o(^SC(CX)) I CX="" G M09
	I '$d(^SM(CX)) W !,"Warning: client ",CX," has no matters",! D CHK
	G M01
M09

Z99	W ! W:NC=0 "No" W:NC>0 NC W " consistency error" W:NC'=1 "s"
	I NC>0 W " - Please report th" W:NC=1 "is" W:NC'=1 "ese" W " to the support desk",!

H01	I C2="N" G I01
	;Recreate SEARCH file -----------------------------------------------
	D CHK W !!,"Part 3 : recreating index files...",!,$$^%now
	D RECR W !,"Index files recreated",!

I01	I C3="N" G END
	;Compress database --------------------------------------------------
	D CHK W !!,"Part 4 : compressing database...",!,$$^%now
	u 0 w @P5,/c(0,12) d ^XSCOMPACT
	u %OP
	w !,"Database compressed",!

END	S (CX,MX)=""
	D END^XSXS
	Q

	;Subroutine to check account balance
ACNEQ	S AB=^AB(AX),A1=$P(AB,%,1),A2=$P(AB,%,2),A3=$P(AB,%,3),A4=$P(AB,%,4,99) I +A3=X Q
	W !,"Account ",AX," balance not equal to sum of ",XXY," transactions",?60,XXX,!?2,"Balance = ",A3,?25," Sum of trans = ",X,?55," Diff = ",A3-X,! D CHK S NC=NC+1
	I "AL"[$e(AX,1)!(AX="N020") S A2=X
	S ^AB(AX)=A1_%_A2_%_X_%_A4
	Q

	;Subroutine to recreate SEARCH files (also used by XSINSTAL)
RECR	F X="SC","AB","CB","SR","SF" D SCH
	K ^SEARCH("SM") S ^SEARCH("SM")="D S^XSXS"
	K ^SEARCH("SA") S ^SEARCH("SA")="D SA^XSXS"
	K ^SEARCH("SB") S ^SEARCH("SB")="D UB^XSXS"
	;Recreate unpaid bills index
	S BX="" K ^SU
R03	S BX=$o(^SB(BX)) I BX="" Q
	I +$p(^SB(BX),%,8) S ^SU(BX)=""
	G R03

SCH	K ^SEARCH(X) S XX=""
S01	S XX=$O(@("^"_X_"(XX)")) I XX="" Q
	S X1=$P(@("^"_X_"(XX)"),%,1),^SEARCH(X,$zconvert(X1,"L"),XX)=""
	G S01

	;Check for end of page
CHK	I $Y>60 D HEAD^XSXS
	Q

	;Check if matter and client exist
checkit	i '$d(^SM(CX,MX)) s ^SM(CX,MX)="Dummy"_%_%_%_%_%_%_0_%_%_%_%,^SM(CX,MX,"N")=%_%_%
	i '$d(^SC(CX)) s ^SC(CX)="Dummy"_%_%_%,^SC(CX,"A")=%_%_%_%_%,^SC(CX,"N")=%_%_%
	q

XSHC
XSHC	;GRI,,,;10:26 AM  13 Dec 2005
	;=======================================================
	; Virgo Accounts - Day End Processing
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       none
	; Files updated: ^CL ^SY ^SZ
	; Subroutines:   X0^XSXS, ^OUTPUT, HEAD^XSXS, END^XSXS,
	;                ^GETX, ^XSSTO
	;
	;=======================================================

	; A1 - effective date of last day-end
	; A2 - effective date of this day-end

	D X0^XSXS
	L (^SY,^SZ,^SC,^SM)

	D ^OUTPUT Q:%OP[%  S N=99,P=1 D HEAD^XSXS
	S A1=^SZ("LDE"),A2=+$H S:A2-A1>1 A2=A2-1 S N1=A2-A1

	;Process standing orders
	D ^XSSTO

	;Print Daybook
	I $O(^SY(""))="" W !,"No Daybook Transactions",! S N=N+2 G D01
	S (X1,X2,X3)=""
C02	S X1=$O(^SY(X1)) I X1="" G D01
	W !,"Daybook for ",X1,!!,"Type Date",?16,"Cli-Matter  C/B Net Amount VAT Amount  Cli-Matter  C/B Ref.",!! S N=N+5
C03	S X2=$O(^SY(X1,X2)) I X2="" G C02
C04	S X3=$O(^SY(X1,X2,X3)) I X3="" G C03
	S SY=^SY(X1,X2,X3),B1=$P(SY,%,1),B2=$P(SY,%,2),B3=$P(SY,%,3),B4=$P(SY,%,4),B5=$P(SY,%,5),B6=$P(SY,%,6),B7=$P(SY,%,7),B8=$P(SY,%,8),B9=$P(SY,%,9),B10=$P(SY,%,10),B11=$p(SY,%,11)
	W X2,?4,B1,?16,B2,?22,B3,?28,B4,?32,$J(B5,10,2) W:B6 ?43,$J(B6,10,2) W ?55,B7,?61,B8,?67,B9,?71,B10,!?2,B11,! S N=N+2 I N>53 D HEAD^XSXS
	G C04

	;Calculate CL interest and check for overdrawn CL accounts
D01	I $g(^SZ("ADE"))'="Y" G F09
	i $e(H0,1)="X" G F09
	I '$d(^CI) W !!,"*** No Client Interest Rates Set Up ***",!! S N=N+4 G F09
	S (CX,MX)="",(T1,T2)=0
F01	S CX=$O(^CL(CX)) I CX="" G F08
F02	S MX=$O(^CL(CX,MX)) I MX="" G F01
	S CL=^CL(CX,MX),B1=$P(CL,%,1),B2=$P(CL,%,2),B3=$P(CL,%,3,99)
	I B1<0 W !,"*** WARNING ***  Client ",CX," Matter ",MX,?42,"Client Account Overdrawn",?74,"***",! S N=N+2,T1=1
	S IX="",CI=0
F04	S IX=$O(^CI(IX)) I IX=""!(IX>B1) G F06
	S CI=^CI(IX) G F04
F06	S C1=B1*N1*CI/36500,T2=T2+C1,B2=B2+C1,^CL(CX,MX)=B1_%_B2_%_B3
	G F02

F08	I 'T1 W !!,"No Overdrawn Client Accounts",! S N=N+3
	W !,"Total Nominal Interest Calculated = ",$j(T2,0,2),!! S N=N+3

F09	S ^SZ("LDE")=A2

	;Check credit office accounts
	S (CX,MX)=""
G02	S CX=$o(^ML(CX)) I CX="" G H01
G03	S MX=$o(^ML(CX,MX)) I MX="" G G02
	S ML=$g(^ML(CX,MX)),M1=$p(ML,%,1),M2=$p(ML,%,2)
	I M1<0 W "*** WARNING ***  Client ",CX," Matter ",MX,?42,"Disbursement Balance in Credit",?74,"***",! S N=N+1
	I M2<0 W "*** WARNING ***  Client ",CX," Matter ",MX,?42,"Bills Balance in Credit",?74,"***",! S N=N+1
	G G03

	;Count clients & matters
H01	S CX="",CN=0
H02	S CX=$o(^SC(CX)) I CX="" G H03
	S CN=CN+1 G H02

H03	S MX="",(MNO,MNC)=0
H04	S CX=$o(^SM(CX)) I CX="" G H06
H05	S MX=$o(^SM(CX,MX)) I MX="" G H04
	I $d(^SM(CX,MX,"C")) S MNC=MNC+1
	E  S MNO=MNO+1
	G H05

H06	W !,"Total No of Clients  = ",CN
	W !,"No of Open Matters   = ",MNO
	W !,"No of Closed Matters = ",MNC,!

	D END^XSXS

F99	W /c(0,22),@P1,"Has daybook printed OK ? ",/el,@P2 D ^GETX S X=$zconvert(X,"U")
	I X="N" G XSHC
	I X'="Y" G F99

	;Delete daybook file
	S X1=""
Z02	S X1=$O(^SY(X1)) I X1="" Q
	K ^SY(X1) G Z02

AUTO	S PGM="XSHC",H2="Day End Processing" G XSHC

XSHD
XSHD	;GRI,,,;04:52 PM  22 Mar 2002
	;=======================================================
	; Virgo Accounts - Period End Processing
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1060
	; Files updated: ^AB ^AY ^SZ
	; Subroutines:   X0^XSXS, ^INPUT, PRINT^XSCE, ^GETX,
	;                ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	;S A1=^SZ("LDE") I A1<+$H D ^XSHC
	D X0^XSXS
	L (^SY,^SZ,^SC,^SM)

	;entry point from year-end
AA	W /c(0,4),/ef S NO="N"
	S %S=1060 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSHD Q
	I A2="Y" W !?8,"WARNING: This could be a long report. You are advised to print it",!?8,"to the Spooler or to a File - if unsure please refer to the manual",!?8,"for details.",!

	D ^OUTPUT Q:%OP[%  S N=99,P=1 D HEAD^XSXS

	;Expense accounts
	S X1="E0",T1=0
A02	S X1=$O(^AB(X1)) I X1]"E999"!(X1="") G B01
	S AB=^AB(X1),B1=$P(AB,%,1),T1=T1+$p(AB,%,2),B3=$P(AB,%,3,99),^AB(X1)=B1_%_0_%_B3
	G A02

	;Income accounts
B01	S X1="I0",T2=0
B02	S X1=$O(^AB(X1)) I X1]"I999"!(X1="") G C01
	S AB=^AB(X1),B1=$P(AB,%,1),T2=T2+$p(AB,%,2),B3=$P(AB,%,3,99),^AB(X1)=B1_%_0_%_B3
	G B02

	;Update profit/loss account
C01	S X1="L530",AB=^AB(X1),B1=$p(AB,%,1),B2=$p(AB,%,2),B3=$p(AB,%,3,99),^AB(X1)=B1_%_(B2-T1+T2)_%_B3

	I A2="N" G END

	;Print transactions
	S (AX,X2)=""
E01	S AX=$o(^AY(AX)) I AX=""!($e(AX,1)="N") G F00
	D PRINT^XSCE W !! S N=N+2
	G E01

	;Remove transactions
F00	U 0 W /c(0,22),@P1,"Remove transactions ? ",/el,@P2 D ^GETX S X=$zconvert(X,"U")
	I X="N" G END
	I X'="Y" G F00
	S (AX,X2)=""
F01	S T1=0,AX=$o(^AY(AX)) I AX="" G END
F02	S X2=$o(^AY(AX,X2)) I X2="" G F05
	S AY=^AY(AX,X2),T1=T1+$p(AY,%,2)
	G F02

F05	K ^AY(AX) I $e(AX,1)'="N" S ^AY(AX,0)=DAT_%_T1_%_%_%_"Balance forward"
	G F01

END	S ^SZ("LPE")=+$H
	U %OP W "Period End Processing Run On ",DAT,!!,"Keep This Report For Audit Purposes",!
	D END^XSXS Q

	;Validation
V01	I X'="PDE" S ERR="Wrong confirmation"
	Q

XSHE
XSHE	;GRI;10:32 AM  16 Mar 1999
	;=======================================================
	; Virgo Accounts - Year End Processing
	;
	; Copyright Graham R Irwin 1993-99
	;
	; Screens:       1061
	; Files updated: ^AB
	; Subroutines:   X0^XSXS, AA^XSHD, H2DMY^DATE, DMY2H^DATE,
	;                ^INPUT, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	L (^SY,^SZ,^SC,^SM)
	S A1=^SZ("LPE")
	I A1<+$H S PSV=PGM,PGM="XSHD" D AA^XSHD Q:'$d(%OP)  Q:%OP[%  S PGM=PSV
	S %H=^SZ("NYE") D H2DMY^DATE S A2=%DAT
	W /C(0,4),/EF S %S=1061 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSHE Q
	D ^OUTPUT Q:%OP[%  S N=99,P=1 D HEAD^XSXS

	;Expense accounts
	S X1="E0",T1=0
A02	S X1=$O(^AB(X1)) I X1]"E999"!(X1="") G B01
	S AB=^AB(X1),B1=$P(AB,%,1,2),B3=$P(AB,%,3),B5=$P(AB,%,5,99),^AB(X1)=B1_%_0_%_B3_%_B5,T1=T1+B3
	G A02

	;Income accounts
B01	S X1="I0",T2=0
B02	S X1=$O(^AB(X1)) I X1]"I999"!(X1="") G C01
	S AB=^AB(X1),B1=$P(AB,%,1,2),B3=$P(AB,%,3),B5=$P(AB,%,5,99),^AB(X1)=B1_%_0_%_B3_%_B5,T2=T2+B3
	G B02

	;Asset accounts
C01	S X1="A0"
C02	S X1=$O(^AB(X1)) I X1]"A999"!(X1="") G D01
	S AB=^AB(X1),B1=$P(AB,%,1,2),B3=$P(AB,%,3),B5=$P(AB,%,5,99),^AB(X1)=B1_%_B3_%_B3_%_B5
	G C02

	;Liability accounts
D01	S X1="L0"
D02	S X1=$O(^AB(X1)) I X1]"L999"!(X1="") G E01
	S AB=^AB(X1),B1=$P(AB,%,1,2),B3=$P(AB,%,3),B5=$P(AB,%,5,99),^AB(X1)=B1_%_B3_%_B3_%_B5
	G D02

	;Non-financial accounts
E01	S X1="N0"
E02	S X1=$O(^AB(X1)) I X1]"N999"!(X1="") G F01
	S AB=^AB(X1),B1=$P(AB,%,1,2),B3=$P(AB,%,3),B5=$P(AB,%,5,99),B2=0
	I X1="N020" S B2=B3
	S ^AB(X1)=B1_%_B2_%_B3_%_B5
	G E02

	;Update capital, current and p&l accounts
F01	S X1="L530",AB=^AB(X1),B1=$p(AB,%,1),B5=$p(AB,%,5,99),^AB(X1)=B1_%_0_%_0_%_(T2-T1)_%_B5
	S X1="L520",AB=^AB(X1),B1=$p(AB,%,1),B3=$p(AB,%,3),B5=$p(AB,%,5,99),^AB(X1)=B1_%_0_%_0_%_B3_%_B5,B3X=B3
	S X1="L510",AB=^AB(X1),B1=$p(AB,%,1),B2=$p(AB,%,2),B5=$p(AB,%,5,99),B2X=T2-T1+B3X+B2,^AB(X1)=B1_%_B2X_%_B2X_%_B2_%_B5

	;Recreate nominal ledger
	K ^AY D ^XSXA

	W "Year End Processing Run On ",DAT," effective date ",A2,!!,"Keep This Report For Audit Purposes",!
	S %DAT=$p(A2,"/",1,2)_"/"_($p(A2,"/",3)+1) D DMY2H^DATE S ^SZ("NYE")=%H

	D END^XSXS Q

	;Validation - y/e confirm
V01	I X'="YRE" S ERR="Wrong confirmation"
	Q

XSHG
XSHG	;GRI,,,;12:51 PM  8 Feb 2002;
	;=======================================================
	; Virgo Accounts - Print Standing Entries
	;
	; Copyright Graham R Irwin 1999-2002
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, H2DMY^DATE, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS
	D ^OUTPUT Q:%OP[%  S N=99,P=1
	D HEAD^XSXS
	W "ID",?8,"--Start--- ---End----   Freq C/B Net",?52,"VAT",?63,"Ref    Acct",!! S N=N+2

	S D0=""
D01	S D0=$o(^SO(D0)) I D0="" G END
	S SO=^SO(D0),D1=$P(SO,%,1),D2=$P(SO,%,2),D3=$P(SO,%,3),D4=$P(SO,%,4),DX=$P(SO,%,5),O2=$P(SO,%,6),O3=$P(SO,%,7),O4=$P(SO,%,8),AX=$P(SO,%,9),O5=$P(SO,%,10),%H=$p(SO,%,11),E2=$j(O2+O3,0,2)
	D H2DMY^DATE
	W D0,?8,D1,?19,D2,?30,D3,?32,D4,?37,DX,?41,O2,?52,O3,?63,O4,?70,AX,!?4,O5,?54,"Next due: ",%DAT,!! S N=N+3 I N>60 D HEAD^XSXS
	G D01

END	D END^XSXS
	Q

XSHH
XSHH	;GRI,,,;11:28 AM  19 Jul 2002;
	;=======================================================
	; Virgo Accounts - Print Bill Register
	;
	; Copyright Graham R Irwin 2002
	;
	; Screens:       none
	; Files updated: none
	; Subroutines:   X0^XSXS, H2DMY^DATE, ^OUTPUT, HEAD^XSXS, END^XSXS
	;
	;=======================================================

	D X0^XSXS

	D ^OUTPUT Q:%OP[%  S N=99,P=1
	D HEAD^XSXS
	D HEAD

	S BX=""
B01	S BX=$o(^SB(BX)) I BX="" G END
	S SB=^SB(BX),B1=$p(SB,%,1),B2=$p(SB,%,2),B3=$p(SB,%,3),B4=$p(SB,%,4),B5=$p(SB,%,5),B6=$p(SB,%,6),B7=$p(SB,%,7),B8=$p(SB,%,8),B9=$p(SB,%,9)
	W BX,?7,B1,?18,B2,?23,B3,?27,$j(B4,11,2),$j(B5,11,2),$j(B6,11,2),$j(B8,11,2)," ",B9,! S N=N+1 I N>60 D HEAD^XSXS
	G B01

END	D END^XSXS
	Q

HEAD	W "Bill # Date",?18,"Cl/Matter Fees value Disb value VAT amount O/S amount St",!! S N=N+2
	Q

XSINIT
XSINIT	;GRI,,,;03:35 PM  4 Jan 1999
	;=======================================================
	; Virgo Accounts - Initialisation
	;
	; Copyright Graham R Irwin 1998
	;
	; Screens:       none
	; Files updated: ^SYS("COL")
	; Subroutines:   ^XSINSTAL, AUTO^XSAF, AUTO^XSHC
	;
	;=======================================================

	D V

	;if no parameters do install
	I '$D(^SZ) D ^XSINSTAL

	;if day-end was not run yesterday do it now
	I $g(^SZ("ADE"))'="N",+$H-^SZ("LDE")>1 D EOD

	;check and run auto matter limits report
	I $d(^SZ("MLR")) S X=^SZ("MLR"),A1=$p(X,%,1),A4=$p(X,%,4) I A1="Y",A4<+$H D AUTO^XSAF

	;check if year-end has been run
	I $d(^SZ("NYE")),^SZ("NYE")<$h W !!,@P3,"Year-end Processing has not yet been run",!!,"Press any key to continue " R *X R:'X *X
	Q

	;Initialise variables
V	S $zp="^",%=$zp,(CX,MX)="",PU="" F J=1:1:80 S PU=PU_"_"
	I '$D(^SYS("COL")) S ^SYS("COL")=7_%_7_%_7_%_7_%_7
	S X=^SYS("COL"),P1=$p(X,%,1),P2=$p(X,%,2),P3=$p(X,%,3),P4=$p(X,%,4),P5=$p(X,%,5),p=+$p(X,%,6),j="/color(",k=","_p_")",P1=j_P1_k,P2=j_P2_k,P3=j_P3_k,P4=j_P4_k,P5=j_P5_k
	S H0=$g(^SZ("SW")),H1="Virgo Accounts"
	W @P1,#
	Q

EOD	D AUTO^XSHC I $G(%OP)=% H
	Q

XSINSTAL
XSINSTAL ;GRI,,,;02:14 PM  8 Feb 2002
	;=======================================================
	; Virgo Accounts - Installation Routine
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       1099
	; Files updated: all
	; Subroutines:   X0^XSXS, ^DATE, ^INPUT, ^GETX, RECR^XSHB
	;
	;=======================================================

	S PGM="XSINSTAL",H2="Installation Routine",A6="X"
	D X0^XSXS,^DATE
	S %S=1099 F %J=1:1 D ^INPUT Q:X="END"!(X[%)
	I X[% G:%J>1 XSINSTAL H
E02	W /C(0,22),@P1,"Update, Amend or Quit ? ",/EL,@P2 D ^GETX,CASE
	I X="Q"!(X[%) G XSINSTAL
	I X="A" G A01
	I X'="U" G E02

	;Update
	W @P1,"  Please wait - Creating files ..."
	K ^SA,^SB,^SC,^SD,^SF,^SM,^SR,^SU,^SV,^SW,^SY,^SZ,^AB,^AY,^CA,^CB,^CI,^CL,^CN,^ML,^TE,^SO,^SX,^NB

	;Activity codes
	S ^SA("!C")="Carried forward"
	S ^SA("G")="General"
	S ^SA("Z!")="Written Off Time"

	;Charge rates
	S X1=0,X2="Non Legal Aid Work" S:A6="X" X2="Standard Work" D SETSR
	S Y1="G",Y2=A7,Y3=0 D SETSR2

	S ^SY=0
	S ^SZ("VAT")=17.5,^SZ("PN")=A1,^SZ("SW")=A3,^SZ("VCB")=A4,^SZ("TU")=A5,(^SZ("LDE"),^SZ("LPE"))=$H-1,^SZ("ADE")="Y"
	S %DAT=A8 D DMY2H^DATE S ^SZ("NYE")=%H

	;Account Balance File
	S X1="I020",X2="Profit Costs (fees)",X3="Y" D SETAB
	S X1="I030",X2="Interest Received",X3="N" D SETAB
	S X1="I040",X2="Other Income" D SETAB
	S X1="E010",X2="Staff Costs" D SETAB
	S X1="E120",X2="Rent & Rates" D SETAB
	S X1="E130",X2="Heating & Lighting" D SETAB
	S X1="E140",X2="Office Maintenance" D SETAB
	S X1="E150",X2="Equipment Hire & Maint" D SETAB
	S X1="E160",X2="Insurance & Pract Certs" D SETAB
	S X1="E170",X2="Telephone & Fax" D SETAB
	S X1="E180",X2="Stationery & Printing" D SETAB
	S X1="E190",X2="Publications & Books" D SETAB
	S X1="E200",X2="Subscriptions" D SETAB
	S X1="E210",X2="Training" D SETAB
	S X1="E220",X2="Advertising & Promotion" D SETAB
	S X1="E230",X2="Agency Fees" D SETAB
	S X1="E240",X2="Bank Charges" D SETAB
	S X1="E250",X2="Audit & Accountancy Fees" D SETAB
	S X1="E260",X2="Other Fees" D SETAB
	S X1="E270",X2="Office Travelling Expenses" D SETAB
	S X1="E280",X2="Non-recoverable Expenses",X3="Y" D SETAB
	S X1="E290",X2="Motor Expenses",X3="N" D SETAB
	S X1="E300",X2="Bad Debts" D SETAB
	S X1="E310",X2="Sundry Expenses" D SETAB
	S X1="E320",X2="Depreciation" D SETAB
	S X1="E330",X2="Bills Written Off",X3="Y" D SETAB
	S X1="E340",X2="Interest Paid" D SETAB
	S X1="A010",X2="Land & Buildings",X3="N" D SETAB
	S X1="A020",X2="Motor Vehicles" D SETAB
	S X1="A030",X2="Office Equipment" D SETAB
	S X1="A040",X2="Other Assets" D SETAB
	S X1="A110",X2="O/S Unpaid Bills",X3="Y" D SETAB
	S X1="A120",X2="Unbilled Disbursements" D SETAB
	S X1="A130",X2="Office Bank Account" D SETAB
	S X1="L010",X2="Client Control Account" D SETAB
	S X1="L020",X2="Trust Control Account" D SETAB
	;S X1="L030",X2="Stakeholder Control A/c" D SETAB
	S X1="L040",X2="VAT Control Account" D SETAB
	S X1="L050",X2="Claims Reserve",X3="N" D SETAB
	S X1="L070",X2="Office Loans" D SETAB
	S X1="L080",X2="Bad Debt Provision" D SETAB
	S X1="L140",X2="VAT Suspense Account",X3="Y" D SETAB
	S X1="L510",X2="Capital Account" D SETAB
	S X1="L520",X2="Current Account" D SETAB
	S X1="L530",X2="Profit/loss Account" D SETAB
	S X1="N020",X2="Work-in-Progress" D SETAB
	S X1="N030",X2="Time Booked" D SETAB
	S X1="N040",X2="Time Billed" D SETAB
	S X1="N050",X2="Time Written Off" D SETAB
	S X1="N060",X2="Disbursements Booked" D SETAB
	S X1="N070",X2="Disbursements Billed" D SETAB
	S X1="N080",X2="Disbursements Written Off" D SETAB
	S X1="N090",X2="Bills Raised" D SETAB
	S X1="N110",X2="Credits Raised" D SETAB

	;Fee Earner File
	S ^SF(1)=A2

	;Cashbooks
	S ^CB(1)="Office No 1 Account"_%_"00-00-00"_%_%_"N"_%_0_%_0_%
	S ^CB(100)="Client Account"_%_"00-00-00"_%_%_"N"_%_0_%_0_%
	S ^CB(101)="Trust Account"_%_"00-00-00"_%_%_"Y"_%_0_%_0_%

	;Client interest bands
	S ^CN(1000)=56,^(2000)=28,^(10000)=14,^(20000)=7

	;Recreate SEARCH file
	D RECR^XSHB

	W /C(0,4),/EF,@P5,"All system files have now been created. You may now start to set up:",!!
	W "*",?6,"other fee earners (if any)",!!
	W "*",?6,"your clients and matters",!!
	W "*",?6,"other bank accounts",!!
	W "*",?6,"client interest rates",!!
	W "*",?6,"your opening balances",!!
	W "You may also wish to print, review (and if necessary amend) the system",!,"parameters. Run off the System Parameters listing and use the appropriate",!,"function to amend the required details.",!!!
	W "Press ENTER to continue ",@P5 R X
	Q

SETAB	S ^AB(X1)=X2_%_0_%_0_%_0_%_X3_% Q

SETSR	S ^SR(X1)=X2 Q

SETSR2	S ^SR(X1,Y1)=Y2_%_Y3_% Q

CASE	S X=$zconvert(X,"U") Q

	;Amend
A01	W /C(0,4),/EF F %J=1:1 D A^INPUT Q:X="END"!(X[%)
	G XSINSTAL:X[%,E02

	;Validation - London firm
V01	I "YNX"'[X S ERR="Must be 'Y', 'N' or 'X'"
	Q

XSLEGAID
XSLEGAID ;GRI,,,;09:09 AM  26 Aug 1996
	;=======================================================
	; Virgo Accounts - Installation Routine (part 2)
	; also used by the LA fee rate update program
	;
	; Copyright Graham R Irwin 1993-1996
	;
	; Screens:       none
	; Files updated: ^SA ^SR
	; Subroutines:   none
	;
	;=======================================================

	;Activity codes
	S ^SA("P")="Preparation"
	S ^SA("L")="Letters"
	S ^SA("T")="Telephone"
	S ^SA("A")="Advocacy"
	S ^SA("C")="Court/counsel"
	S ^SA("W")="Travelling & Waiting"
	S ^SA("R")="Routine letters out/tel"
	S ^SA("AU")="Advoc Duty sol Unsoc hrs"
	S ^SA("AS")="Advoc Duty sol Std hrs"
	S ^SA("AO")="Advocacy Own solicitor"
	S ^SA("PU")="Prep Duty sol Unsoc hrs"
	;S ^SA("PS")="Prep Duty sol Std hrs"
	;S ^SA("PO")="Preparation Own sol"
	S ^SA("RU")="Rou let/tel D/sol Uns hrs"
	;S ^SA("RS")="Rou let/tel D/sol Std hrs"
	;S ^SA("RO")="Rou let/tel Own sol"
	S ^SA("WU")="Trav/wait D/sol Uns hrs"
	S ^SA("WS")="Trav/wait D/sol Std hrs"
	S ^SA("WO")="Trav/wait Own solicitor"
	S ^SA("A1")="Advocacy Senior solicitor"
	S ^SA("A2")="Advocacy Solicitor"
	S ^SA("C1")="Court/counsel Senior sol"
	S ^SA("C2")="Court/counsel Solicitor"
	S ^SA("C3")="Court/counsel Trainee"
	S ^SA("P1")="Preparation Senior sol"
	S ^SA("P2")="Preparation Solicitor"
	S ^SA("P3")="Preparation Trainee"
	S ^SA("PX")="Prep'n S-East Circuit"
	S ^SA("PY")="Prep'n Non-SE Circuit"
	S ^SA("W1")="Travel/wait Sen sol"
	S ^SA("W2")="Travel/wait Solicitor"
	S ^SA("W3")="Travel/wait Trainee"
	S ^SA("LI")="Letters In"
	S ^SA("LO")="Letters Out"
	S ^SA("AB")="Advocacy Bail appl'n"
	S ^SA("DE")="Duty sol Enh rate"
	S ^SA("DS")="Duty sol Std rate"
	S ^SA("DT")="Duty sol Travelling"
	S ^SA("ST")="Standby"
	S ^SA("TA")="Advice/assist on Teleph"
	S ^SA("TR")="Routine Telephone calls"

	;Charge Rates
	S X1="GF",X2="Green Form & ABWOR" D SETSR
	S Y1="A",Y2=55.25,Y3=0 D SETSR2
	S Y1="C",Y2=30 D SETSR2
	S Y1="P",Y2=44 S:A6="Y" Y2=46.5 D SETSR2
	S Y1="R",Y2=0,Y3=3.4 S:A6="Y" Y3=3.55 D SETSR2
	S Y1="W",Y2=24.5,Y3=0 D SETSR2

	S X1="FG",X2="FR: Green Form & ABWOR" D SETSR
	S Y1="A",Y2=57.25,Y3=0 D SETSR2
	S Y1="C",Y2=31 D SETSR2
	S Y1="P",Y2=45.5 S:A6="Y" Y2=48.25 D SETSR2
	S Y1="R",Y2=0,Y3=3.55 S:A6="Y" Y3=3.7 D SETSR2
	S Y1="W",Y2=25.5,Y3=0 D SETSR2

	S X1="MH",X2="Mental Health Review" D SETSR
	S Y1="A",Y2=64,Y3=0 D SETSR2
	S Y1="C",Y2=30 D SETSR2
	S Y1="P",Y2=52.75 S:A6="Y" Y2=56.25 D SETSR2
	S Y1="R",Y2=0,Y3=3.75 D SETSR2
	S Y1="W",Y2=24.5,Y3=0 D SETSR2

	S X1="FM",X2="FR: Mental Health Review" D SETSR
	S Y1="A",Y2=66.25,Y3=0 D SETSR2
	S Y1="C",Y2=31 D SETSR2
	S Y1="P",Y2=54.5 S:A6="Y" Y2=58.25 D SETSR2
	S Y1="R",Y2=0,Y3=3.9 D SETSR2
	S Y1="W",Y2=25.5,Y3=0 D SETSR2

	S X1="AB",X2="ABWOR Unsoc Hrs" D SETSR
	S Y1="AU",Y2=73.67,Y3=0 D SETSR2
	S Y1="C",Y2=39.99 D SETSR2
	S Y1="PU",Y2=58.67 S:A6="Y" Y2=62 D SETSR2
	S Y1="WU",Y2=32.67 D SETSR2
	S Y1="RU",Y2=0,Y3=4.53 S:A6="Y" Y3=4.73 D SETSR2

	S X1="FA",X2="FR: ABWOR Unsoc Hrs" D SETSR
	S Y1="AU",Y2=76.33,Y3=0 D SETSR2
	S Y1="C",Y2=41.33 D SETSR2
	S Y1="PU",Y2=60.67 S:A6="Y" Y2=64.33 D SETSR2
	S Y1="WU",Y2=33.99 D SETSR2
	S Y1="RU",Y2=0,Y3=4.73 S:A6="Y" Y3=4.93 D SETSR2

	S X1="MC",X2="Magistrates - Criminal" D SETSR
	S Y1="A",Y2=56.5,Y3=0 D SETSR2
	S Y1="C",Y2=30.5 D SETSR2
	S Y1="P",Y2=44.75 S:A6="Y" Y2=47.25 D SETSR2
	S Y1="R",Y2=0,Y3=3.45 S:A6="Y" Y3=3.6 D SETSR2
	S Y1="W",Y2=24.75,Y3=0 D SETSR2

	S X1="FC",X2="FR: Magistrates - Criminal" D SETSR
	S Y1="A",Y2=57.75,Y3=0 D SETSR2
	S Y1="C",Y2=31.5 D SETSR2
	S Y1="P",Y2=46 S:A6="Y" Y2=48.5 D SETSR2
	S Y1="R",Y2=0,Y3=3.55 S:A6="Y" Y3=3.7 D SETSR2
	S Y1="W",Y2=25.5,Y3=0 D SETSR2

	;S X1="CA",X2="Crown Ct/Ct of Appeal" D SETSR
	;S Y1="A1",Y2=62.5,Y3=0 D SETSR2
	;S Y1="A2",Y2=54.5 D SETSR2
	;S Y1="C1",Y2=41.25 D SETSR2
	;S Y1="C2",Y2=33 D SETSR2
	;S Y1="C3",Y2=20 D SETSR2
	;S Y1="P1",Y2=51.5 S:A6="Y" Y2=54 D SETSR2
	;S Y1="P2",Y2=43.5 S:A6="Y" Y2=45.75 D SETSR2
	;S Y1="P3",Y2=28.75 S:A6="Y" Y2=33 D SETSR2
	;S Y1="R",Y2=0,Y3=3.35 S:A6="Y" Y3=3.5 D SETSR2
	;S Y1="W1",Y2=24.25,Y3=0 D SETSR2
	;S Y1="W2" D SETSR2
	;S Y1="W3",Y2=12 D SETSR2

	S X1="CH",X2="Criminal Higher Courts" D SETSR
	S Y1="A1",Y2=64.5,Y3=0 D SETSR2
	S Y1="A2",Y2=56 D SETSR2
	S Y1="C1",Y2=41.75 D SETSR2
	S Y1="C2",Y2=34 D SETSR2
	S Y1="C3",Y2=20.5 D SETSR2
	S Y1="P1",Y2=53 S:A6="Y" Y2=55.75 D SETSR2
	S Y1="P2",Y2=45 S:A6="Y" Y2=47.25 D SETSR2
	S Y1="P3",Y2=29.75 S:A6="Y" Y2=34 D SETSR2
	S Y1="R",Y2=0,Y3=3.45 S:A6="Y" Y3=3.6 D SETSR2
	S Y1="W1",Y2=24.75,Y3=0 D SETSR2
	S Y1="W2" D SETSR2
	S Y1="W3",Y2=12.5 D SETSR2

	S X1="CV",X2="County Ct - Family" D SETSR
	S Y1="A",Y2=64,Y3=0 D SETSR2
	S Y1="C",Y2=32.5 D SETSR2
	S Y1="P",Y2=58 S:A6="Y" Y2=61.25 D SETSR2
	S Y1="LI",Y2=0,Y3=1.8 D SETSR2
	S Y1="LO",Y3=3.65 D SETSR2
	S Y1="T" D SETSR2
	S Y1="W",Y2=29.25,Y3=0 D SETSR2

	S X1="HV",X2="High Court - Family" D SETSR
	S Y1="A",Y2=64,Y3=0 D SETSR2
	S Y1="C",Y2=37 D SETSR2
	S Y1="P",Y2=65.5 S:A6="Y" Y2=69.75 D SETSR2
	S Y1="LI",Y2=0,Y3=2.05 D SETSR2
	S Y1="LO",Y3=4.25 D SETSR2
	S Y1="T" D SETSR2
	S Y1="W",Y2=32,Y3=0 D SETSR2

	S X1="HP",X2="High Ct - Prescr Family" D SETSR
	S Y1="A",Y2=55.75,Y3=0 D SETSR2
	S Y1="C",Y2=37 D SETSR2
	S Y1="PY",Y2=43.75 D SETSR2
	S Y1="PX",Y2=46.75 D SETSR2
	S Y1="LI",Y2=0,Y3=2.05 D SETSR2
	S Y1="LO",Y3=4.25 D SETSR2
	S Y1="T" D SETSR2
	S Y1="W",Y2=32,Y3=0 D SETSR2

	S X1="CP",X2="County Ct - Prescr Family" D SETSR
	S Y1="A",Y2=52.75,Y3=0 D SETSR2
	S Y1="C",Y2=32.5 D SETSR2
	S Y1="PY",Y2=38.75 D SETSR2
	S Y1="PX",Y2=41 D SETSR2
	S Y1="LI",Y2=0,Y3=1.8 D SETSR2
	S Y1="LO",Y3=3.65 D SETSR2
	S Y1="T" D SETSR2
	S Y1="W",Y2=29.25,Y3=0 D SETSR2

	S X1="CL",X2="County Court - Civil" D SETSR
	S Y1="A",Y2=65,Y3=0 D SETSR2
	S Y1="C",Y2=32 D SETSR2
	S Y1="P",Y2=65 S:A6="Y" Y2=69 D SETSR2
	S Y1="LO",Y2=0,Y3=6.5 D SETSR2
	S Y1="T",Y3=3.6 D SETSR2
	S Y1="W",Y2=28.75,Y3=0 D SETSR2

	S X1="HL",X2="High Court - Civil" D SETSR
	S Y1="A",Y2=74,Y3=0 D SETSR2
	S Y1="C",Y2=36.4 D SETSR2
	S Y1="P",Y2=74 S:A6="Y" Y2=78.5 D SETSR2
	S Y1="LO",Y2=0,Y3=7.4 D SETSR2
	S Y1="T",Y3=4.1 D SETSR2
	S Y1="W",Y2=32.7,Y3=0 D SETSR2

	S X1="AP",X2="Advice at Police Stn" D SETSR
	S Y1="ST",Y2=3.65,Y3=0 S:A6="Y" Y2=3.75 D SETSR2
	S Y1="AU",Y2=60 D SETSR2
	S Y1="AS",Y2=45.5 S:A6="Y" Y2=48.5 D SETSR2
	S Y1="AO" D SETSR2
	S Y1="WU",Y2=60 D SETSR2
	S Y1="WO",Y2=25.25 D SETSR2
	S Y1="WS",Y2=45.5 S:A6="Y" Y2=48.5 D SETSR2
	S Y1="TA",Y2=20.75 S:A6="Y" Y2=21.5 D SETSR2
	S Y1="TR",Y2=0,Y3=3.5 S:A6="Y" Y3=3.6 D SETSR2

	S X1="FP",X2="FR: Advice at Police Stn" D SETSR
	S Y1="ST",Y2=3.8,Y3=0 S:A6="Y" Y2=3.85 D SETSR2
	S Y1="AU",Y2=61.75 D SETSR2
	S Y1="AS",Y2=46.5 S:A6="Y" Y2=50.25 D SETSR2
	S Y1="AO" D SETSR2
	S Y1="WU",Y2=61.75 D SETSR2
	S Y1="WO",Y2=25.75 D SETSR2
	S Y1="WS",Y2=46.5 S:A6="Y" Y2=50.25 D SETSR2
	S Y1="TA",Y2=21.25 S:A6="Y" Y2=22 D SETSR2
	S Y1="TR",Y2=0,Y3=3.6 S:A6="Y" Y3=3.75 D SETSR2

	S X1="CC",X2="Crown Court" D SETSR
	S Y1="AB",Y2=26.25,Y3=0 S:A6="Y" Y2=28.75 D SETSR2
	S Y1="C",Y2=21.4 D SETSR2
	S Y1="W",Y2=18.5 D SETSR2

	S X1="MP",X2="Magistrates - Presc Fam" D SETSR
	S Y1="A",Y2=55.25,Y3=0 D SETSR2
	S Y1="C",Y2=30 D SETSR2
	S Y1="P",Y2=44 S:A6="Y" Y2=46.75 D SETSR2
	S Y1="R",Y2=0,Y3=3.4 S:A6="Y" Y3=3.55 D SETSR2
	S Y1="W",Y2=24.6,Y3=0 D SETSR2

	S X1="FZ",X2="FR: Mag Court - Presc Fam" D SETSR
	S Y1="A",Y2=56.25,Y3=0 D SETSR2
	S Y1="C",Y2=30.25 D SETSR2
	S Y1="P",Y2=44.5 S:A6="Y" Y2=47.5 D SETSR2
	S Y1="R",Y2=0,Y3=3.45 S:A6="Y" Y3=3.6 D SETSR2
	S Y1="W",Y2=25,Y3=0 D SETSR2

	S X1="FY",X2="FR: County Ct - Presc Fam" D SETSR
	S Y1="A",Y2=52.75,Y3=0 D SETSR2
	S Y1="C",Y2=33 D SETSR2
	S Y1="PY",Y2=39.25 D SETSR2
	S Y1="PX",Y2=41.5 D SETSR2
	S Y1="LI",Y2=0,Y3=1.85 D SETSR2
	S Y1="LO",Y3=3.7 D SETSR2
	S Y1="T" D SETSR2
	S Y1="W",Y2=29.5,Y3=0 D SETSR2

	S X1="FX",X2="FR: High Court - Presc Fam" D SETSR
	S Y1="A",Y2=56.75,Y3=0 D SETSR2
	S Y1="C",Y2=37.5 D SETSR2
	S Y1="PY",Y2=44.25 D SETSR2
	S Y1="PX",Y2=47.5 D SETSR2
	S Y1="LI",Y2=0,Y3=2.1 D SETSR2
	S Y1="LO",Y3=4.25 D SETSR2
	S Y1="T",Y3=4.2 D SETSR2
	S Y1="W",Y2=32.5,Y3=0 D SETSR2

	S X1="FH",X2="FR: High Court - Civil" D SETSR
	S Y1="A",Y2=75,Y3=0 D SETSR2
	S Y1="C",Y2=37 D SETSR2
	S Y1="P",Y2=75 S:A6="Y" Y2=79.5 D SETSR2
	S Y1="LO",Y2=0,Y3=7.5 D SETSR2
	S Y1="T",Y3=4.15 D SETSR2
	S Y1="W",Y2=33.25,Y3=0 D SETSR2

	S X1="FL",X2="FR: County Court - Civil" D SETSR
	S Y1="A",Y2=66,Y3=0 D SETSR2
	S Y1="C",Y2=32.5 D SETSR2
	S Y1="P",Y2=66 S:A6="Y" Y2=70 D SETSR2
	S Y1="LO",Y2=0,Y3=6.6 D SETSR2
	S Y1="T",Y3=3.65 D SETSR2
	S Y1="W",Y2=29.2,Y3=0 D SETSR2

	S X1="FV",X2="FR: County Ct - Civil Fam" D SETSR
	S Y1="A",Y2=65,Y3=0 D SETSR2
	S Y1="C",Y2=33 D SETSR2
	S Y1="P",Y2=59 S:A6="Y" Y2=62 D SETSR2
	S Y1="LI",Y2=0,Y3=1.85 D SETSR2
	S Y1="LO",Y3=3.7 D SETSR2
	S Y1="T" D SETSR2
	S Y1="W",Y2=29.5,Y3=0 D SETSR2

	S X1="FF",X2="FR: High Court - Civil Fam" D SETSR
	S Y1="A",Y2=65,Y3=0 D SETSR2
	S Y1="C",Y2=37.5 D SETSR2
	S Y1="P",Y2=66.5 S:A6="Y" Y2=70.75 D SETSR2
	S Y1="LI",Y2=0,Y3=2.1 D SETSR2
	S Y1="LO",Y3=4.25 D SETSR2
	S Y1="T",Y3=4.2 D SETSR2
	S Y1="W",Y2=32.5,Y3=0 D SETSR2

	S X1="OT",X2="Court Duty Solicitor" D SETSR
	S Y1="DS",Y2=50.75,Y3=0 S:A6="Y" Y2=52 D SETSR2
	S Y1="DE",Y2=63.44 S:A6="Y" Y2=65 D SETSR2
	S Y1="DT",Y2=25 D SETSR2

	S X1="FO",X2="FR: Court Duty Solicitor" D SETSR
	S Y1="DS",Y2=52.25,Y3=0 S:A6="Y" Y2=53.5 D SETSR2
	S Y1="DE",Y2=65.31 S:A6="Y" Y2=66.88 D SETSR2
	S Y1="DT",Y2=25.5 D SETSR2

	Q

SETSR	K ^SR(X1) S ^SR(X1)=X2 Q

SETSR2	S ^SR(X1,Y1)=Y2_%_Y3_% Q

XSLOAD
XSLOAD	;GRI;11:31 AM  18 Jan 1999
	;=======================================================
	; Virgo Accounts - Release Update Routine
	;
	; Copyright Graham R Irwin 1993-9
	;
	; Screens:       none
	; Files updated: ^HELP, ^MENU, ^SCREEN, ^MENU1
	;                and any others within file gsave
	; Subroutines:   auto^%gload
	;
	;=======================================================

	k ^HELP,^MENU,^SCREEN,^MENU1
	s f="gsave"
	o 10:(file=f:mode="r")
	d auto^%gload(10,0,1,"A",0,0)
	q

XSRESET
XSRESET	;gri;09:02 AM  3 Mar 1999;
	;=======================================================
	; Virgo Accounts - Reset next bill number
	;
	; Copyright Graham R Irwin 1999
	;=======================================================

	W #
	W /c(0,4),"Reset next bill number? (Y/N) " R X D CASE
	I X="Y" S ^SB=0

	Q

CASE	S X=$zconvert(X,"U") Q

XSSTO
XSSTO	;GRI,,,;11:45 AM  8 Nov 2002;
	;=======================================================
	; Virgo Accounts - Update Standing Entries
	;                  Subroutine of Day End Processing
	;
	; Copyright Graham R Irwin 1999-2002
	;
	; Screens:       none
	; Files updated: ^SO ^SX ^CB ^SV ^SY ^AB ^AY
	; Subroutines:   DMY2H^DATE, H2DMY^DATE, YX^XSXS, UPAB^XSXS
	;
	;=======================================================

	; A2 is effective date of day end ($H format)

	S VAT=^SZ("VAT")/100

	;Check for due standing entries
	S (X1,D0)=""
C01	S X1=$o(^SX(X1)) I X1=""!(A2<X1) Q
C02	S D0=$o(^SX(X1,D0)) I D0="" G C01
	S SO=^SO(D0),D1=$P(SO,%,1),D2=$P(SO,%,2),D3=$P(SO,%,3),D4=$P(SO,%,4),DX=$P(SO,%,5),O2=$P(SO,%,6),O3=$P(SO,%,7),O4=$P(SO,%,8),AX=$P(SO,%,9),O5=$P(SO,%,10),D1X=$p(SO,%,11),E2=$j(O2+O3,0,2),TT="ST"_D3
	S %H=X1 D H2DMY^DATE S O1=%DAT

	;Update ledgers
	D YX^XSXS
	S ^SY(DAT,TT,YX)=O1_%_%_%_DX_%_O2_%_O3_%_"Acct:"_%_AX_%_%_O4_%_O5
	I D3="R" S ^CB(DX,YX)=O1_%_%_E2_%_TT_%_O4_%_O5_%_"N"_%
	I D3="P" S ^CB(DX,YX)=O1_%_E2_%_%_TT_%_O4_%_O5_%_"N"_%
	I D3="R" S CB=^CB(DX),B1=$P(CB,%,1,5),B6=$P(CB,%,6)+E2,^CB(DX)=B1_%_B6_%
	I D3="P" S CB=^CB(DX),B1=$P(CB,%,1,5),B6=$P(CB,%,6)-E2,^CB(DX)=B1_%_B6_%
	S T1=O2 S:"AE"[$e(AX,1) T1=-O2
	I D3="R" D UPAB^XSXS(AX,T1,O1,TT,O4,O5)
	I D3="P" D UPAB^XSXS(AX,-T1,O1,TT,O4,O5)

	;Update VAT records
	I O3="*"!(^SZ("VCB")="X") G NV1
	I D3="R" S SV=$g(^SV),V1=$p(SV,%,1),V2=$p(SV,%,2),V3=$p(SV,%,3)+O2,V4=$p(SV,%,4)+O3,^SV=V1_%_V2_%_V3_%_V4,^SV(YX)="STR"_%_O1_%_O2_%_O3
	I D3="P" S SV=$G(^SV),V1=$P(SV,%,1)+O2,V2=$P(SV,%,2)+O3,V3=$P(SV,%,3,99),^SV=V1_%_V2_%_V3,^SV("P"_YX)="STP"_%_O1_%_O2_%_O3
	I D3="R" D UPAB^XSXS("L040",O3,O1,TT,O4,O5)
	I D3="P" D UPAB^XSXS("L040",-O3,O1,TT,O4,O5)

	;Update control a/c
NV1	I D3="R" D UPAB^XSXS("A130",E2,O1,TT,O4,O5)
	I D3="P" D UPAB^XSXS("A130",-E2,O1,TT,O4,O5)

	;Update next due date
	S %DAT=D2 D DMY2H^DATE S X9=%H
	S %H=X1 D H2DMY^DATE S D=$p(%DAT,"/",1),M=$p(%DAT,"/",2),Y=$p(%DAT,"/",3)
	I $e(D4,$l(D4))="M" S M=M+D4 S:M>12 M=M-12,Y=Y+1 S %DAT=D_"/"_M_"/"_Y D DMY2H^DATE
	I $e(D4,$l(D4))="D" S %H=X1+D4
	I %H>X9 S %H=99999
	S ^SO(D0)=D1_%_D2_%_D3_%_D4_%_DX_%_O2_%_O3_%_O4_%_AX_%_O5_%_%H_%
	K ^SX(X1,D0) S ^SX(%H,D0)=""

	G C02

XSX
XSX	;GRI,,,;09:40 AM  15 Apr 2002
	;=======================================================
	; Virgo Accounts - Main Menu
	;
	; Copyright Graham R Irwin 1993-2002
	;
	; Screens:       none
	; Files updated: ^WORK($j) ^WORK2($j) ^WORK3($j)
	; Subroutines:   ^INPASSW, ^XSINIT, ^DATE, ^Cont
	;
	;=======================================================

	; H0 is the Virgo licence number (which indentifies installed
	;	options and type of firm)
	; H1 is the Virgo Accounts descriptor
	; H2 is the program description (from the menu)
	; P1 to P5 are the screen colours
	; PGM is the program name
	; PU is a row of underscores
	; PX is the current sub-menu number
	; DAT is today's date (DD/MM/YYYY format)
	; TIM is the current time
	; CX is the default (or last used) client code
	; MX is the default (or last used) matter code

	W !!,"Virgo Accounts",!!,"Copyright Irwin Associates, 1998-2002",!

	;Check for password
	i $d(^Access) d ^INPASSW i X'=^Access h
XS0	D ^XSINIT

XSX1	S $ZT="ERROR^XSX" D ^DATE
	i $$volspace^%dos<102400 w !!,"Insufficient disk space - Unable to run Virgo Accounts.",*7,! s X=$$^Cont(0) h

	S H2="Main Menu",PX=0 D WRITE
A1	W /C(0,22),@P5,"Always Exit before switching off the computer"
	W /C(0,21),@P1,"Enter option no. (To Exit enter 0 or press Alt+F4) : ",/el D ^GETX S K=X
	I K=0!(K="****") K ^WORK($j),^WORK2($j),^WORK3($j) H
	i K="" w *7 G A1
	i K?1.2n G A2
	S H2="",PGM=K I $zrstatus(K)'="" D ^@K G XS0
	D CASE S PGM=K I $zrstatus(K)'="" D ^@K G XS0
A2	I '$d(^MENU(0,K)) W *7 G A1
	S Q=$g(^MENU(0,K,"IF")) I Q'="",@Q W *7 G A1
	S PX=$p(^MENU(0,K),%,2) G LEVL2

	;Sub-menu
LEVL2	K (%,P1,P2,P3,P4,P5,PU,PX,H0,H1,H2,PGM,DAT,TIM,CX,MX)
	S H2=$P(^MENU(PX),%,1) D ^DATE,WRITE
L2	W /C(0,21),"Enter option number : ",/el R K
	I K=""!(K[%) G XSX1
	I K'?1N.N G L2
	I '$d(^MENU(PX,K)) G L2
	S Q=$g(^MENU(PX,K,"IF")) I Q'="",@Q G L2
	S X=^MENU(PX,K),H2=$p(X,%,1),PGM=$p(X,%,2)
	D ^@PGM
	L  G LEVL2

	;Display menu options
WRITE	W @P4,#,$g(^MENU),?80-$L(H1)\2,H1,?70,DAT,!?80-$L(H2)\2,H2,?75,TIM,!!!
	S PY=""
W1	S PY=$o(^MENU(PX,PY)) I PY="" Q
	S X=^MENU(PX,PY),Q=$g(^MENU(PX,PY,"IF")) I Q'="",@Q G W1
	W ?10,@P5,PY,?15,@P1,$p(X,%,1),!
	G W1

CASE	S K=$zconvert(K,"U") Q

	;Error trap
ERROR	U 0 W @P5,*7,!,"*** An error has occurred during processing, please take a note of",!,"the following message and contact the Support Desk",!,$ZE,!,$ZERR," ",$ZENAME d ^%errlog
	R !!,"Press ENTER when ready",X G XSX1

XSXA
XSXA	;gri;08:57 AM  30 Apr 2002
	;=======================================================
	; Virgo Accounts - recreate the Nominal ledger at yrend
	;
	; Copyright Graham R Irwin 1993
	;=======================================================	;

	i $d(^AY) q
	s AX=""
A01	s AX=$o(^AB(AX)) i AX="" g A99
	s AB=^AB(AX),A3=$p(AB,%,3)
	i A3'=0 s ^AY(AX,0)=DAT_%_A3_%_%_%_"Balance forward"
	g A01

A99	q

XSXS
XSXS	;GRI,,,;09:50 AM  1 Nov 2007
	;=======================================================
	; Virgo Accounts - Common Routines
	;
	; Copyright Graham R Irwin 1993-2003
	;
	; Files updated: ^AB ^AY ^SY
	; Subroutines:   ^%video, ^Cont, ^GETX
	;
	;=======================================================

	; Matter Search  * * * * * * * * * * * * * * * * * * * *

	; Note: client code = CX

S	d uwo^%video(6,18)
	W #,@P1 S %K=1,S3=""
	;I '$D(^SM(CX)) W "No Matters for this Client" S X="" G S99
S01	S S3=$O(^SM(CX,S3)) I S3=""!($Y>13) G S03
S02	S S2=$P(^SM(CX,S3),%,1)
	;I $d(^SM(CX,S3,"C")) G S01
	W %K,?10,S3,?20,S2,! S SS(%K)=S3,%K=%K+1 G S01
S03	W /C(0,15),"Select an entry " D ^GETX I X[% G S99
	I X,$G(SS(X))="" G S03
	I 'X G S99:S3="" W # K SS G S02
	S X=SS(X)
S99	K S2,S3,SS
	d uwc^%video()
	Q

	; Start routine  * * * * * * * * * * * * * * * * * * * *

X0	W #,@P4,PGM,?80-$L(H1)\2,H1,!?80-$L(H2)\2,H2,!!!
	Q

	; Matter Code Validation * * * * * * * * * * * * * * * *

	; Note: client code = CX

EX	I '$D(^SM(CX,X)) S ERR="Does not exist" Q
	S XZ=$p(^(X),%,1)
	W:$e(XZ,$l(XZ))="$" @P3,*7 W ?48,XZ,/el
	Q

	; Get Item Number (used by all posting programs) * * * *

	; Updates ^SY

YX	L ^SY
	S YX=^SY+1,^SY=YX
	L  Q

	; Date Validation 1  * * * * * * * * * * * * * * * * * *

DAT	Q:$D(ERR)  S %DAT=X D DMY2H^DATE
	I %H>+$H S ERR="May not be in the future" Q
	I $g(^SZ("D30"))="N" Q
	I +$H-30>%H S ERR="May not be more than 30 days ago"
	Q

	; Date Validation 2  * * * * * * * * * * * * * * * * * *

DAT1	Q:$D(ERR)  S %DAT=X D DMY2H^DATE
	I %H>+$H S ERR="May not be in the future" Q
	Q

	; Update Nominal Ledger  * * * * * * * * * * * * * * * *

	; Updates ^AB and ^AY

UPAB(AX,T1,V2,V3,V4,V5)

	; Parameters:-
	; account code, amount, date, trans type, ref, narrative

	I +T1=0 Q  ;don't bother if amount is zero

	; update account balances
	S AB=^AB(AX),AB1=$P(AB,%,1),AB2=$P(AB,%,2),AB3=$P(AB,%,3),AB4=$P(AB,%,4,99),AB2=AB2+T1,AB3=AB3+T1,^AB(AX)=AB1_%_AB2_%_AB3_%_AB4

	; update nominal except for statistical accounts
	I $e(AX,1)="N" Q
	S ^AY(AX,YX)=V2_%_T1_%_V3_%_V4_%_V5
	Q

	; Report Heading * * * * * * * * * * * * * * * * * * * *

	; Note: page no = P; line no = N; prog name = PGM; prog title = H2

HEAD	D ^DATE
	I $g(^WRITE($j))="WB" Q:P>1  W "<html><head><title>Virgo Accounts: ",H2,"</title></head><body><pre>"
	I P>1 W #
	W !,PGM,?80-$L(^SZ("PN"))\2,^SZ("PN"),?61,DAT," ",TIM,!?80-$L(H2)\2,H2,?70,"Page ",P,!,$e(PU,1,78),!!! S N=6,P=P+1
	Q

	; Report Ending  * * * * * * * * * * * * * * * * * * * *

END	I %OP=1 d ^Cont(0)
	U %OP W !!,"** End of Report **"
	I $g(^WRITE($j))="WB" W !,"</pre></body></html>" K ^WRITE($j)
	E  W #
	I %OP>1 C %OP
	Q

	; Activity Search  * * * * * * * * * * * * * * * * * * *

	; Note: charge group = RX

SA	d uwo^%video(6,18)
	W #,@P1 S %K=1,S3=""
A01	S S3=$O(^SR(RX,S3)) I S3=""!($Y>13) G A03
A02	S S2=$G(^SA(S3))
	W %K,?10,S3,?20,S2,! S SS(%K)=S3,%K=%K+1 G A01
A03	W /C(0,15),"Select an entry " D ^GETX I X[% G A99
	I X,$G(SS(X))="" G A03
	I 'X G A99:S3="" W # K SS G A02
	S X=SS(X)
A99	K S2,S3,SS
	d uwc^%video()
	Q

	; Unpaid Bills Search  * * * * * * * * * * * * * * * * *

UB	d uwo^%video(6,18)
	W #,@P1 S %K=1,S3=""
B01	S S3=$O(^SU(S3)) I S3=""!($Y>13) G B03
B02	S S2=$G(^SB(S3))
	W %K,?10,S3,?20,$p(S2,%,1),?32,$j($p(S2,%,4)+$p(S2,%,5)+$p(S2,%,6),10,2),?44,$p(^SC($p(S2,%,2)),%,1),! S SS(%K)=S3,%K=%K+1 G B01
B03	W /C(0,15),"Select an entry " D ^GETX I X[% G B99
	I X,$G(SS(X))="" G B03
	I 'X G B99:S3="" W # K SS G B02
	S X=SS(X)
B99	K S2,S3,SS
	d uwc^%video()
	Q

XSY
XSY	;GRI,,,;08:54 AM  12 Feb 1999;
	;=======================================================
	; Virgo Accounts - Enquiries Only Menu
	;
	; Copyright Graham R Irwin 1998
	;
	; Screens:       none
	; Files updated: ^WORK($j) ^WORK2($j) ^WORK3($j)
	; Subroutines:   ^INPASSW, ^XSINIT, ^DATE, ^Cont
	;
	;=======================================================

	; H0 is the Virgo licence number (which indentifies installed
	;	options and type of firm)
	; H1 is the Virgo Accounts descriptor
	; H2 is the menu descriptor
	; P1 to P5 are the screen colours
	; PU is a row of underscores
	; PX is the current sub-menu number
	; DAT is today's date (DD/MM/YYYY format)
	; TIM is the current time
	; CX is the default (or last used) client code
	; MX is the default (or last used) matter code

	W !!,"Virgo Accounts",!!,"Copyright Irwin Associates, 1998-99",!

XS0	D V^XSINIT

XSX1	S $ZT="ERROR^XSY"
	i $$volspace^%dos<10240 w !!,"Insufficient disk space, please refer to your manual",! s X=$$^Cont(0) h

A0	S H2="Enquiries Menu",PX=0 D ^DATE,WRITE
	K (%,P1,P2,P3,P4,P5,PU,PX,H0,H1,H2,PGM,DAT,TIM,CX,MX)
	W /C(0,22),@P5,"Always Exit before switching off the computer"
	W /C(0,21),@P1,"Enter option no. (To Exit enter 0 or press Alt+F4) : ",/el D ^GETX S K=X
	I K=0!(K="****") K ^WORK($j),^WORK2($j),^WORK3($j) H
	i K="" w *7 G A0
	i K[% w *7 G A0
	i K?1.2n G A2
	S H2="",PGM=K I $zrstatus(K)'="" D ^@K G XS0
	D CASE S PGM=K I $zrstatus(K)'="" D ^@K G XS0
A2	S X=^MENU1(PX,K),H2=$p(X,%,1),PGM=$p(X,%,2) D ^@PGM
	G A0

	;Display menu options
WRITE	W @P4,#,$g(^MENU1),?80-$L(H1)\2,H1,?70,DAT,!?80-$L(H2)\2,H2,?75,TIM,!!!
	S PY=""
W1	S PY=$o(^MENU1(PX,PY)) I PY="" Q
	S X=^MENU1(PX,PY),Q=$g(^MENU1(PX,PY,"IF")) I Q'="",@Q G W1
	W ?10,@P5,PY,?15,@P1,$p(X,%,1),!
	G W1

CASE	S K=$zconvert(K,"U") Q

	;Error trap
ERROR	U 0 W @P5,*7,!,"*** An error has occurred during processing, please take a note of",!,"the following message and contact the Support Desk",!,$ZE,!,$ZERR," ",$ZENAME d ^%errlog
	R !!,"Press ENTER when ready",X G XSX1

